#ifdef pp_RLETEST
MODULE RLE

IMPLICIT NONE

PUBLIC RLECOMPRESS

CONTAINS

INTEGER FUNCTION RLECOMPRESS(QIN, NQIN, QMIN, QMAX, COUT)

!  COMPRESS THE ARRAY QIN(1:NQIN) BY 1)  CONVERTING QIN TO ONE BYTE INTEGERS AND
!  2)  USING RUN-LENGTH ENCODING TO CONVERT RUNS OF IDENTICAL INTEGERS IIIIIIII 
!  TO  #In WHERE #=255 IS A MARKER CHARACTER TO DISTINQUISH BETWEEN LITTERAL CHARACTERS
!  AND RUNS AND n IS THE NUMBER OF REPEATS

INTEGER, INTENT(IN) :: NQIN
REAL, INTENT(IN), DIMENSION(NQIN) :: QIN
REAL, INTENT(IN) :: QMIN, QMAX
CHARACTER(1), INTENT(OUT), DIMENSION(NQIN) :: COUT

CHARACTER(1), PARAMETER :: MARK=CHAR(255)
CHARACTER(1) :: THISCHAR, LASTCHAR
INTEGER :: IIN, IOUT, NREPEATS, IQVAL

! CHECK INPUTS

IF(QMAX.LE.QMIN.OR.NQIN.LE.0)THEN
  RLECOMPRESS = 0
  RETURN
ENDIF    

IOUT=1
LASTCHAR=MARK
DO IIN = 1, NQIN 
  IQVAL = 254*(QIN(IIN) - QMIN)/(QMAX-QMIN)
  IF(IQVAL.LT.0)IQVAL=0
  IF(IQVAL.GT.254)IQVAL=254
  THISCHAR =CHAR(IQVAL)

  IF(THISCHAR.EQ.LASTCHAR)THEN
    NREPEATS = NREPEATS + 1
   ELSE
    NREPEATS = 1
  ENDIF
  IF(NREPEATS.GE.1.AND.NREPEATS.LE.3)THEN
    COUT(IOUT)=THISCHAR
    LASTCHAR=THISCHAR
  ELSE IF(NREPEATS.GE.4)THEN
    IF(NREPEATS.EQ.4)THEN
      IOUT = IOUT - 3
      COUT(IOUT) = MARK
      IOUT = IOUT + 1
      COUT(IOUT) = THISCHAR
      IOUT = IOUT + 1
    ENDIF
    IF(NREPEATS.NE.4)IOUT = IOUT - 1
    COUT(IOUT) = CHAR(NREPEATS)
    IF(NREPEATS==254)THEN
      NREPEATS=1
      LASTCHAR=MARK
    ENDIF
  ENDIF
  IOUT = IOUT + 1
END DO

RLECOMPRESS = IOUT - 1

END FUNCTION RLECOMPRESS

END MODULE RLE
#else
MODULE RLE
END MODULE RLE
#endif
