MODULE VEGE
 
USE PRECISION_PARAMETERS
USE GLOBAL_CONSTANTS
USE MESH_POINTERS
USE TRAN
USE PART
USE MEMORY_FUNCTIONS, ONLY:CHKMEMERR
USE TYPES, ONLY: DROPLET_TYPE, PARTICLE_CLASS_TYPE, PARTICLE_CLASS, WALL_TYPE
IMPLICIT NONE
PRIVATE
PUBLIC INITIALIZE_RAISED_VEG, RAISED_VEG_MASS_ENERGY_TRANSFER, GET_REV_vege
TYPE (DROPLET_TYPE), POINTER :: DR
TYPE (PARTICLE_CLASS_TYPE), POINTER :: PC
CHARACTER(255), PARAMETER :: vegeid='$Id$'
CHARACTER(255), PARAMETER :: vegerev='$Revision$'
CHARACTER(255), PARAMETER :: vegedate='$Date$'
LOGICAL, ALLOCATABLE, DIMENSION(:,:,:) :: VEG_PRESENT_FLAG,CELL_TAKEN_FLAG
INTEGER :: IZERO,NLP_VEG_FUEL,NCONE_TREE,NXB,NYB
REAL(EB) :: RCELL,R_TREE,XCELL,XI,YJ,YCELL,ZCELL,ZK
 
CONTAINS
 

SUBROUTINE INITIALIZE_RAISED_VEG(NM)

USE MEMORY_FUNCTIONS, ONLY: RE_ALLOCATE_DROPLETS
REAL(EB) CROWN_LENGTH,CROWN_VOLUME,TANGENT,CROWN_WIDTH,CROWN_WIDTH_BOTTOM
REAL(EB) DX_RING,DZ_RING,INNER_RADIUS,OUTER_RADIUS,R_CTR_CYL,  &
         RING_BOTTOM,RING_TOP,SLANT_WIDTH
REAL(EB) V_CELL,XLOC,YLOC,ZLOC
INTEGER NCT,NLP_TREE,NLP_RECT_VEG,N_TREE,NXB,NYB,NZB,IPC
INTEGER I,II,I_OUTER_RING,JJ,KK,K_BOTTOM_RING
INTEGER, INTENT(IN) :: NM

!IF (.NOT. TREE) RETURN !Exit if there are no trees anywhere
IF (.NOT. TREE_MESH(NM)) RETURN !Exit routine if no raised veg in mesh
IF (EVACUATION_ONLY(NM)) RETURN  ! Don't waste time if an evac mesh
CALL POINT_TO_MESH(NM)

ALLOCATE(VEG_PRESENT_FLAG(0:IBP1,0:JBP1,0:KBP1))
CALL ChkMemErr('VEGE','VEG_PRESENT_FLAG',IZERO)
ALLOCATE(CELL_TAKEN_FLAG(0:IBP1,0:JBP1,0:KBP1))
CALL ChkMemErr('VEGE','CELL_TAKEN_FLAG',IZERO)

!Diagnostic files
!IF (NM == NMESHES) THEN
!OPEN(9999,FILE='total_droplet_mass.out',STATUS='REPLACE')
! OPEN(9998,FILE='diagnostics.out',STATUS='REPLACE')
!ENDIF

TREE_MESH(NM) = .FALSE. 

TREE_LOOP: DO NCT=1,N_TREES

   VEG_PRESENT_FLAG = .FALSE. ; CELL_TAKEN_FLAG = .FALSE.
   IPC = TREE_PARTICLE_CLASS(NCT)
   PC=>PARTICLE_CLASS(IPC)
   PC%KILL_RADIUS = 0.5_EB/PC%VEG_SV !radius bound below which fuel elements are removed
! 
! Build a conical volume of solid (vegetation) fuel
!
   IF_CONE_VEGETATION: IF(VEG_FUEL_GEOM(NCT) == 'CONE') THEN
!
   CROWN_WIDTH  = CROWN_W(NCT)
   CROWN_LENGTH = TREE_H(NCT) - CROWN_B_H(NCT)
   TANGENT = 0.5_EB*CROWN_W(NCT)/CROWN_LENGTH
   CROWN_VOLUME = PI*CROWN_WIDTH**2*CROWN_LENGTH/12._EB
 
   NLP_TREE = 0

   DO NZB=1,KBAR
     IF (Z(NZB).GE.Z_TREE(NCT)+CROWN_B_H(NCT) .AND. Z(NZB).LE.Z_TREE(NCT)+TREE_H(NCT)) THEN
      PARTICLE_TAG = PARTICLE_TAG + NMESHES
!      R_TREE = TANGENT*(TREE_H(NCT)+Z_TREE(NCT)-Z(NZB)+0.5_EB*DZ(NZB))
      R_TREE = TANGENT*(TREE_H(NCT)+Z_TREE(NCT)-Z(NZB))
      DO NXB = 1,IBAR
       DO NYB = 1,JBAR
        RCELL = SQRT((X(NXB)-X_TREE(NCT))**2 + (Y(NYB)-Y_TREE(NCT))**2)
        IF (RCELL .LE. R_TREE) THEN
         NLP  = NLP + 1
         NLP_TREE = NLP_TREE + 1
         IF (NLP.GT.NLPDIM) THEN
          CALL RE_ALLOCATE_DROPLETS(1,NM,0,1000)
          DROPLET=>MESHES(NM)%DROPLET
         ENDIF
         DR=>DROPLET(NLP)
         DR%TAG = PARTICLE_TAG
         DR%X = REAL(NXB,EB)
         DR%Y = REAL(NYB,EB)
         DR%Z = REAL(NZB,EB)
         DR%CLASS = IPC
         VEG_PRESENT_FLAG(NXB,NYB,NZB) = .TRUE.
        ENDIF
       ENDDO   
      ENDDO 
     ENDIF
   ENDDO
   NLP_VEG_FUEL = NLP_TREE
!
   ENDIF IF_CONE_VEGETATION
! 
! Build a frustum volume of solid (vegetation) fuel
!
   IF_FRUSTUM_VEGETATION: IF(VEG_FUEL_GEOM(NCT) == 'FRUSTUM') THEN
!
   CROWN_WIDTH_BOTTOM  = CROWN_W(NCT)
   CROWN_LENGTH = TREE_H(NCT) - CROWN_B_H(NCT)
   R_CTR_CYL    = 0.5*MIN(CROWN_W_TOP(NCT),CROWN_W_BOTTOM(NCT))
   SLANT_WIDTH  = 0.5*ABS(CROWN_W_TOP(NCT) - CROWN_W_BOTTOM(NCT))

   TANGENT = SLANT_WIDTH/CROWN_LENGTH
   CROWN_VOLUME = PI*CROWN_LENGTH*(CROWN_W_BOTTOM(NCT)**2 + CROWN_W_TOP(NCT)*CROWN_W_TOP(NCT) + &
                   CROWN_W_TOP(NCT)**2)/3._EB
 
   NLP_TREE = 0

   DO NZB=1,KBAR
     IF (Z(NZB).GE.Z_TREE(NCT)+CROWN_B_H(NCT) .AND. Z(NZB).LE.Z_TREE(NCT)+TREE_H(NCT)) THEN
      PARTICLE_TAG = PARTICLE_TAG + NMESHES
      R_TREE = R_CTR_CYL + TANGENT*(TREE_H(NCT)+Z_TREE(NCT)-Z(NZB))
      DO NXB = 1,IBAR
       DO NYB = 1,JBAR
        RCELL = SQRT((X(NXB)-X_TREE(NCT))**2 + (Y(NYB)-Y_TREE(NCT))**2)
        IF (RCELL .LE. R_TREE) THEN
         NLP  = NLP + 1
         NLP_TREE = NLP_TREE + 1
         IF (NLP.GT.NLPDIM) THEN
          CALL RE_ALLOCATE_DROPLETS(1,NM,0,1000)
          DROPLET=>MESHES(NM)%DROPLET
         ENDIF
         DR=>DROPLET(NLP)
         DR%TAG = PARTICLE_TAG
         DR%X = REAL(NXB,EB)
         DR%Y = REAL(NYB,EB)
         DR%Z = REAL(NZB,EB)
         DR%CLASS = IPC
         VEG_PRESENT_FLAG(NXB,NYB,NZB) = .TRUE.
        ENDIF
       ENDDO   
      ENDDO 
     ENDIF
   ENDDO
   NLP_VEG_FUEL = NLP_TREE
!
   ENDIF IF_FRUSTUM_VEGETATION
!
! Build a cylindrical volume of vegetative fuel
!
   IF_CYLINDRICAL_VEGETATION: IF (VEG_FUEL_GEOM(NCT) .EQ. 'CYLINDER') THEN
!
   CROWN_WIDTH = CROWN_W(NCT)
   R_TREE  = 0.5*CROWN_WIDTH
   CROWN_LENGTH = TREE_H(NCT) - CROWN_B_H(NCT)
   CROWN_VOLUME = 0.25*PI*CROWN_WIDTH**2*CROWN_LENGTH
   NLP_TREE = 0

   DO NZB=1,KBAR
     IF (Z(NZB).GE.Z_TREE(NCT)+CROWN_B_H(NCT) .AND. Z(NZB).LE.Z_TREE(NCT)+TREE_H(NCT)) THEN
      PARTICLE_TAG = PARTICLE_TAG + NMESHES
      DO NXB = 1,IBAR
       DO NYB = 1,JBAR
        RCELL = SQRT((X(NXB)-X_TREE(NCT))**2 + (Y(NYB)-Y_TREE(NCT))**2)
        IF (RCELL .LE. R_TREE) THEN
         NLP  = NLP + 1
         NLP_TREE = NLP_TREE + 1
         IF (NLP.GT.NLPDIM) THEN
          CALL RE_ALLOCATE_DROPLETS(1,NM,0,1000)
          DROPLET=>MESHES(NM)%DROPLET
         ENDIF
         DR=>DROPLET(NLP)
         DR%TAG = PARTICLE_TAG
         DR%X = REAL(NXB,EB)
         DR%Y = REAL(NYB,EB)
         DR%Z = REAL(NZB,EB)
         DR%CLASS = IPC
         VEG_PRESENT_FLAG(NXB,NYB,NZB) = .TRUE.
        ENDIF
       ENDDO   
      ENDDO 
     ENDIF
   ENDDO
   NLP_VEG_FUEL = NLP_TREE
!
   ENDIF IF_CYLINDRICAL_VEGETATION
!
! Build a rectangular volume containing vegetation
!
   IF_RECTANGULAR_VEGETATION:IF (VEG_FUEL_GEOM(NCT) .EQ. 'RECTANGLE')THEN
       NLP_RECT_VEG = 0
       DO NZB=0,KBAR-1
        ZLOC = Z(NZB) + 0.5_EB*DZ(NZB)
        IF (ZLOC.GE.ZS_RECT_VEG(NCT) .AND. ZLOC.LE.ZF_RECT_VEG(NCT)) THEN
         DO NXB = 0,IBAR-1
          XLOC = X(NXB) + 0.5_EB*DX(NXB)
          IF (XLOC .GE. XS_RECT_VEG(NCT) .AND. XLOC .LE. XF_RECT_VEG(NCT)) THEN
           DO NYB = 0,JBAR-1
            YLOC = Y(NYB) + 0.5_EB*DY(NYB)
            IF (YLOC .GE. YS_RECT_VEG(NCT) .AND. YLOC .LE. YF_RECT_VEG(NCT)) THEN
             NLP  = NLP + 1
             NLP_RECT_VEG = NLP_RECT_VEG + 1
             IF (NLP.GT.NLPDIM) THEN
              CALL RE_ALLOCATE_DROPLETS(1,NM,0,1000)
              DROPLET=>MESHES(NM)%DROPLET
             ENDIF
             DR=>DROPLET(NLP)
             DR%TAG = PARTICLE_TAG
             DR%X = REAL(NXB,EB)
             DR%Y = REAL(NYB,EB)
             DR%Z = REAL(NZB,EB)
             DR%CLASS = IPC
             VEG_PRESENT_FLAG(NXB,NYB,NZB) = .TRUE.
            ENDIF
           ENDDO   
          ENDIF
         ENDDO 
        ENDIF
       ENDDO
       NLP_VEG_FUEL = NLP_RECT_VEG
      ENDIF IF_RECTANGULAR_VEGETATION
!
! Build a ring volume of vegetation fuel
!
   IF_RING_VEGETATION_BUILD: IF (VEG_FUEL_GEOM(NCT) .EQ. 'RING') THEN
   OUTER_RADIUS = 0.5_EB*CROWN_W(NCT)
   RING_BOTTOM  = Z_TREE(NCT) + CROWN_B_H(NCT)
   RING_TOP     = Z_TREE(NCT) + TREE_H(NCT)
!  print*,'--------- NM = ',nm
!  print*,outer_radius
   DO II=1,IBAR-1
    IF(X(II) .LE. OUTER_RADIUS .AND. X(II+1) .GT. OUTER_RADIUS) I_OUTER_RING = II
   ENDDO
!  print*,i_outer_ring,nct
!  print*,dx(i_outer_ring),ring_thickness_veg(nct)
!  DX_RING = MAX(DX(I_OUTER_RING),RING_THICKNESS_VEG(NCT))
   DX_RING = DX(1)
   INNER_RADIUS = OUTER_RADIUS - DX_RING
   DO KK=1,KBAR-1
    IF(Z(KK) .LE. RING_BOTTOM .AND. Z(KK+1) .GT. RING_BOTTOM) K_BOTTOM_RING = KK
   ENDDO
   DZ_RING  = MAX(DZ(K_BOTTOM_RING),RING_TOP-RING_BOTTOM)
   RING_TOP = RING_BOTTOM + DZ_RING
   NLP_TREE = 0
!
   DO NZB=1,KBAR
     IF (Z(NZB).GE.RING_BOTTOM .AND. Z(NZB).LE.RING_TOP) THEN
      PARTICLE_TAG = PARTICLE_TAG + NMESHES
      DO NXB = 1,IBAR
       DO NYB = 1,JBAR
        RCELL = SQRT((X(NXB)-X_TREE(NCT))**2 + (Y(NYB)-Y_TREE(NCT))**2)
        IF (RCELL .LE. OUTER_RADIUS .AND. RCELL .GE. INNER_RADIUS) THEN
         NLP  = NLP + 1
         NLP_TREE = NLP_TREE + 1
         IF (NLP.GT.NLPDIM) THEN
          CALL RE_ALLOCATE_DROPLETS(1,NM,0,1000)
          DROPLET=>MESHES(NM)%DROPLET
         ENDIF
         DR=>DROPLET(NLP)
         DR%TAG = PARTICLE_TAG
         DR%X = REAL(NXB,EB)
         DR%Y = REAL(NYB,EB)
         DR%Z = REAL(NZB,EB)
         DR%CLASS = IPC
         VEG_PRESENT_FLAG(NXB,NYB,NZB) = .TRUE.
        ENDIF
       ENDDO   
      ENDDO 
     ENDIF
   ENDDO
   NLP_VEG_FUEL = NLP_TREE
   ENDIF IF_RING_VEGETATION_BUILD
!
! For the current vegetation type (particle class) assign one fuel 
! element (droplet) to each grid cell and initialize droplet properties
! (this is precautionary needs more tested to determine its necessity)
!
   REP_VEG_ELEMS: DO I=NLP-NLP_VEG_FUEL+1,NLP
    DR=>DROPLET(I)
    DR%IGNITOR = .FALSE.
    DO NZB=0,KBAR
     DO NXB=0,IBAR
      GRID_LOOP: DO NYB=0,JBAR
       IF (.NOT. VEG_PRESENT_FLAG(NXB,NYB,NZB)) CYCLE GRID_LOOP
       IF (REAL(NXB,EB).EQ.DR%X .AND. REAL(NYB,EB).EQ.DR%Y .AND. REAL(NZB,EB).EQ.DR%Z) THEN 
        IF(CELL_TAKEN_FLAG(NXB,NYB,NZB)) THEN
         DR%R = 0.0001_EB*PC%KILL_RADIUS
         CYCLE REP_VEG_ELEMS
        ENDIF
        CELL_TAKEN_FLAG(NXB,NYB,NZB) = .TRUE.
        DR%X = X(NXB) - 0.5_EB*DX(NXB)
        DR%Y = Y(NYB) - 0.5_EB*DY(NYB)
        DR%Z = Z(NZB) - 0.5_EB*DZ(NZB)
        IF (VEG_FUEL_GEOM(NCT) .EQ. 'RECTANGLE')THEN
         DR%X = X(NXB) + 0.5_EB*DX(NXB)
         DR%Y = Y(NYB) + 0.5_EB*DY(NYB)
         DR%Z = Z(NZB) + 0.5_EB*DZ(NZB)
        ENDIF
        TREE_MESH(NM) = .TRUE.
        DR%SHOW = .TRUE.
        DR%T   = 0.
        DR%U = 0.
        DR%V = 0.
        DR%W = 0.
        DR%R =  2./PC%VEG_SV !cylinder, Porterie
        DR%IOR = 0
        DR%VEG_FUEL_MASS = PC%VEG_BULK_DENSITY
        DR%VEG_MOIST_MASS = PC%VEG_MOISTURE*DR%VEG_FUEL_MASS
        DR%VEG_PACKING_RATIO = PC%VEG_BULK_DENSITY/PC%VEG_DENSITY 
        DR%VEG_KAPPA = 0.25*PC%VEG_SV*PC%VEG_BULK_DENSITY/PC%VEG_DENSITY
        DR%TMP = PC%VEG_INITIAL_TEMPERATURE
        IF(IGN_ELEM(NCT)) THEN
          DR%TMP = TMPA
          DR%IGNITOR = .TRUE.
          DR%VEG_IGN_TON      = TON_IGN_ELEMS(NCT)
          DR%VEG_IGN_TOFF     = TOFF_IGN_ELEMS(NCT)
          DR%VEG_IGN_TRAMPON  = T_RAMPON_IGN_ELEMS(NCT)
          DR%VEG_IGN_TRAMPOFF = T_RAMPOFF_IGN_ELEMS(NCT)
        ENDIF
        DR%VEG_EMISS = 4.*SIGMA*DR%VEG_KAPPA*DR%TMP**4
        DR%VEG_DIVQR = 0.0_EB
        DR%VEG_N_TREE_OUTPUT = 0
!       TREE_MESH_OUT(NM) = .FALSE.
        IF (N_TREE_OUT(NCT) .NE. 0) THEN
!print*,'in vege 1'
!print*,'in vege 1, NCT, N_TREE_OUT(NCT)',nct,n_tree_out(nct)
         DR%VEG_N_TREE_OUTPUT = N_TREE_OUT(NCT)
!        TREE_MESH_OUT(NM) = .TRUE.
        ENDIF
        CYCLE REP_VEG_ELEMS
       ENDIF
      ENDDO GRID_LOOP
     ENDDO
    ENDDO
   ENDDO REP_VEG_ELEMS
!
!print*,'in vege 2: NM,NCT,N_TREE_OUT(NCT)', NM,NCT,N_TREE_OUT(NCT)
!print*,'in vege 2: NLP,NM,TREE_MESH_OUT(NM),NCT',NLP,NM,TREE_MESH_OUT(NM),NCT
ENDDO TREE_LOOP

CALL REMOVE_DROPLETS(0._EB,NM)
!print*,'in vege3: NLP',nlp

DEALLOCATE(VEG_PRESENT_FLAG)
DEALLOCATE(CELL_TAKEN_FLAG)

!Fill veg output arrays with initial values
IF (N_TREES_OUT == 0) RETURN
CALL POINT_TO_MESH(NM)
TREE_OUTPUT_DATA(:,:,NM) = 0._EB
DROPLET_LOOP: DO I=1,NLP
 DR=>DROPLET(I)
 N_TREE = DR%VEG_N_TREE_OUTPUT
 IF (N_TREE /= 0) THEN
   XI = CELLSI(FLOOR((DR%X-XS)*RDXINT))
   YJ = CELLSJ(FLOOR((DR%Y-YS)*RDYINT))
   ZK = CELLSK(FLOOR((DR%Z-ZS)*RDZINT))
   II  = FLOOR(XI+1._EB)
   JJ  = FLOOR(YJ+1._EB)
   KK  = FLOOR(ZK+1._EB)
   V_CELL = DX(II)*DY(JJ)*DZ(KK)
   TREE_OUTPUT_DATA(N_TREE,1,NM) = TREE_OUTPUT_DATA(N_TREE,1,NM) + DR%VEG_FUEL_MASS*V_CELL !kg
   TREE_OUTPUT_DATA(N_TREE,2,NM) = TREE_OUTPUT_DATA(N_TREE,2,NM) + DR%VEG_MOIST_MASS*V_CELL !kg
   TREE_OUTPUT_DATA(N_TREE,3,NM) = TREE_OUTPUT_DATA(N_TREE,3,NM) + DR%VEG_DIVQC*V_CELL*0.001_EB !kW
   TREE_OUTPUT_DATA(N_TREE,4,NM) = TREE_OUTPUT_DATA(N_TREE,4,NM) + DR%VEG_DIVQR*V_CELL*0.001_EB !kW
 ENDIF
ENDDO DROPLET_LOOP


END SUBROUTINE INITIALIZE_RAISED_VEG


SUBROUTINE RAISED_VEG_MASS_ENERGY_TRANSFER(T,NM)
    
! Mass and energy transfer between gas and raised vegetation fuel elements 

USE PHYSICAL_FUNCTIONS, ONLY : GET_MASS_FRACTION
USE MATH_FUNCTIONS, ONLY : AFILL2

!arrays for debugging
REAL(EB), POINTER, DIMENSION(:,:,:) :: HOLD1,HOLD2,HOLD3,HOLD4
REAL(EB), POINTER, DIMENSION(:,:,:) :: UU,VV,WW !,RHOP

REAL(EB) :: RE_D
REAL(EB) :: RDT,T,V_CELL,V_VEG
REAL(EB) :: CP_H2O,H_VAP_H2O,TMP_H2O_BOIL
REAL(EB) :: K_AIR,MASS_GAS,MU_AIR,RHO_GAS,RRHO_GAS_NEW,TMP_GAS,UBAR,VBAR,WBAR,UREL,VREL,WREL
REAL(EB) :: CHAR_FCTR,CP_VEG,DTMP_VEG,MPV_MOIST,MPV_MOIST_MIN,MPV_VEG,MPV_VEG_MIN, &
            SV_VEG,TMP_VEG,TMP_VEG_NEW
REAL(EB) :: TMP_IGNITOR
REAL(EB) :: MPV_ADDED,MPV_MOIST_LOSS,MPV_VOLIT,MPV_MOIST_LOSS_MAX,MPV_VOLIT_MAX
REAL(EB) :: QCON_VEG,QNET_VEG,QRAD_VEG,QREL,TMP_GMV,Q_FOR_DRYING,Q_VOLIT,Q_FOR_VOLIT, &
            Q_UPTO_VOLIT
REAL(EB) :: H_SENS_VEG_VOLIT,MW_TERM,Q_ENTHALPY,Q_VEG_MOIST,Q_VEG_VOLIT
REAL(EB) :: MW_AVERAGE,MW_VEG_MOIST_TERM,MW_VEG_VOLIT_TERM
REAL(EB) :: XI,YJ,ZK
REAL(EB) :: A_H2O_VEG,E_H2O_VEG,A_PYR_VEG,E_PYR_VEG,L_PYR_VEG
INTEGER :: I,II,JJ,KK,IIX,JJY,KKZ,IPC,N_TREE
INTEGER, INTENT(IN) :: NM
LOGICAL :: VEG_DEGRADATION_1,VEG_DEGRADATION_2

!place holder
REAL(EB) :: RCP_TEMPORARY

!Debug
REAL(EB)TOTAL_BULKDENS_MOIST,TOTAL_BULKDENS_DRY_FUEL,TOTAL_MASS_DRY_FUEL,TOTAL_MASS_MOIST


!IF (.NOT. TREE) RETURN !Exit if no raised veg anywhere
IF (.NOT. TREE_MESH(NM)) RETURN !Exit if raised veg is not present in mesh
CALL POINT_TO_MESH(NM)

!IF (PREDICTOR) THEN
    UU => U
    VV => V
    WW => W
!   RHOP => RHO
!ELSE
!   UU => US
!   VV => VS
!   WW => WS
!   RHOP => RHOS
!ENDIF

! Initializations

RDT    = 1._EB/DT
!RCP_TEMPORARY = 1._EB/CP_GAMMA
RCP_TEMPORARY = 1._EB/1010._EB

! Empirical coefficients
CP_H2O       = 4190._EB !J/kg/K specific heat of water
TMP_H2O_BOIL = 373.15_EB
H_VAP_H2O    = 2259._EB*1000._EB !J/kg/K heat of vaporization of water

!D_AIR  = 2.6E-5_EB  ! Water Vapor - Air binary diffusion (m2/s at 25 C, Incropera & DeWitt, Table A.8) 
!SC_AIR = 0.6_EB     ! NU_AIR/D_AIR (Incropera & DeWitt, Chap 7, External Flow)
!PR_AIR = 0.7_EB     

! Working arrays
D_VAP            = 0._EB
TREE_OUTPUT_DATA(:,:,NM) = 0._EB !for output of veg data
!DMPVDT_FM_VEG  = 0.0_EB

!Clear arrays and scalars
HOLD1 => WORK4 ; WORK4 = 0._EB
HOLD2 => WORK5 ; WORK5 = 0._EB
HOLD3 => WORK6 ; WORK6 = 0._EB
HOLD4 => WORK7 ; WORK7 = 0._EB
TOTAL_BULKDENS_MOIST    = 0.0_EB
TOTAL_BULKDENS_DRY_FUEL = 0.0_EB
TOTAL_MASS_MOIST    = 0.0_EB
TOTAL_MASS_DRY_FUEL = 0.0_EB
V_VEG               = 0.0_EB

!print*,'vege h-m transfer: NM, NLP',nm,nlp

DROPLET_LOOP: DO I=1,NLP

 DR => DROPLET(I)
 IPC = DR%CLASS
 PC=>PARTICLE_CLASS(IPC)
 IF (.NOT. PC%TREE) CYCLE DROPLET_LOOP !Ensure grid cell has vegetation
 IF (PC%MASSLESS) CYCLE DROPLET_LOOP   !Skip droplet if massless
 IF (PC%VEG_STEM) CYCLE DROPLET_LOOP   !Skip droplet if veg is a tree stem

! Intialize quantities
 Q_VEG_MOIST    = 0.0_EB
 Q_VEG_VOLIT    = 0.0_EB
 Q_UPTO_VOLIT   = 0.0_EB
 Q_VOLIT        = 0.0_EB
 MPV_MOIST_LOSS = 0.0_EB
 MPV_VOLIT      = 0.0_EB
 MW_VEG_MOIST_TERM = 0.0_EB
 MW_VEG_VOLIT_TERM = 0.0_EB

! Vegetation variables
 CHAR_FCTR = 1._EB - PC%VEG_CHAR_FRACTION
 SV_VEG  = PC%VEG_SV !surface-to-volume ration 1/m
 TMP_VEG = DR%TMP
 MPV_VEG = DR%VEG_FUEL_MASS !bulk density of dry veg
 MPV_MOIST     = DR%VEG_MOIST_MASS !bulk density of moisture in veg
 MPV_VEG_MIN   = PC%VEG_FUEL_MPV_MIN
 MPV_MOIST_MIN = PC%VEG_MOIST_MPV_MIN
 MPV_MOIST_LOSS_MAX = PC%VEG_DEHYDRATION_RATE_MAX*DT
 MPV_VOLIT_MAX      = PC%VEG_BURNING_RATE_MAX*DT

! Determine grid cell quantities of the vegetation fuel element
 XI = CELLSI(FLOOR((DR%X-XS)*RDXINT))
 YJ = CELLSJ(FLOOR((DR%Y-YS)*RDYINT))
 ZK = CELLSK(FLOOR((DR%Z-ZS)*RDZINT))
 II  = FLOOR(XI+1._EB)
 JJ  = FLOOR(YJ+1._EB)
 KK  = FLOOR(ZK+1._EB)
 IIX = FLOOR(XI+0.5_EB)
 JJY = FLOOR(YJ+0.5_EB)
 KKZ = FLOOR(ZK+0.5_EB)
 V_CELL = DX(II)*DY(JJ)*DZ(KK)

! Gas velocities in vegetation grid cell
 UBAR = AFILL2(UU,II-1,JJY,KKZ,XI-II+1,YJ-JJY+.5_EB,ZK-KKZ+.5_EB)
 VBAR = AFILL2(VV,IIX,JJ-1,KKZ,XI-IIX+.5_EB,YJ-JJ+1,ZK-KKZ+.5_EB)
 WBAR = AFILL2(WW,IIX,JJY,KK-1,XI-IIX+.5_EB,YJ-JJY+.5_EB,ZK-KK+1)
 UREL = DR%U - UBAR
 VREL = DR%V - VBAR
 WREL = DR%W - WBAR
 QREL = MAX(1.E-6_EB,SQRT(UREL*UREL + VREL*VREL + WREL*WREL))

! Other gas quantities
 TMP_GAS  = TMP(II,JJ,KK)
 RHO_GAS  = RHO(II,JJ,KK)
 MASS_GAS = RHO_GAS*V_CELL
 MU_AIR   = Y2MU_C(MIN(5000,NINT(TMP_GAS)))*SPECIES(0)%MW
 K_AIR    = CPOPR*MU_AIR !W/m.K
 TMP_GMV  = TMP_GAS - TMP_VEG
 CP_VEG   = (0.1 + 0.0037*TMP_VEG)*1000 !J/kg/K Ritchie IAFSS 1997:177-188

! Divergence of convective and radiative heat fluxes
!print*,'---- NM=',NM
!print*,rho_gas,qrel,sv_veg,mu_air
 RE_D     = RHO_GAS*QREL*2./(SV_VEG*MU_AIR)
 QCON_VEG = SV_VEG*(0.5*K_AIR*0.683*RE_D**0.466)*0.5*TMP_GMV !W/m^2
 QCON_VEG = SV_VEG*DR%VEG_PACKING_RATIO*QCON_VEG !W/m^3
 DR%VEG_DIVQC = QCON_VEG
 QRAD_VEG = DR%VEG_DIVQR

! Divergence of net heat flux
 QNET_VEG = QCON_VEG + QRAD_VEG !W/m^2

! Update temperature of vegetation
 DTMP_VEG    = DT*QNET_VEG/(CP_VEG*MPV_VEG + CP_H2O*MPV_MOIST)
 TMP_VEG_NEW = TMP_VEG + DTMP_VEG

! Set temperature of inert ignitor elements
 IF(DR%IGNITOR) THEN
  TMP_IGNITOR = PC%VEG_INITIAL_TEMPERATURE
  TMP_VEG_NEW = TMP_IGNITOR
  IF(T.GE.DR%VEG_IGN_TON .AND. T.LE.DR%VEG_IGN_TON+DR%VEG_IGN_TRAMPON) THEN
    TMP_VEG_NEW = &
      TMPA + (TMP_IGNITOR-TMPA)*(T-DR%VEG_IGN_TON)/DR%VEG_IGN_TRAMPON
  ENDIF  
  IF(T.GE.DR%VEG_IGN_TOFF .AND. T.LE.DR%VEG_IGN_TOFF+DR%VEG_IGN_TRAMPOFF)THEN 
    TMP_VEG_NEW = &
      TMP_IGNITOR - (TMP_IGNITOR-TMP_GAS)*(T-DR%VEG_IGN_TOFF)/DR%VEG_IGN_TRAMPOFF
  ENDIF
  IF(T .GT. DR%VEG_IGN_TOFF+DR%VEG_IGN_TRAMPOFF) THEN
   DR%R = 0.0001_EB*PC%KILL_RADIUS !remove ignitor element
   TMP_VEG_NEW = TMP_GAS
  ENDIF
 ENDIF

 VEG_DEGRADATION_2 = .FALSE. 
 VEG_DEGRADATION_1 = .TRUE. 

!      ************** Non-Arrehnius Degradation model *************************
! Drying occurs if qnet > 0 with Tveg held at 100 c
! Pyrolysis occurs according to Morvan & Dupuy empirical formula. Linear
! temperature dependence with qnet factor
!
 IF_VEG_DEGRADATION_1: IF(VEG_DEGRADATION_1) THEN
 IF_NET_HEAT_INFLUX: IF (QNET_VEG > 0.0_EB .AND. .NOT. DR%IGNITOR) THEN !dehydrate or pyrolyze 

! Drying of vegetation 
   IF_DEHYDRATION: IF (MPV_MOIST > MPV_MOIST_MIN .AND. TMP_VEG_NEW > TMP_H2O_BOIL) THEN
     Q_FOR_DRYING   = (TMP_VEG_NEW - TMP_H2O_BOIL)/DTMP_VEG * QNET_VEG
     MPV_MOIST_LOSS = MIN(DT*Q_FOR_DRYING/H_VAP_H2O,MPV_MOIST-MPV_MOIST_MIN)
     MPV_MOIST_LOSS = MIN(MPV_MOIST_LOSS,MPV_MOIST_LOSS_MAX) !use specified max
     TMP_VEG_NEW       = TMP_H2O_BOIL
     DR%VEG_MOIST_MASS = MPV_MOIST - MPV_MOIST_LOSS !kg/m^3
     IF (DR%VEG_MOIST_MASS <= MPV_MOIST_MIN) DR%VEG_MOIST_MASS = 0.0_EB
     Q_VEG_MOIST       = MPV_MOIST_LOSS*CP_H2O*(TMP_VEG_NEW - TMPA)
     MW_VEG_MOIST_TERM = MPV_MOIST_LOSS/MW_H2O
!    IF (I == 1) print*,MPV_MOIST,MPV_MOIST_LOSS
   ENDIF IF_DEHYDRATION

! Volitalization of vegetation
  IF_VOLITALIZATION: IF(MPV_MOIST <= MPV_MOIST_MIN) THEN

    IF_MD_VOLIT: IF(MPV_VEG > MPV_VEG_MIN .AND. TMP_VEG_NEW >= 400._EB) THEN !Morvan & Dupuy volitalization
     Q_UPTO_VOLIT = MAX(CP_VEG*MPV_VEG*(400._EB-TMP_VEG),0._EB)
     Q_FOR_VOLIT  = DT*QNET_VEG - Q_UPTO_VOLIT
     Q_VOLIT      = CHAR_FCTR*Q_FOR_VOLIT*0.01_EB*(TMP_VEG-400._EB)
     MPV_VOLIT    = Q_VOLIT*0.00000239_EB
     MPV_VOLIT    = MAX(MPV_VOLIT,0._EB)
     MPV_VOLIT    = MIN(MPV_VOLIT,MPV_VOLIT_MAX) !user specified max
     MPV_VOLIT    = MIN(MPV_VOLIT,(MPV_VEG-MPV_VEG_MIN))
     MPV_VEG      = MPV_VEG - MPV_VOLIT
     Q_VOLIT      = MPV_VOLIT*418000._EB
     TMP_VEG_NEW  = TMP_VEG + (Q_FOR_VOLIT-Q_VOLIT)/(MPV_VEG*CP_VEG)
     TMP_VEG_NEW  = MIN(TMP_VEG_NEW,500._EB) !set to high vap temp if too hot
     DR%VEG_FUEL_MASS = MPV_VEG
!Handle veg. fuel elements if element mass <= prescribed char mass
     IF (MPV_VEG <= MPV_VEG_MIN) THEN
       IF(PC%VEG_REMOVE_CHARRED) DR%R = 0.0001_EB*PC%KILL_RADIUS !fuel element will be removed
     ENDIF
!Enthalpy of volatiles using Cp,volatiles(T) from Ritchie
     H_SENS_VEG_VOLIT = 0.0445_EB*(TMP_VEG_NEW**1.5_EB - TMP_GAS**1.5_EB) - & 
                        0.136_EB*(TMP_VEG_NEW - TMP_GAS)
     H_SENS_VEG_VOLIT = H_SENS_VEG_VOLIT*1000._EB !J/kg
     Q_VEG_VOLIT      = MPV_VOLIT*H_SENS_VEG_VOLIT !J
     MW_VEG_VOLIT_TERM= MPV_VOLIT/REACTION(1)%MW_FUEL
    ENDIF IF_MD_VOLIT

   ENDIF IF_VOLITALIZATION

 ENDIF IF_NET_HEAT_INFLUX
 ENDIF IF_VEG_DEGRADATION_1

!      ************** Arrehnius Degradation model *************************
! Drying and pyrolysis occur according to Arrehnius expressions obtained 
! from the literature
!
 IF_VEG_DEGRADATION_2: IF(VEG_DEGRADATION_2) THEN
  A_H2O_VEG = 600000._EB !1/s sqrt(K)
  E_H2O_VEG = 5800._EB !K
  A_PYR_VEG = 36300._EB !1/s
  E_PYR_VEG = 7250._EB !K
  L_PYR_VEG = 418 !J/kg
  IF_NOT_IGNITOR: IF (.NOT. DR%IGNITOR) THEN !dehydrate or pyrolyze 

! Drying of vegetation 
   IF_DEHYDRATION_2: IF (MPV_MOIST > MPV_MOIST_MIN) THEN
     MPV_MOIST_LOSS = MIN(DT*MPV_MOIST*A_H2O_VEG*EXP(-E_H2O_VEG/TMP_VEG)/SQRT(TMP_VEG), &
                          MPV_MOIST-MPV_MOIST_MIN)
     MPV_MOIST_LOSS = MIN(MPV_MOIST_LOSS,MPV_MOIST_LOSS_MAX) !use specified max
     MPV_MOIST      = MPV_MOIST - MPV_MOIST_LOSS
     DR%VEG_MOIST_MASS = MPV_MOIST !kg/m^3
     IF (MPV_MOIST <= MPV_MOIST_MIN) DR%VEG_MOIST_MASS = 0.0_EB
     MW_VEG_MOIST_TERM = MPV_MOIST_LOSS/MW_H2O
     Q_VEG_MOIST  = MPV_MOIST_LOSS*CP_H2O*(TMP_VEG - TMPA)
!    IF (I == 1) print*,MPV_MOIST,MPV_MOIST_LOSS
   ENDIF IF_DEHYDRATION_2

! Volitalization of vegetation
  IF_VOLITALIZATION_2: IF(MPV_VEG > MPV_VEG_MIN) THEN
     MPV_VOLIT    = MAX(CHAR_FCTR*DT*MPV_VEG*A_PYR_VEG*EXP(-E_PYR_VEG/TMP_VEG),0._EB)
     MPV_VOLIT    = MIN(MPV_VOLIT,MPV_VOLIT_MAX) !user specified max
     MPV_VOLIT    = MIN(MPV_VOLIT,(MPV_VEG-MPV_VEG_MIN))
     MPV_VEG      = MPV_VEG - MPV_VOLIT
     DR%VEG_FUEL_MASS = MPV_VEG
!Handle veg. fuel elements if element mass <= prescribed char mass
     IF (MPV_VEG <= MPV_VEG_MIN) THEN
       IF(PC%VEG_REMOVE_CHARRED) DR%R = 0.0001_EB*PC%KILL_RADIUS !fuel element will be removed
     ENDIF
!Enthalpy of volatiles using Cp,volatiles(T) from Ritchie
     H_SENS_VEG_VOLIT = 0.0445*(TMP_VEG**1.5 - TMP_GAS**1.5) - 0.136*(TMP_VEG - TMP_GAS)
     H_SENS_VEG_VOLIT = H_SENS_VEG_VOLIT*1000. !J/kg
     Q_VEG_VOLIT      = MPV_VOLIT*H_SENS_VEG_VOLIT !J
     MW_VEG_VOLIT_TERM= MPV_VOLIT/REACTION(1)%MW_FUEL
  ENDIF IF_VOLITALIZATION_2

! print*,'TMP_VEG before',TMP_VEG
  TMP_VEG_NEW  = TMP_VEG_NEW - (MPV_MOIST_LOSS*H_VAP_H2O + MPV_VOLIT*L_PYR_VEG)/ &
                                   (DR%VEG_MOIST_MASS*CP_H2O + DR%VEG_FUEL_MASS*CP_VEG)
! print*,'TMP_VEG after',TMP_VEG_NEW
! print*,'********************************************'
 ENDIF IF_NOT_IGNITOR
 ENDIF IF_VEG_DEGRADATION_2

 DR%TMP = TMP_VEG_NEW
 DR%VEG_EMISS = 4.*SIGMA*DR%VEG_KAPPA*DR%TMP**4 !used in RTE solver

! Add affects of thermal degradation of vegetation to velocity divergence
 MW_TERM    = MW_VEG_MOIST_TERM + MW_VEG_VOLIT_TERM
 MW_AVERAGE = R0/RSUM(II,JJ,KK)/RHO_GAS*(MW_VEG_MOIST_TERM + MW_VEG_VOLIT_TERM)
 Q_ENTHALPY = Q_VEG_MOIST + Q_VEG_VOLIT
 D_VAP(II,JJ,KK) = D_VAP(II,JJ,KK)            & 
                   + (-QCON_VEG*RCP_TEMPORARY + RDT*Q_ENTHALPY*RCP_TEMPORARY)/(RHO_GAS*TMP_GAS)  &
                   + RDT*MW_AVERAGE

! Add water vapor and fuel vapor mass to total density
  MPV_ADDED     = MPV_MOIST_LOSS + MPV_VOLIT
  RHO(II,JJ,KK) = RHO_GAS + MPV_ADDED
  RRHO_GAS_NEW  = 1._EB/RHO(II,JJ,KK)
! print*,'NM =',NM
! print*,'** ',rho(ii,jj,kk)

! Add water vapor mass to water vapor mass fraction
 IF (I_WATER /= 0) THEN 
! YY(II,JJ,KK,I_WATER) = (MPV_MOIST_LOSS + YY(II,JJ,KK,I_WATER)*RHO_GAS)/(MPV_MOIST_LOSS + RHO_GAS)
! YY(II,JJ,KK,I_WATER) = YY(II,JJ,KK,I_WATER) +  MPV_MOIST_LOSS*RRHO_GAS_NEW
  YY(II,JJ,KK,I_WATER) = YY(II,JJ,KK,I_WATER) + (MPV_MOIST_LOSS - MPV_ADDED*YY(II,JJ,KK,I_WATER))*RRHO_GAS_NEW
! YY(II,JJ,KK,I_WATER) = MIN(1._EB,YY(II,JJ,KK,I_WATER))
! DMPVDT_FM_VEG(II,JJ,KK,I_WATER) = DMPVDT_FM_VEG(II,JJ,KK,I_WATER) + RDT*MPV_MOIST_LOSS
 ENDIF

! Add fuel vapor mass to fuel mass fraction
 IF (I_FUEL /= 0) THEN 
! YY(II,JJ,KK,I_FUEL) = (MPV_VOLIT + YY(II,JJ,KK,I_FUEL)*RHO_GAS)/(MPV_VOLIT + RHO_GAS)
! YY(II,JJ,KK,I_FUEL) = YY(II,JJ,KK,I_FUEL) + MPV_VOLIT*RRHO_GAS_NEW
  YY(II,JJ,KK,I_FUEL) = YY(II,JJ,KK,I_FUEL) + (MPV_VOLIT - MPV_ADDED*YY(II,JJ,KK,I_FUEL))*RRHO_GAS_NEW
! YY(II,JJ,KK,I_FUEL) = MIN(1._EB,YY(II,JJ,KK,I_FUEL))
! DMPVDT_FM_VEG(II,JJ,KK,I_FUEL) = DMPVDT_FM_VEG(II,JJ,KK,I_FUEL) + RDT*MPV_VOLIT
 ENDIF

! WRITE(9998,'(A)')'T,TMP_VEG,QCON_VEG,QRAD_VEG'
!IF (II==0.5*IBAR .AND. JJ==0.5*JBAR .AND. KK==0.333*KBAR) THEN
!IF (II==12 .AND. JJ==12 .AND. KK==4) THEN 
!IF (II==20 .AND. JJ==20 .AND. KK==25) THEN !M=14% and 49% element burnout
!IF (II==27 .AND. JJ==20 .AND. KK==7) THEN !M=49% not full element burnout
! WRITE(9998,'(9(ES12.4))')T,TMP_GAS,TMP_VEG,QCON_VEG,QRAD_VEG,DR%VEG_MOIST_MASS,DR%VEG_FUEL_MASS, &
!                          MPV_MOIST_LOSS_MAX*RDT,MPV_VOLIT_MAX*RDT
!ENDIF

! V_VEG               = V_VEG + V_CELL
! TOTAL_MASS_MOIST    = TOTAL_MASS_MOIST + DR%VEG_MOIST_MASS*V_CELL
! TOTAL_MASS_DRY_FUEL = TOTAL_MASS_DRY_FUEL + DR%VEG_FUEL_MASS*V_CELL

 N_TREE = DR%VEG_N_TREE_OUTPUT
 IF (N_TREE /= 0) THEN
  TREE_OUTPUT_DATA(N_TREE,1,NM) = TREE_OUTPUT_DATA(N_TREE,1,NM) + DR%VEG_FUEL_MASS*V_CELL !kg
  TREE_OUTPUT_DATA(N_TREE,2,NM) = TREE_OUTPUT_DATA(N_TREE,2,NM) + DR%VEG_MOIST_MASS*V_CELL !kg
  TREE_OUTPUT_DATA(N_TREE,3,NM) = TREE_OUTPUT_DATA(N_TREE,3,NM) + DR%VEG_DIVQC*V_CELL*0.001_EB !kW
  TREE_OUTPUT_DATA(N_TREE,4,NM) = TREE_OUTPUT_DATA(N_TREE,4,NM) + DR%VEG_DIVQR*V_CELL*0.001_EB !kW
 ENDIF

ENDDO DROPLET_LOOP

!print*,'--------------------------------'
!print*,'VEGE: NM, TREE_OUTPUT_DATA(1,1,NM),(1,2,NM)',nm, tree_output_data(1,1,nm),tree_output_data(1,2,nm)

! Write out total bulk
!TOTAL_BULKDENS_MOIST = TOTAL_MASS_MOIST/V_VEG
!TOTAL_BULKDENS_DRY_FUEL = TOTAL_MASS_DRY_FUEL/V_VEG
!WRITE(9999,'(5(ES12.4))')T,TOTAL_BULKDENS_DRY_FUEL,TOTAL_BULKDENS_MOIST,TOTAL_MASS_DRY_FUEL,TOTAL_MASS_MOIST

!VEG_TOTAL_DRY_MASS(NM)   = TOTAL_MASS_DRY_FUEL
!VEG_TOTAL_MOIST_MASS(NM) = TOTAL_MASS_MOIST

! Remove vegetation that has completely burned (i.e., DR%R set equal to zero)
CALL REMOVE_DROPLETS(T,NM)
 
END SUBROUTINE RAISED_VEG_MASS_ENERGY_TRANSFER


SUBROUTINE GET_REV_vege(MODULE_REV,MODULE_DATE)
INTEGER,INTENT(INOUT) :: MODULE_REV
CHARACTER(255),INTENT(INOUT) :: MODULE_DATE

WRITE(MODULE_DATE,'(A)') vegerev(INDEX(vegerev,':')+1:LEN_TRIM(vegerev)-2)
READ (MODULE_DATE,'(I5)') MODULE_REV
WRITE(MODULE_DATE,'(A)') vegedate

END SUBROUTINE GET_REV_vege


END MODULE VEGE
