PROGRAM FDS  

! Fire Dynamics Simulator, Main Program, Multiple CPU version.

USE PRECISION_PARAMETERS
USE MESH_VARIABLES
USE GLOBAL_CONSTANTS
USE TRAN
USE DUMP
USE READ_INPUT
USE INIT
USE DIVG
USE PRES
USE MASS
USE PART
USE VELO
USE RAD
USE MEMORY_FUNCTIONS
USE COMP_FUNCTIONS, ONLY : SECOND, WALL_CLOCK_TIME, SHUTDOWN
USE MATH_FUNCTIONS, ONLY : GAUSSJ
USE DEVICE_VARIABLES
USE WALL_ROUTINES
USE FIRE
USE RADCONS
USE CONTROL_FUNCTIONS
USE EVAC

IMPLICIT NONE

! Miscellaneous declarations

CHARACTER(255), PARAMETER :: mainmpiid='$Id$'
CHARACTER(255), PARAMETER :: mainmpirev='$Revision$'
CHARACTER(255), PARAMETER :: mainmpidate='$Date$'
LOGICAL  :: EX,DIAGNOSTICS,EXCHANGE_RADIATION=.TRUE.
INTEGER  :: LO10,NM,IZERO,REVISION_NUMBER,IOS
CHARACTER(255) :: REVISION_DATE
REAL(EB) :: T_MAX,T_MIN
REAL(EB), ALLOCATABLE, DIMENSION(:) ::  T,TC_GLB,TC_LOC,DT_SYNC, DTNEXT_SYNC,DSUM_ALL,PSUM_ALL,USUM_ALL
INTEGER, ALLOCATABLE, DIMENSION(:) ::  MESH_STOP_STATUS,COUNT_GLB,COUNT_LOC
LOGICAL, ALLOCATABLE, DIMENSION(:) ::  ACTIVE_MESH,STATE_GLB,STATE_LOC
INTEGER NOM,IWW,IW
INTEGER, PARAMETER :: N_DROP_ADOPT_MAX=10000
TYPE (MESH_TYPE), POINTER :: M,M4
TYPE (OMESH_TYPE), POINTER :: M2,M3
 
! MPI stuff

! INCLUDE '/usr/local/include/mpif.h'  ! Uncomment this line only if the compiler needs help finding mpif.h
INCLUDE 'mpif.h'
INTEGER :: N,MYID=0,NUMPROCS=1,I,IERR,STATUS(MPI_STATUS_SIZE)
INTEGER :: RNODE,BUFFER_SIZE,TAG,PNAMELEN
INTEGER, ALLOCATABLE, DIMENSION(:,:,:) :: TAGS
INTEGER, ALLOCATABLE, DIMENSION(:) :: REQ,PREQ
INTEGER, ALLOCATABLE, DIMENSION(:,:) :: ARRAY_OF_STATUSES
INTEGER, ALLOCATABLE, DIMENSION(:,:) :: ARRAY_OF_STATUSES2
INTEGER :: N_REQ,N_PREQ
CHARACTER(MPI_MAX_PROCESSOR_NAME) PNAME
REAL(EB) :: MPI_WALL_TIME,MPI_WALL_TIME_START
 
! Initialize MPI (First executable lines of code)
 
CALL MPI_INIT(IERR)
CALL MPI_COMM_RANK(MPI_COMM_WORLD, MYID, IERR)
CALL MPI_COMM_SIZE(MPI_COMM_WORLD, NUMPROCS, IERR)
CALL MPI_GET_PROCESSOR_NAME(PNAME, PNAMELEN, IERR)
MPI_WALL_TIME_START = MPI_WTIME()
 
WRITE(LU_ERR,'(A,I2,A,I2,A,A)') 'Mesh ',MYID+1,' of ', NUMPROCS,' is alive on ',PNAME(1:PNAMELEN)

! Start wall clock timing

WALL_CLOCK_START = WALL_CLOCK_TIME()
 
! Assign a compilation date (All Nodes)

WRITE(VERSION_STRING,'(A)') '5.1.0'
PARALLEL = .TRUE.

IF (INDEX(mainmpirev,':',BACK=.TRUE.)>0) THEN
   WRITE(REVISION_DATE,'(A)',IOSTAT=IOS,ERR=5) mainmpirev(INDEX(mainmpirev,':')+1:LEN_TRIM(mainmpirev)-2)
   5 REVISION_NUMBER = 0
   IF (IOS==0) READ(REVISION_DATE,'(I5)') REVISION_NUMBER
   WRITE(REVISION_DATE,'(A)') mainmpidate
   CALL GET_REVISION_NUMBER(REVISION_NUMBER,REVISION_DATE)
   SVN_REVISION_NUMBER = REVISION_NUMBER
   WRITE(COMPILE_DATE,'(A)',IOSTAT=IOS,ERR=10) REVISION_DATE(INDEX(REVISION_DATE,'(')+1:INDEX(REVISION_DATE,')')-1)
   10 IF (IOS>0) COMPILE_DATE = 'null'
ENDIF

! Read input from CHID.data file (All Nodes)
 
CALL READ_DATA(MYID)

CALL EVAC_READ_DATA
 
IF (NMESHES/=NUMPROCS) CALL SHUTDOWN('ERROR: Number of meshes not equal to '// 'number of threads')
 
! Open and write to Smokeview file (Master Node Only)
 
CALL ASSIGN_FILE_NAMES
IF (MYID==0) CALL WRITE_SMOKEVIEW_FILE
 
! Stop all the processes if this is just a set-up run
 
IF (SET_UP) CALL SHUTDOWN('Stop FDS, Set-up only')
 
! Set up Time arrays (All Nodes)
 
ALLOCATE(ACTIVE_MESH(NMESHES),STAT=IZERO)
CALL ChkMemErr('MAIN','ACTIVE_MESH',IZERO)
ALLOCATE(T(NMESHES),STAT=IZERO)
CALL ChkMemErr('MAIN','T',IZERO)
ALLOCATE(DT_SYNC(NMESHES),STAT=IZERO)
CALL ChkMemErr('MAIN','DT_SYNC',IZERO)
ALLOCATE(DTNEXT_SYNC(NMESHES),STAT=IZERO)
CALL ChkMemErr('MAIN','DTNEXT_SYNC',IZERO)
ALLOCATE(MESH_STOP_STATUS(NMESHES),STAT=IZERO)
CALL ChkMemErr('MAIN','MESH_STOP_STATUS',IZERO)

! Set up dummy arrays to hold various arrays that must be exchanged among meshes

ALLOCATE(COUNT_LOC(N_DEVC),STAT=IZERO)
CALL ChkMemErr('MAIN','COUNT_LOC',IZERO) 
ALLOCATE(COUNT_GLB(N_DEVC),STAT=IZERO)
CALL ChkMemErr('MAIN','COUNT_GLB',IZERO) 
ALLOCATE(STATE_GLB(N_DEVC),STAT=IZERO)
CALL ChkMemErr('MAIN','STATE_GLB',IZERO) 
ALLOCATE(STATE_LOC(N_DEVC),STAT=IZERO)
CALL ChkMemErr('MAIN','STATE_LOC',IZERO) 
ALLOCATE(TC_GLB(N_DEVC),STAT=IZERO)
CALL ChkMemErr('MAIN','TC_GLB',IZERO)
ALLOCATE(TC_LOC(N_DEVC),STAT=IZERO)
CALL ChkMemErr('MAIN','TC_LOC',IZERO)

! Allocate a few arrays needed to exchange divergence and pressure info among meshes

IF (N_ZONE > 0) THEN
   ALLOCATE(DSUM_ALL(N_ZONE),STAT=IZERO)
   ALLOCATE(PSUM_ALL(N_ZONE),STAT=IZERO)
   ALLOCATE(USUM_ALL(N_ZONE),STAT=IZERO)
ENDIF

! Start the clock

T     = T_BEGIN
MESH_STOP_STATUS = NO_STOP
 
! Create unique tags for all mesh exchanges
 
ALLOCATE(REQ(NMESHES*NMESHES*10)) 
REQ = MPI_REQUEST_NULL
ALLOCATE(PREQ(NMESHES*NMESHES*10)) 
PREQ = MPI_REQUEST_NULL
ALLOCATE(ARRAY_OF_STATUSES(MPI_STATUS_SIZE,NMESHES*NMESHES*10))
ALLOCATE(ARRAY_OF_STATUSES2(MPI_STATUS_SIZE,NMESHES*NMESHES*10))
ALLOCATE(TAGS(NMESHES,NMESHES,0:4))
TAG = 0
DO NM=1,NMESHES
   DO NOM=NM,NMESHES
      TAG = TAG+1
      TAGS(NM,NOM,0) = TAG
      TAGS(NOM,NM,0) = TAG
   ENDDO
ENDDO
TAGS(:,:,1) = TAGS(:,:,0) + 1000
TAGS(:,:,2) = TAGS(:,:,0) + 2000
TAGS(:,:,3) = TAGS(:,:,0) + 3000
TAGS(:,:,4) = TAGS(:,:,0) + 4000
 
! Initialize global parameters (All Nodes)
 
CALL INITIALIZE_GLOBAL_VARIABLES
CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
 
! Initialize radiation (All Nodes)
 
IF (RADIATION) CALL INIT_RADIATION
CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
 
! Allocate and initialize mesh-specific variables
 
DO NM=MYID+1,NMESHES,NUMPROCS
   CALL INITIALIZE_MESH_VARIABLES(NM)
ENDDO
CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
 
! Allocate and initialize mesh variable exchange arrays
 
DO NM=MYID+1,NMESHES,NUMPROCS
CALL INITIALIZE_MESH_EXCHANGE(NM)
ENDDO
CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
 
CALL MPI_ALLGATHER(I_MIN(1,MYID+1),NMESHES,MPI_INTEGER,I_MIN, NMESHES,MPI_INTEGER,MPI_COMM_WORLD,IERR)
CALL MPI_ALLGATHER(I_MAX(1,MYID+1),NMESHES,MPI_INTEGER,I_MAX, NMESHES,MPI_INTEGER,MPI_COMM_WORLD,IERR)
CALL MPI_ALLGATHER(J_MIN(1,MYID+1),NMESHES,MPI_INTEGER,J_MIN, NMESHES,MPI_INTEGER,MPI_COMM_WORLD,IERR)
CALL MPI_ALLGATHER(J_MAX(1,MYID+1),NMESHES,MPI_INTEGER,J_MAX, NMESHES,MPI_INTEGER,MPI_COMM_WORLD,IERR)
CALL MPI_ALLGATHER(K_MIN(1,MYID+1),NMESHES,MPI_INTEGER,K_MIN, NMESHES,MPI_INTEGER,MPI_COMM_WORLD,IERR)
CALL MPI_ALLGATHER(K_MAX(1,MYID+1),NMESHES,MPI_INTEGER,K_MAX, NMESHES,MPI_INTEGER,MPI_COMM_WORLD,IERR)
CALL MPI_ALLGATHER(NIC(1,MYID+1),  NMESHES,MPI_INTEGER,NIC,   NMESHES,MPI_INTEGER,MPI_COMM_WORLD,IERR)
 
I_MIN = TRANSPOSE(I_MIN)
I_MAX = TRANSPOSE(I_MAX)
J_MIN = TRANSPOSE(J_MIN)
J_MAX = TRANSPOSE(J_MAX)
K_MIN = TRANSPOSE(K_MIN)
K_MAX = TRANSPOSE(K_MAX)
NIC   = TRANSPOSE(NIC)
 
DO NM=MYID+1,NMESHES,NUMPROCS
   CALL DOUBLE_CHECK(NM)
ENDDO
CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
 
! Potentially read data from a previous calculation 
 
DO NM=MYID+1,NMESHES,NUMPROCS
   IF (RESTART) CALL READ_RESTART(T(NM),NM)
ENDDO
CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
 
! Initialize output files containing global data (Master Node Only)
 
IF (MYID==0) CALL INITIALIZE_GLOBAL_DUMPS
IF (MYID==0) CALL INIT_EVAC_DUMPS
CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
 
! Initialize output files that are mesh-specific
 
DO NM=MYID+1,NMESHES,NUMPROCS
   CALL INITIALIZE_MESH_DUMPS(NM)
   CALL INITIALIZE_DROPLETS(NM)
   CALL INITIALIZE_TREES(NM)
   CALL INITIALIZE_EVAC(NM)
   CALL POST_RECEIVES(NM,0)
ENDDO
CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
CALL INIT_EVAC_GROUPS
 
! Write out character strings to .smv file
 
CALL WRITE_STRINGS
 
! Initialize Mesh Exchange Arrays (All Nodes)
 
CALL MESH_EXCHANGE(0)
CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)

! Make an initial dump of ambient values

IF (.NOT.RESTART) THEN
   DO NM=MYID+1,NMESHES,NUMPROCS
      CALL UPDATE_OUTPUTS(T(NM),NM)      
      CALL DUMP_MESH_OUTPUTS(T(NM),NM)
   ENDDO
ENDIF

! Ensure the time is known to all meshes

CALL MPI_ALLGATHER(T(MYID+1),1,MPI_DOUBLE_PRECISION,T,1, MPI_DOUBLE_PRECISION,MPI_COMM_WORLD,IERR)
CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)

! Make an initial dump of global output quantities

IF (.NOT.RESTART) THEN
   CALL UPDATE_CONTROLS(T)
   CALL DUMP_GLOBAL_OUTPUTS(T(1))
ENDIF
 
! Check for changes in VENT or OBSTruction control and device status at t=T_BEGIN

IF (.NOT.RESTART) THEN
   OBST_VENT_LOOP: DO NM=MYID+1,NMESHES,NUMPROCS
      CALL OPEN_AND_CLOSE(T(NM),NM)  
   ENDDO OBST_VENT_LOOP
ENDIF

!***********************************************************************************************************************************
!                                                           MAIN TIMESTEPPING LOOP
!***********************************************************************************************************************************
 
MAIN_LOOP: DO  
 
   ICYC  = ICYC + 1 
 
   IF (MOD(ICYC,3)==0 .AND. TIMING) THEN
      MPI_WALL_TIME = MPI_WTIME() - MPI_WALL_TIME_START
      WRITE(0,'(A,I2,A,I6,A,F12.4)')  ' Thread ',MYID+1,' starts iteration',ICYC,' at ', MPI_WALL_TIME
   ENDIF
 
   EXCHANGE_RADIATION = .FALSE.
   IF (RADIATION) THEN
      IF (MOD(ICYC,ANGLE_INCREMENT*TIME_STEP_INCREMENT)==0) EXCHANGE_RADIATION = .TRUE.
   ENDIF
 
   ! Synchronize clocks
   
   CALL MPI_ALLGATHER(T(MYID+1),1,MPI_DOUBLE_PRECISION,T,1, MPI_DOUBLE_PRECISION,MPI_COMM_WORLD,IERR)

   ! Check for program stops
 
   INQUIRE(FILE=TRIM(CHID)//'.stop',EXIST=EX)
   IF (EX) MESH_STOP_STATUS = USER_STOP
   CALL MPI_ALLGATHER(MESH_STOP_STATUS(MYID+1),1,MPI_INTEGER,MESH_STOP_STATUS,1,MPI_INTEGER,MPI_COMM_WORLD,IERR)
 
   ! Figure out fastest and slowest meshes
 
   T_MAX = -1000000._EB
   T_MIN =  1000000._EB
   DO NM=1,NMESHES
      T_MIN = MIN(T(NM),T_MIN)
      T_MAX = MAX(T(NM),T_MAX)
      IF (MESH_STOP_STATUS(NM)/=NO_STOP) GLOBAL_STOP_STATUS = MESH_STOP_STATUS(NM)
   ENDDO
 
   ! Determine time step
 
   IF (SYNCHRONIZE) THEN
      DT_SYNC(MYID+1) = MESHES(MYID+1)%DTNEXT
      CALL MPI_ALLGATHER(DT_SYNC(MYID+1),1,MPI_DOUBLE_PRECISION, DT_SYNC,1,MPI_DOUBLE_PRECISION,MPI_COMM_WORLD,IERR)
      IF (SYNC_TIME_STEP(MYID+1)) THEN
         MESHES(MYID+1)%DTNEXT = MINVAL(DT_SYNC,MASK=SYNC_TIME_STEP)
         T(MYID+1) = MINVAL(T,MASK=SYNC_TIME_STEP)
         ACTIVE_MESH(MYID+1) = .TRUE.
      ELSE
         ACTIVE_MESH(MYID+1) = .FALSE.
         IF (T(MYID+1)+MESHES(MYID+1)%DTNEXT <= T_MAX)  ACTIVE_MESH(MYID+1) = .TRUE.
         IF (GLOBAL_STOP_STATUS/=NO_STOP) ACTIVE_MESH(MYID+1) = .TRUE.
      ENDIF
   ELSE
      ACTIVE_MESH = .FALSE.
      DO NM=1,NMESHES
         IF (T(NM)+MESHES(NM)%DTNEXT <= T_MAX) ACTIVE_MESH(NM) = .TRUE.
         IF (GLOBAL_STOP_STATUS/=NO_STOP) ACTIVE_MESH(NM) = .TRUE.
      ENDDO
   ENDIF
 
   ! Determine when to dump out diagnostics to the .out file
 
   DIAGNOSTICS = .FALSE.
   LO10 = LOG10(REAL(MAX(1,ABS(ICYC)),EB))
   IF (MOD(ICYC,10**LO10)==0 .OR. MOD(ICYC,100)==0 .OR. T_MIN>=T_END .OR. GLOBAL_STOP_STATUS/=NO_STOP) DIAGNOSTICS = .TRUE.
 
   ! Give every processor the full ACTIVE_MESH array
 
   CALL MPI_ALLGATHER(ACTIVE_MESH(MYID+1), 1, MPI_LOGICAL,  ACTIVE_MESH,1, MPI_LOGICAL, MPI_COMM_WORLD, IERR)
 
   ! If no meshes are due to be updated, update them all
 
   IF (ALL(.NOT.ACTIVE_MESH)) ACTIVE_MESH = .TRUE.
   CALL EVAC_MAIN_LOOP
 
   PREDICTOR = .TRUE.
   CORRECTOR = .FALSE.

   ! Diagnostic calls

   IF (DEBUG) WRITE(0,*) 'Cycle ',ICYC,' Mesh ',MYID+1, ' starting',ACTIVE_MESH(MYID+1)

   IF (MOD(ICYC,3)==0 .AND. TIMING .AND. ACTIVE_MESH(MYID+1)) THEN
      MPI_WALL_TIME = MPI_WTIME() - MPI_WALL_TIME_START
      WRITE(0,'(A,I3,A,F12.4)')  ' Thread ',MYID+1,' is active at ', MPI_WALL_TIME
   ENDIF
 
   ! Begin the PREDICTOR step
 
   COMPUTE_FINITE_DIFFERENCES_1: DO NM=MYID+1,NMESHES,NUMPROCS
      IF (.NOT.ACTIVE_MESH(NM)) CYCLE COMPUTE_FINITE_DIFFERENCES_1
      MESHES(NM)%DT = MESHES(NM)%DTNEXT
      NTCYC(NM)   = NTCYC(NM) + 1
 
      CALL INSERT_DROPLETS_AND_PARTICLES(T(NM),NM)
      CALL COMPUTE_VELOCITY_FLUX(T(NM),NM)
      CALL UPDATE_PARTICLES(T(NM),NM)
      IF (.NOT.ISOTHERMAL .OR. N_SPECIES>0) CALL MASS_FINITE_DIFFERENCES(NM)
 
   ENDDO COMPUTE_FINITE_DIFFERENCES_1

   ! Estimate quantities at next time step, and decrease/increase time step if necessary based on CFL condition
 
   CHANGE_TIME_STEP_LOOP: DO

      ! Predict density and mass fractions at next time step, and then start the divergence calculation
 
      COMPUTE_DENSITY_LOOP: DO NM=MYID+1,NMESHES,NUMPROCS
         IF (.NOT.ACTIVE_MESH(NM)) CYCLE COMPUTE_DENSITY_LOOP
         IF (.NOT.ISOTHERMAL .OR. N_SPECIES>0) CALL DENSITY(NM)
         CALL POST_RECEIVES(NM,1)
      ENDDO COMPUTE_DENSITY_LOOP

      ! Exchange density and species mass fractions in interpolated boundaries

      IF (MOD(ICYC,3)==0.AND.TIMING.AND.ACTIVE_MESH(MYID+1)) THEN
         MPI_WALL_TIME = MPI_WTIME() - MPI_WALL_TIME_START
         WRITE(0,'(A,I3,A,F12.4)')  ' Thread ',MYID+1,' enters Mesh Exchange 1 at ', MPI_WALL_TIME
      ENDIF
      CALL MESH_EXCHANGE(1)
      IF (MOD(ICYC,3)==0.AND.TIMING.AND.ACTIVE_MESH(MYID+1)) THEN
         MPI_WALL_TIME = MPI_WTIME() - MPI_WALL_TIME_START
         WRITE(0,'(A,I3,A,F12.4)')  ' Thread ',MYID+1,' exits  Mesh Exchange 1 at ', MPI_WALL_TIME
      ENDIF

      ! Do mass and energy boundary conditions, and begin divergence calculation

      COMPUTE_DIVERGENCE_LOOP: DO NM=MYID+1,NMESHES,NUMPROCS
         IF (.NOT.ACTIVE_MESH(NM)) CYCLE COMPUTE_DIVERGENCE_LOOP
         IF (.NOT.ISOTHERMAL .OR. N_SPECIES>0) CALL WALL_BC(T(NM),NM)
         CALL DIVERGENCE_PART_1(T(NM),NM)
      ENDDO COMPUTE_DIVERGENCE_LOOP

      ! If there are pressure ZONEs, exchange integrated quantities mesh to mesh for use in the divergence calculation

      CALL EXCHANGE_DIVERGENCE_INFO

      ! Finish the divergence calculation and solve for the pressure at the current time step

      COMPUTE_PRESSURE_LOOP: DO NM=MYID+1,NMESHES,NUMPROCS
         IF (.NOT.ACTIVE_MESH(NM)) CYCLE COMPUTE_PRESSURE_LOOP
         CALL DIVERGENCE_PART_2(NM)
         CALL PRESSURE_SOLVER(NM)
         CALL EVAC_PRESSURE_LOOP(NM)
      ENDDO COMPUTE_PRESSURE_LOOP

      ! Experimental pressure correction

      IF (PRESSURE_CORRECTION) CALL CORRECT_PRESSURE(1)
 
      ! Predict the velocity components at the next time step

      PREDICT_VELOCITY_LOOP: DO NM=MYID+1,NMESHES,NUMPROCS
         IF (.NOT.ACTIVE_MESH(NM)) CYCLE PREDICT_VELOCITY_LOOP
         CALL VELOCITY_PREDICTOR(T(NM),NM,MESH_STOP_STATUS(NM))
      ENDDO PREDICT_VELOCITY_LOOP

      ! Exchange information about the time step status, and if need be, repeat the CHANGE_TIME_STEP_LOOP
 
      IF (SYNCHRONIZE) THEN
         NM = MYID+1
         CALL MPI_ALLGATHER(CHANGE_TIME_STEP(NM),1,MPI_LOGICAL, CHANGE_TIME_STEP,1,MPI_LOGICAL,MPI_COMM_WORLD,IERR)
         CALL MPI_ALLGATHER(MESH_STOP_STATUS(NM),1,MPI_INTEGER,MESH_STOP_STATUS,1, MPI_INTEGER,MPI_COMM_WORLD,IERR)
         IF (ANY(MESH_STOP_STATUS/=NO_STOP)) THEN
            IF (ANY(MESH_STOP_STATUS==INSTABILITY_STOP)) THEN
               GLOBAL_STOP_STATUS = INSTABILITY_STOP
               DIAGNOSTICS = .TRUE.
            ENDIF
            EXIT CHANGE_TIME_STEP_LOOP
         ENDIF
         IF (ANY(CHANGE_TIME_STEP)) THEN
            CHANGE_TIME_STEP = .TRUE.
            DT_SYNC(NM) = MESHES(NM)%DT
            DTNEXT_SYNC(NM) = MESHES(NM)%DTNEXT
            CALL MPI_ALLGATHER(DT_SYNC(NM),1,MPI_DOUBLE_PRECISION, DT_SYNC,1,MPI_DOUBLE_PRECISION,MPI_COMM_WORLD,IERR)
            CALL MPI_ALLGATHER(DTNEXT_SYNC(NM),1,MPI_DOUBLE_PRECISION, DTNEXT_SYNC,1,MPI_DOUBLE_PRECISION,MPI_COMM_WORLD,IERR)
      !EVAC: Timo: Next line should be here somehow
            IF (EVACUATION_ONLY(NM)) CHANGE_TIME_STEP(NM) = .FALSE.
            MESHES(NM)%DTNEXT = MINVAL(DTNEXT_SYNC,MASK=SYNC_TIME_STEP)
            MESHES(NM)%DT     = MINVAL(DT_SYNC,MASK=SYNC_TIME_STEP)
         ENDIF
      ENDIF
 
      IF (MESH_STOP_STATUS(MYID+1)/=NO_STOP) THEN
         GLOBAL_STOP_STATUS = MESH_STOP_STATUS(MYID+1)
         IF (GLOBAL_STOP_STATUS==INSTABILITY_STOP) DIAGNOSTICS = .TRUE.
         EXIT CHANGE_TIME_STEP_LOOP
      ENDIF
 
      IF (.NOT.ANY(CHANGE_TIME_STEP)) EXIT CHANGE_TIME_STEP_LOOP
 
   ENDDO CHANGE_TIME_STEP_LOOP
 
   CHANGE_TIME_STEP = .FALSE.
 
   ! Set-up "receive" arrays for inter-mesh info exchange, and advance the time to start the CORRECTOR step
 
   UPDATE_TIME: DO NM=MYID+1,NMESHES,NUMPROCS
      IF (.NOT.ACTIVE_MESH(NM)) CYCLE UPDATE_TIME
      CALL POST_RECEIVES(NM,2)
      T(NM) = T(NM) + MESHES(NM)%DT  
   ENDDO UPDATE_TIME
 
   !============================================================================================================================
 
   ! Exchange information among meshes
 
   IF (MOD(ICYC,3)==0.AND.TIMING.AND.ACTIVE_MESH(MYID+1)) THEN
      MPI_WALL_TIME = MPI_WTIME() - MPI_WALL_TIME_START
      WRITE(0,'(A,I3,A,F12.4)')  ' Thread ',MYID+1,' enters Mesh Exchange 2 at ', MPI_WALL_TIME
   ENDIF
 
   CALL MESH_EXCHANGE(2)
 
   IF (MOD(ICYC,3)==0.AND.TIMING.AND.ACTIVE_MESH(MYID+1)) THEN
      MPI_WALL_TIME = MPI_WTIME() - MPI_WALL_TIME_START
      WRITE(0,'(A,I3,A,F12.4)')  ' Thread ',MYID+1,' exits  Mesh Exchange 2 at ', MPI_WALL_TIME
   ENDIF
 
   !============================================================================================================================
 
   CORRECTOR = .TRUE.
   PREDICTOR = .FALSE.
 
   COMPUTE_FINITE_DIFFERENCES_2: DO NM=MYID+1,NMESHES,NUMPROCS
      IF (.NOT.ACTIVE_MESH(NM)) CYCLE COMPUTE_FINITE_DIFFERENCES_2
      CALL COMPUTE_VELOCITY_FLUX(T(NM),NM)
      IF (.NOT.ISOTHERMAL .OR. N_SPECIES>0) THEN
         CALL MASS_FINITE_DIFFERENCES(NM)
         CALL DENSITY(NM)
      ENDIF
      CALL POST_RECEIVES(NM,3)   ! Post Receive Arrays
   ENDDO COMPUTE_FINITE_DIFFERENCES_2

   ! Exchange density and mass species

   IF (MOD(ICYC,3)==0.AND.TIMING.AND.ACTIVE_MESH(MYID+1)) THEN
      MPI_WALL_TIME = MPI_WTIME() - MPI_WALL_TIME_START
      WRITE(0,'(A,I3,A,F12.4)')  ' Thread ',MYID+1,' enters Mesh Exchange 3 at ', MPI_WALL_TIME
   ENDIF
   CALL MESH_EXCHANGE(3)
   IF (MOD(ICYC,3)==0.AND.TIMING.AND.ACTIVE_MESH(MYID+1)) THEN
      MPI_WALL_TIME = MPI_WTIME() - MPI_WALL_TIME_START
      WRITE(0,'(A,I3,A,F12.4)')  ' Thread ',MYID+1,' exits  Mesh Exchange 3 at ', MPI_WALL_TIME
   ENDIF

   COMPUTE_DIVERGENCE_2: DO NM=MYID+1,NMESHES,NUMPROCS
      IF (.NOT.ACTIVE_MESH(NM)) CYCLE COMPUTE_DIVERGENCE_2
      IF (.NOT.ISOTHERMAL .OR. N_SPECIES>0) THEN
         IF (N_REACTIONS > 0) CALL COMBUSTION (NM)
         CALL WALL_BC(T(NM),NM)
         CALL COMPUTE_RADIATION(NM)
      ENDIF
      CALL UPDATE_PARTICLES(T(NM),NM)
      CALL DIVERGENCE_PART_1(T(NM),NM)
   ENDDO COMPUTE_DIVERGENCE_2

   CALL EXCHANGE_DIVERGENCE_INFO

   COMPUTE_PRESSURE_2: DO NM=MYID+1,NMESHES,NUMPROCS
      IF (.NOT.ACTIVE_MESH(NM)) CYCLE COMPUTE_PRESSURE_2
      CALL DIVERGENCE_PART_2(NM)
      CALL PRESSURE_SOLVER(NM)
      CALL EVAC_PRESSURE_LOOP(NM)
   ENDDO COMPUTE_PRESSURE_2

   IF (PRESSURE_CORRECTION) CALL CORRECT_PRESSURE(2)
 
   CORRECT_VELOCITY_LOOP: DO NM=MYID+1,NMESHES,NUMPROCS
      IF (.NOT.ACTIVE_MESH(NM)) CYCLE CORRECT_VELOCITY_LOOP
      CALL POST_RECEIVES(NM,4)   ! Post Receive Arrays
      CALL OPEN_AND_CLOSE(T(NM),NM)   ! Doors, windows, etc.
      CALL VELOCITY_CORRECTOR(T(NM),NM)
      CALL UPDATE_OUTPUTS(T(NM),NM)
      CALL DUMP_MESH_OUTPUTS(T(NM),NM)
      IF (DIAGNOSTICS) CALL CHECK_DIVERGENCE(NM)
   ENDDO CORRECT_VELOCITY_LOOP
 
   !+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
 
   ! Exchange information among meshes
 
   IF (MOD(ICYC,3)==0.AND.TIMING.AND.ACTIVE_MESH(MYID+1)) THEN
      MPI_WALL_TIME = MPI_WTIME() - MPI_WALL_TIME_START
      WRITE(0,'(A,I3,A,F12.4)')  ' Thread ',MYID+1,' enters Mesh Exchange 4 at ', MPI_WALL_TIME
   ENDIF
 
   CALL MESH_EXCHANGE(4)
   CALL EVAC_EXCHANGE

   IF (MOD(ICYC,3)==0.AND.TIMING.AND.ACTIVE_MESH(MYID+1)) THEN
      MPI_WALL_TIME = MPI_WTIME() - MPI_WALL_TIME_START
      WRITE(0,'(A,I3,A,F12.4)')  ' Thread ',MYID+1,' exits  Mesh Exchange 4 at ', MPI_WALL_TIME
   ENDIF
 
   !+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
 
   ! Write character strings out to the .smv file
 
   IF (DIAGNOSTICS) CALL WRITE_STRINGS
 
   ! Exchange info for diagnostic print out
 
   IF (DIAGNOSTICS) CALL EXCHANGE_DIAGNOSTICS

   ! Check for dumping end of timestep outputs

   CALL DUMP_GLOBAL_OUTPUTS(T_MIN)
   CALL UPDATE_CONTROLS(T)
 
   ! Dump out diagnostics

   IF (MYID==0 .AND. DIAGNOSTICS) CALL WRITE_DIAGNOSTICS(T)
 
   ! Stop the run

   IF (T_MIN>=T_END .OR. GLOBAL_STOP_STATUS/=NO_STOP) EXIT MAIN_LOOP
 
   ! Flush Buffers (All Nodes)
 
   IF (MOD(ICYC,10)==0 .AND. FLUSH_FILE_BUFFERS) THEN
      IF (MYID==0) CALL FLUSH_GLOBAL_BUFFERS
      DO NM=MYID+1,NMESHES,NUMPROCS
         CALL FLUSH_LOCAL_BUFFERS(NM)
      ENDDO
   ENDIF

   IF (MOD(ICYC,3) ==0 .AND. TIMING) THEN
      MPI_WALL_TIME = MPI_WTIME() - MPI_WALL_TIME_START
      WRITE(0,'(A,I3,A,I6,A,F12.4)')  ' Thread ',MYID+1,' ends Iteration ',ICYC,' at ', MPI_WALL_TIME
   ENDIF
 
ENDDO MAIN_LOOP
 
!***********************************************************************************************************************************
!                                                     END OF TIME STEPPING LOOP
!***********************************************************************************************************************************
 
TUSED(1,MYID+1) = SECOND() - TUSED(1,MYID+1)
CALL MPI_GATHER(TUSED(1,MYID+1),N_TIMERS,MPI_DOUBLE_PRECISION, TUSED,N_TIMERS,MPI_DOUBLE_PRECISION,0, MPI_COMM_WORLD,IERR)
IF (MYID==0) CALL TIMINGS
CALL MPI_FINALIZE(IERR)
 
SELECT CASE(GLOBAL_STOP_STATUS)
   CASE(NO_STOP)
      CALL SHUTDOWN('STOP: FDS completed successfully')
   CASE(INSTABILITY_STOP) 
      CALL SHUTDOWN('STOP: Numerical Instability')
   CASE(USER_STOP) 
      CALL SHUTDOWN('STOP: FDS stopped by user')
END SELECT
 
 
CONTAINS
 
 
SUBROUTINE EXCHANGE_DIVERGENCE_INFO

! Exchange information mesh to mesh needed for divergence integrals

IF (N_ZONE > 0) THEN
   CALL MPI_ALLREDUCE(DSUM(1,MYID+1),DSUM_ALL(1),N_ZONE,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(PSUM(1,MYID+1),PSUM_ALL(1),N_ZONE,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(USUM(1,MYID+1),USUM_ALL(1),N_ZONE,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   DSUM(1:N_ZONE,MYID+1) = DSUM_ALL(1:N_ZONE)
   PSUM(1:N_ZONE,MYID+1) = PSUM_ALL(1:N_ZONE)
   USUM(1:N_ZONE,MYID+1) = USUM_ALL(1:N_ZONE)
ENDIF

END SUBROUTINE EXCHANGE_DIVERGENCE_INFO

 
SUBROUTINE INITIALIZE_MESH_EXCHANGE(NM)
 
! Create arrays by which info is to exchanged across meshes
 
INTEGER IMIN,IMAX,JMIN,JMAX,KMIN,KMAX,NOM,IOR,IW
INTEGER, INTENT(IN) :: NM
TYPE (MESH_TYPE), POINTER :: M2,M
LOGICAL FOUND
 
M=>MESHES(NM)
ALLOCATE(MESHES(NM)%OMESH(NMESHES))
 
OTHER_MESH_LOOP: DO NOM=1,NMESHES
 
   IF (NOM==NM) CYCLE OTHER_MESH_LOOP
 
   M2=>MESHES(NOM)
   IMIN=0 
   IMAX=M2%IBP1
   JMIN=0 
   JMAX=M2%JBP1
   KMIN=0 
   KMAX=M2%KBP1
   NIC(NOM,NM) = 0
   FOUND = .FALSE.
   SEARCH_LOOP: DO IW=1,M%NEWC
      IF (M%IJKW(9,IW)/=NOM) CYCLE SEARCH_LOOP
      NIC(NOM,NM) = NIC(NOM,NM) + 1
      FOUND = .TRUE.
      IOR = M%IJKW(4,IW)
      SELECT CASE(IOR)
         CASE( 1) 
            IMIN=MAX(IMIN,M%IJKW(10,IW)-1)
         CASE(-1) 
            IMAX=MIN(IMAX,M%IJKW(13,IW))
         CASE( 2) 
            JMIN=MAX(JMIN,M%IJKW(11,IW)-1)
         CASE(-2) 
            JMAX=MIN(JMAX,M%IJKW(14,IW))
         CASE( 3) 
            KMIN=MAX(KMIN,M%IJKW(12,IW)-1)
         CASE(-3) 
            KMAX=MIN(KMAX,M%IJKW(15,IW))
      END SELECT
   ENDDO SEARCH_LOOP
 
   IF ( M2%XS>=M%XS .AND. M2%XF<=M%XF .AND. M2%YS>=M%YS .AND. M2%YF<=M%YF .AND. M2%ZS>=M%ZS .AND. M2%ZF<=M%ZF ) FOUND = .TRUE.
 
   IF (.NOT.FOUND) CYCLE OTHER_MESH_LOOP
 
   I_MIN(NOM,NM) = IMIN
   I_MAX(NOM,NM) = IMAX
   J_MIN(NOM,NM) = JMIN
   J_MAX(NOM,NM) = JMAX
   K_MIN(NOM,NM) = KMIN
   K_MAX(NOM,NM) = KMAX
 
   ALLOCATE(M%OMESH(NOM)% RHO(IMIN:IMAX,JMIN:JMAX,KMIN:KMAX))
   M%OMESH(NOM)%RHO = RHOA
   ALLOCATE(M%OMESH(NOM)%RHOS(IMIN:IMAX,JMIN:JMAX,KMIN:KMAX))
   M%OMESH(NOM)%RHOS = RHOA
   ALLOCATE(M%OMESH(NOM)%   H(IMIN:IMAX,JMIN:JMAX,KMIN:KMAX))
   M%OMESH(NOM)%H = 0.
   ALLOCATE(M%OMESH(NOM)%   U(IMIN:IMAX,JMIN:JMAX,KMIN:KMAX))
   M%OMESH(NOM)%U = U0
   ALLOCATE(M%OMESH(NOM)%   V(IMIN:IMAX,JMIN:JMAX,KMIN:KMAX))
   M%OMESH(NOM)%V = V0
   ALLOCATE(M%OMESH(NOM)%   W(IMIN:IMAX,JMIN:JMAX,KMIN:KMAX))
   M%OMESH(NOM)%W = W0
   ALLOCATE(M%OMESH(NOM)% FVX(IMIN:IMAX,JMIN:JMAX,KMIN:KMAX))
   ALLOCATE(M%OMESH(NOM)% FVY(IMIN:IMAX,JMIN:JMAX,KMIN:KMAX))
   ALLOCATE(M%OMESH(NOM)% FVZ(IMIN:IMAX,JMIN:JMAX,KMIN:KMAX))
 
   IF (N_SPECIES>0) THEN
      ALLOCATE(M%OMESH(NOM)%  YY(IMIN:IMAX,JMIN:JMAX,KMIN:KMAX,N_SPECIES))
      ALLOCATE(M%OMESH(NOM)% YYS(IMIN:IMAX,JMIN:JMAX,KMIN:KMAX,N_SPECIES))
      DO N=1,N_SPECIES
      M%OMESH(NOM)%YY(:,:,:,N)  = SPECIES(N)%YY0
      M%OMESH(NOM)%YYS(:,:,:,N) = SPECIES(N)%YY0
      ENDDO
   ENDIF
 
   ! Wall arrays
 
   ALLOCATE(M%OMESH(NOM)%IJKW(15,M2%NEWC))
   ALLOCATE(M%OMESH(NOM)%BOUNDARY_TYPE(0:M2%NEWC))
   ALLOCATE(M%OMESH(NOM)%WALL(0:M2%NEWC))
 
   ! Particle and Droplet Orphan Arrays
 
   IF (DROPLET_FILE) THEN
      M%OMESH(NOM)%N_DROP_ORPHANS = 0
      M%OMESH(NOM)%N_DROP_ORPHANS_DIM = N_DROP_ADOPT_MAX
      ALLOCATE(M%OMESH(NOM)%DROPLET(M%OMESH(NOM)%N_DROP_ORPHANS_DIM), STAT=IZERO)
      CALL ChkMemErr('INIT','DROPLET',IZERO)
   ENDIF
 
ENDDO OTHER_MESH_LOOP
 
END SUBROUTINE INITIALIZE_MESH_EXCHANGE
 
 

SUBROUTINE DOUBLE_CHECK(NM)
 
! Double check exchange pairs
 
INTEGER NOM
INTEGER, INTENT(IN) :: NM
TYPE (MESH_TYPE), POINTER :: M2,M
 
M=>MESHES(NM)
 
OTHER_MESH_LOOP: DO NOM=1,NMESHES
 
   IF (NOM==NM) CYCLE OTHER_MESH_LOOP
 
   IF (NIC(NM,NOM)==0 .AND. NIC(NOM,NM)>0) THEN
      M2=>MESHES(NOM)
      ALLOCATE(M%OMESH(NOM)%IJKW(15,M2%NEWC))
      ALLOCATE(M%OMESH(NOM)%BOUNDARY_TYPE(0:M2%NEWC))
      ALLOCATE(M%OMESH(NOM)%WALL(0:M2%NEWC))
   ENDIF
 
ENDDO OTHER_MESH_LOOP
 
END SUBROUTINE DOUBLE_CHECK
 
 

SUBROUTINE POST_RECEIVES(NM,CODE)
USE RADCONS, ONLY: NRA,NSB
 
INTEGER, INTENT(IN) :: NM,CODE
 
N_REQ = 0
 
OTHER_MESH_LOOP: DO NOM=1,NMESHES
 
   IF (NIC(NM,NOM)==0 .AND. NIC(NOM,NM)==0) CYCLE OTHER_MESH_LOOP
   IF (CODE>0 .AND. .NOT.ACTIVE_MESH(NOM)) CYCLE OTHER_MESH_LOOP
 
   IF (DEBUG) THEN
      WRITE(0,*) NM,' posting receives from ',NOM,' code=',code
      IF (CODE==0) WRITE(0,'(A,I2,A,I2,A,I5)') 'NIC(',NM,',',NOM,')=',NIC(NM,NOM)
   ENDIF
 
   M =>MESHES(NM)
   M4=>MESHES(NOM)
   M3=>MESHES(NM)%OMESH(NOM)
 
   RNODE = NOM-1
   TAG   = TAGS(NM,NOM,CODE)
 
   INITIALIZATION_IF: IF (CODE==0) THEN
 
      IF (NIC(NM,NOM)>0) THEN
         ALLOCATE(M3%REAL_RECV_PKG1(MAX(NIC(NM,NOM),NIC(NOM,NM))*(2+N_SPECIES)+1))
         ALLOCATE(M3%REAL_RECV_PKG2(MAX(NIC(NM,NOM),NIC(NOM,NM))*(2          )+1))
         ALLOCATE(M3%REAL_RECV_PKG3(MAX(NIC(NM,NOM),NIC(NOM,NM))*(2+N_SPECIES)+1))
         ALLOCATE(M3%REAL_RECV_PKG4(MAX(NIC(NM,NOM),NIC(NOM,NM))*(8          )+1))
         ALLOCATE(M3%REAL_RECV_PKG5((NRA*NSB+1)*NIC(NM,NOM)+1))
         ALLOCATE(M3%REAL_RECV_PKG7(MAX(NIC(NM,NOM),NIC(NOM,NM))*(6          )+1))
      ENDIF
 
      N_REQ = N_REQ+1
      CALL MPI_IRECV(M3%IJKW(1,1),15*M4%NEWC,MPI_INTEGER,RNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
 
      IF (NIC(NM,NOM)>0 .OR. NIC(NOM,NM)>0) THEN
         ALLOCATE(M3%REAL_RECV_PKG6(13*N_DROP_ADOPT_MAX))
         ALLOCATE(M3%INTG_RECV_PKG1( 2*N_DROP_ADOPT_MAX))
         ALLOCATE(M3%LOGI_RECV_PKG1( 2*N_DROP_ADOPT_MAX))
      ENDIF
 
   ENDIF INITIALIZATION_IF

   ! First posting for density and mass fraction
 
   IF (CODE==1 .AND. NIC(NM,NOM)>0) THEN
      N_REQ = N_REQ+1
      CALL MPI_IRECV(M3%REAL_RECV_PKG1(1),SIZE(M3%REAL_RECV_PKG1),MPI_DOUBLE_PRECISION,RNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
   ENDIF

   ! First posting for pressure

   IF (CODE==2 .AND. NIC(NM,NOM)>0) THEN
      N_REQ = N_REQ+1
      CALL MPI_IRECV(M3%REAL_RECV_PKG2(1),SIZE(M3%REAL_RECV_PKG2),MPI_DOUBLE_PRECISION,RNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
   ENDIF

   ! Second posting for density and mass fraction
 
   IF (CODE==3 .AND. NIC(NM,NOM)>0) THEN
      N_REQ=N_REQ+1
      CALL MPI_IRECV(M3%REAL_RECV_PKG3(1),SIZE(M3%REAL_RECV_PKG3),MPI_DOUBLE_PRECISION,RNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
   ENDIF

   ! BOUNDARY_TYPE

   IF (CODE==0 .OR. CODE==4) THEN
      N_REQ = N_REQ+1
      CALL MPI_IRECV(M3%BOUNDARY_TYPE(0),M4%NEWC+1,MPI_INTEGER,RNODE,TAG,MPI_COMM_WORLD, REQ(N_REQ),IERR)
   ENDIF

   ! Second posting for pressure and only posting for velocities

   IF (CODE==4 .AND. NIC(NM,NOM)>0) THEN
      N_REQ=N_REQ+1
      CALL MPI_IRECV(M3%REAL_RECV_PKG4(1),SIZE(M3%REAL_RECV_PKG4),MPI_DOUBLE_PRECISION,RNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
   ENDIF

   ! Radiation

   IF (CODE==4 .AND. EXCHANGE_RADIATION .AND. NIC(NM,NOM)>0) THEN
      IWW = NIC(NM,NOM)
      N_REQ=N_REQ+1
      CALL MPI_IRECV(M3%REAL_RECV_PKG5(1),(NRA*NSB+1)*IWW+1,MPI_DOUBLE_PRECISION,RNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
   ENDIF
 
   ! Droplets
 
   IF (CODE==4 .AND. DROPLET_FILE .AND. (NIC(NM,NOM)>0 .OR. NIC(NOM,NM)>0)) THEN

      N_REQ=N_REQ+1
      CALL MPI_IRECV(M3%N_DROP_ADOPT,1,MPI_INTEGER,RNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
 
      BUFFER_SIZE=13*N_DROP_ADOPT_MAX
      N_REQ=N_REQ+1
      CALL MPI_IRECV(M3%REAL_RECV_PKG6(1),BUFFER_SIZE,MPI_DOUBLE_PRECISION,RNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
      BUFFER_SIZE=2*N_DROP_ADOPT_MAX
      N_REQ=N_REQ+1
      CALL MPI_IRECV(M3%INTG_RECV_PKG1(1),BUFFER_SIZE,MPI_INTEGER,RNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
      BUFFER_SIZE=2*N_DROP_ADOPT_MAX
      N_REQ=N_REQ+1
      CALL MPI_IRECV(M3%LOGI_RECV_PKG1(1),BUFFER_SIZE,MPI_LOGICAL,RNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)

   ENDIF
 
ENDDO OTHER_MESH_LOOP
 
END SUBROUTINE POST_RECEIVES
 
 
 
SUBROUTINE MESH_EXCHANGE(CODE)
USE RADCONS, ONLY: NRA,NSB
 
! Exchange Information between Meshes
 
REAL(EB) :: TNOW
INTEGER, INTENT(IN) :: CODE
INTEGER NM,II,JJ,KK,LL,NC,N,NN,SNODE
INTEGER :: NN1,NN2
 
TNOW = SECOND()
 
NM = MYID+1

! Information about Mesh NM is packed into SEND packages and shipped out to the other meshes (machines) via MPI
 
SEND_OTHER_MESH_LOOP: DO NOM=1,NMESHES
 
   IF (NIC(NOM,NM)==0 .AND. NIC(NM,NOM)==0)  CYCLE SEND_OTHER_MESH_LOOP
 
   IF (CODE>0) THEN
      IF (.NOT.ACTIVE_MESH(NM) .OR. .NOT.ACTIVE_MESH(NOM))  CYCLE SEND_OTHER_MESH_LOOP
   ENDIF
 
   IF (DEBUG) THEN
      WRITE(0,*) NM,' sending data to ',NOM,' code=',CODE, ' tag=',TAGS(NM,NOM,CODE)
   ENDIF
 
   M =>MESHES(NM)
   M3=>MESHES(NM)%OMESH(NOM)
   M4=>MESHES(NOM)
 
   SNODE = NOM-1
   RNODE = NM-1
 
   TAG = TAGS(NM,NOM,CODE)

   ! Initial information exchange
 
   INITIALIZE_SEND_IF: IF (CODE==0) THEN
 
      IF (NIC(NOM,NM)>0) THEN
         ALLOCATE(M3%REAL_SEND_PKG1(MAX(NIC(NOM,NM),NIC(NM,NOM))*(2+N_SPECIES)+1))
         ALLOCATE(M3%REAL_SEND_PKG2(MAX(NIC(NOM,NM),NIC(NM,NOM))*(2          )+1))
         ALLOCATE(M3%REAL_SEND_PKG3(MAX(NIC(NOM,NM),NIC(NM,NOM))*(2+N_SPECIES)+1))
         ALLOCATE(M3%REAL_SEND_PKG4(MAX(NIC(NOM,NM),NIC(NM,NOM))*(8          )+1))
         ALLOCATE(M3%REAL_SEND_PKG5((NRA*NSB+1)*NIC(NOM,NM)+1))
         ALLOCATE(M3%REAL_SEND_PKG7(MAX(NIC(NOM,NM),NIC(NM,NOM))*(6          )+1))
      ENDIF
 
      N_REQ=N_REQ+1
      CALL MPI_ISEND(M%IJKW(1,1),15*M%NEWC, MPI_INTEGER,SNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
 
      IF (DROPLET_FILE .AND.  (NIC(NOM,NM)>0 .OR. NIC(NM,NOM)>0)) THEN
         ALLOCATE(M3%REAL_SEND_PKG6(13*N_DROP_ADOPT_MAX))
         ALLOCATE(M3%INTG_SEND_PKG1( 2*N_DROP_ADOPT_MAX))
         ALLOCATE(M3%LOGI_SEND_PKG1( 2*N_DROP_ADOPT_MAX))
      ENDIF
 
   ENDIF INITIALIZE_SEND_IF

   ! Exchange of density and species mass fractions following the PREDICTOR update
 
   IF (CODE==1 .AND. NIC(NOM,NM)>0) THEN
      LL = 0
      IWW = 0
      PACK_REAL_SEND_PKG1: DO IW=1,M4%NEWC
         IF (M3%IJKW(9,IW)/=NM .OR. M3%BOUNDARY_TYPE(IW)/=INTERPOLATED_BOUNDARY) CYCLE PACK_REAL_SEND_PKG1
         DO KK=M3%IJKW(12,IW),M3%IJKW(15,IW)
            DO JJ=M3%IJKW(11,IW),M3%IJKW(14,IW)
               DO II=M3%IJKW(10,IW),M3%IJKW(13,IW)
                  IWW = IWW + 1
                  M3%REAL_SEND_PKG1(LL+1) = REAL(IW,EB)
                  M3%REAL_SEND_PKG1(LL+2) = M%RHOS(II,JJ,KK)
                  IF (N_SPECIES>0) M3%REAL_SEND_PKG1(LL+3:LL+2+N_SPECIES) = M%YYS(II,JJ,KK,1:N_SPECIES)
                  LL = LL+2+N_SPECIES
               ENDDO
            ENDDO
         ENDDO
      ENDDO PACK_REAL_SEND_PKG1
      M3%REAL_SEND_PKG1(IWW*(2+N_SPECIES)+1) = -999.0_EB
      N_REQ=N_REQ+1
      CALL MPI_ISEND(M3%REAL_SEND_PKG1(1),IWW*(2+N_SPECIES)+1,MPI_DOUBLE_PRECISION,SNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
   ENDIF

   ! Send pressure information at the end of the PREDICTOR stage of the time step
 
   IF (CODE==2 .AND. NIC(NOM,NM)>0) THEN
      LL = 0
      IWW = 0
      PACK_REAL_SEND_PKG2: DO IW=1,M4%NEWC
         IF (M3%IJKW(9,IW)/=NM .OR. M3%BOUNDARY_TYPE(IW)/=INTERPOLATED_BOUNDARY) CYCLE PACK_REAL_SEND_PKG2
         DO KK=M3%IJKW(12,IW),M3%IJKW(15,IW)
            DO JJ=M3%IJKW(11,IW),M3%IJKW(14,IW)
               DO II=M3%IJKW(10,IW),M3%IJKW(13,IW)
                  IWW = IWW + 1
                  M3%REAL_SEND_PKG2(LL+1) = REAL(IW,EB)
                  M3%REAL_SEND_PKG2(LL+2) = M%H(II,JJ,KK)
                  LL = LL+2
               ENDDO
            ENDDO
         ENDDO
      ENDDO PACK_REAL_SEND_PKG2
      M3%REAL_SEND_PKG2(IWW*2+1) = -999.0_EB
      N_REQ=N_REQ+1
      CALL MPI_ISEND(M3%REAL_SEND_PKG2(1),IWW*2+1,MPI_DOUBLE_PRECISION,SNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
   ENDIF

   ! Exchange density and mass fraction following CORRECTOR update
 
   IF (CODE==3 .AND. NIC(NOM,NM)>0) THEN
      LL = 0
      IWW = 0
      PACK_REAL_SEND_PKG3: DO IW=1,M4%NEWC
         IF (M3%IJKW(9,IW)/=NM .OR. M3%BOUNDARY_TYPE(IW)/=INTERPOLATED_BOUNDARY) CYCLE PACK_REAL_SEND_PKG3
         DO KK=M3%IJKW(12,IW),M3%IJKW(15,IW)
            DO JJ=M3%IJKW(11,IW),M3%IJKW(14,IW)
               DO II=M3%IJKW(10,IW),M3%IJKW(13,IW)
                  IWW = IWW + 1
                  M3%REAL_SEND_PKG3(LL+1) = REAL(IW,EB)
                  M3%REAL_SEND_PKG3(LL+2) = M%RHO(II,JJ,KK)
                  IF (N_SPECIES>0) M3%REAL_SEND_PKG3(LL+3:LL+2+N_SPECIES) = M%YY(II,JJ,KK,1:N_SPECIES)
                  LL = LL+2+N_SPECIES
               ENDDO
             ENDDO
          ENDDO
      ENDDO PACK_REAL_SEND_PKG3
      M3%REAL_SEND_PKG3(IWW*(2+N_SPECIES)+1) = -999.0_EB
      N_REQ=N_REQ+1
      CALL MPI_ISEND(M3%REAL_SEND_PKG3(1),IWW*(2+N_SPECIES)+1,MPI_DOUBLE_PRECISION,SNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
   ENDIF

   ! Exchange BOUNDARY_TYPE following the CORRECTOR stage of the time step

   IF (CODE==0 .OR. CODE==4) THEN
      N_REQ=N_REQ+1
      CALL MPI_ISEND(M%BOUNDARY_TYPE(0),M%NEWC+1,MPI_INTEGER,SNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
   ENDIF

   ! Exchange pressure and velocities following CORRECTOR stage of time step
 
   IF (CODE==4 .AND. NIC(NOM,NM)>0) THEN
      LL = 0
      IWW = 0
      PACK_REAL_SEND_PKG4: DO IW=1,M4%NEWC
         IF (M3%IJKW(9,IW)/=NM .OR. M3%BOUNDARY_TYPE(IW)/=INTERPOLATED_BOUNDARY) CYCLE PACK_REAL_SEND_PKG4
         DO KK=M3%IJKW(12,IW),M3%IJKW(15,IW)
            DO JJ=M3%IJKW(11,IW),M3%IJKW(14,IW)
               DO II=M3%IJKW(10,IW),M3%IJKW(13,IW)
                  IWW = IWW + 1
                  M3%REAL_SEND_PKG4(LL+1) = REAL(IW,EB)
                  M3%REAL_SEND_PKG4(LL+2) = M%H(II,JJ,KK)
                  M3%REAL_SEND_PKG4(LL+3) = M%U(II,JJ,KK)
                  M3%REAL_SEND_PKG4(LL+4) = M%V(II,JJ,KK)
                  M3%REAL_SEND_PKG4(LL+5) = M%W(II,JJ,KK)
                  M3%REAL_SEND_PKG4(LL+6) = M%U(II-1,JJ,KK)
                  M3%REAL_SEND_PKG4(LL+7) = M%V(II,JJ-1,KK)
                  M3%REAL_SEND_PKG4(LL+8) = M%W(II,JJ,KK-1)
                  LL = LL+8
               ENDDO
             ENDDO
          ENDDO
      ENDDO PACK_REAL_SEND_PKG4
      M3%REAL_SEND_PKG4(IWW*8+1) = -999.0_EB
      N_REQ=N_REQ+1
      CALL MPI_ISEND(M3%REAL_SEND_PKG4(1),IWW*8+1,MPI_DOUBLE_PRECISION,SNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
   ENDIF

   ! Send out radiation info

   SEND_RADIATION: IF ( CODE==4 .AND. NIC(NOM,NM)>0 .AND. EXCHANGE_RADIATION) THEN
      IWW=0
      LL =0
      PACK_REAL_SEND_PKG5: DO IW=1,M4%NEWC
         IF (M3%IJKW(9,IW)/=NM .OR. M3%BOUNDARY_TYPE(IW)/=INTERPOLATED_BOUNDARY) CYCLE PACK_REAL_SEND_PKG5
         IWW = IWW+1
         LL  = LL +1
         M3%REAL_SEND_PKG5(LL) = REAL(IW,EB)
         DO NN2=1,NSB
            DO NN1=1,NRA
               LL = LL + 1
               M3%REAL_SEND_PKG5(LL) = M3%WALL(IW)%ILW(NN1,NN2)
            ENDDO
         ENDDO
      ENDDO PACK_REAL_SEND_PKG5
      M3%REAL_SEND_PKG5(LL+1) = -999.0_EB
      N_REQ=N_REQ+1
      CALL MPI_ISEND(M3%REAL_SEND_PKG5(1),(NRA*NSB+1)*IWW+1,MPI_DOUBLE_PRECISION,SNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
   ENDIF SEND_RADIATION
 
   ! Get Number of Droplet Orphans (droplets that have left other meshes and are waiting to be picked up)
 
   IF (CODE==4 .AND. DROPLET_FILE) THEN
      N_REQ=N_REQ+1
      CALL MPI_ISEND(M3%N_DROP_ORPHANS,1,MPI_INTEGER,SNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
   ENDIF
 
   ! Sending/Receiving Droplet Buffer Arrays
 
   IF_SEND_DROPLETS: IF (CODE==4 .AND. DROPLET_FILE) THEN 
 
      NC = 13
      DO N=1,MIN(M3%N_DROP_ORPHANS,N_DROP_ADOPT_MAX)
         M3%REAL_SEND_PKG6((N-1)*NC+1)  = M3%DROPLET(N)%X
         M3%REAL_SEND_PKG6((N-1)*NC+2)  = M3%DROPLET(N)%Y
         M3%REAL_SEND_PKG6((N-1)*NC+3)  = M3%DROPLET(N)%Z
         M3%REAL_SEND_PKG6((N-1)*NC+4)  = M3%DROPLET(N)%TMP
         M3%REAL_SEND_PKG6((N-1)*NC+5)  = M3%DROPLET(N)%U
         M3%REAL_SEND_PKG6((N-1)*NC+6)  = M3%DROPLET(N)%V
         M3%REAL_SEND_PKG6((N-1)*NC+7)  = M3%DROPLET(N)%W
         M3%REAL_SEND_PKG6((N-1)*NC+8)  = M3%DROPLET(N)%R
         M3%REAL_SEND_PKG6((N-1)*NC+9)  = M3%DROPLET(N)%PWT
         M3%REAL_SEND_PKG6((N-1)*NC+10) = M3%DROPLET(N)%A_X
         M3%REAL_SEND_PKG6((N-1)*NC+11) = M3%DROPLET(N)%A_Y
         M3%REAL_SEND_PKG6((N-1)*NC+12) = M3%DROPLET(N)%A_Z
         M3%REAL_SEND_PKG6((N-1)*NC+13) = M3%DROPLET(N)%T
      ENDDO
      BUFFER_SIZE = NC*MIN(M3%N_DROP_ORPHANS,N_DROP_ADOPT_MAX)
      BUFFER_SIZE = MAX(1,BUFFER_SIZE)
      N_REQ=N_REQ+1
      CALL MPI_ISEND(M3%REAL_SEND_PKG6(1),BUFFER_SIZE,MPI_DOUBLE_PRECISION,SNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
 
      NC = 2
      DO N=1,MIN(M3%N_DROP_ORPHANS,N_DROP_ADOPT_MAX)
         M3%INTG_SEND_PKG1((N-1)*NC+1) = M3%DROPLET(N)%IOR
         M3%INTG_SEND_PKG1((N-1)*NC+2) = M3%DROPLET(N)%CLASS
      ENDDO
      BUFFER_SIZE = NC*MIN(M3%N_DROP_ORPHANS,N_DROP_ADOPT_MAX)
      BUFFER_SIZE = MAX(1,BUFFER_SIZE)
      N_REQ=N_REQ+1
      CALL MPI_ISEND(M3%INTG_SEND_PKG1(1),BUFFER_SIZE,MPI_INTEGER,SNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
 
      NC = 2
      DO N=1,MIN(M3%N_DROP_ORPHANS,N_DROP_ADOPT_MAX)
         M3%LOGI_SEND_PKG1((N-1)*NC+1) = M3%DROPLET(N)%SHOW
         M3%LOGI_SEND_PKG1((N-1)*NC+2) = M3%DROPLET(N)%SPLAT
      ENDDO
      BUFFER_SIZE = NC*MIN(M3%N_DROP_ORPHANS,N_DROP_ADOPT_MAX)
      BUFFER_SIZE = MAX(1,BUFFER_SIZE)
      N_REQ=N_REQ+1
      CALL MPI_ISEND(M3%LOGI_SEND_PKG1(1),BUFFER_SIZE,MPI_LOGICAL,SNODE,TAG,MPI_COMM_WORLD,REQ(N_REQ),IERR)
 
      M3%N_DROP_ORPHANS = 0
 
   ENDIF IF_SEND_DROPLETS
 
ENDDO SEND_OTHER_MESH_LOOP
 

! Information from Mesh NM is RECV'ed by Mesh NOM.  NOM is the receiver, NM is the sender.
 
CALL MPI_WAITALL(N_REQ,REQ(1:N_REQ),ARRAY_OF_STATUSES,IERR)
 
NOM = MYID+1
 
MESH_LOOP: DO NM=1,NMESHES
 
   IF (NIC(NOM,NM)==0 .AND. NIC(NM,NOM)==0) CYCLE MESH_LOOP
 
   IF (CODE>0) THEN
      IF (.NOT.ACTIVE_MESH(NM) .OR. .NOT.ACTIVE_MESH(NOM))  CYCLE MESH_LOOP
   ENDIF
 
   IF (DEBUG) THEN
      WRITE(0,*) NOM,' receiving data from ',NM, ' code=',CODE,' tag=',TAGS(NM,NOM,CODE)
   ENDIF
 
   M =>MESHES(NM)
   M2=>MESHES(NOM)%OMESH(NM)
   M4=>MESHES(NOM)
 
   SNODE = NOM-1
   RNODE = NM-1
 
   TAG = TAGS(NM,NOM,CODE)

   ! Receive information before the time stepping starts needed for radiation exchange
 
   IF (CODE==0 .AND. RADIATION) THEN
      DO IW=1,M%NEWC
         IF (M2%IJKW(9,IW)==NOM) THEN
            ALLOCATE(M2%WALL(IW)%ILW(NRA,NSB))
            M2%WALL(IW)%ILW = SIGMA*TMPA4*RPI
         ENDIF
      ENDDO
   ENDIF 
 
   ! Unpack densities and species mass fractions following PREDICTOR exchange

   IF (CODE==1 .AND. NIC(NOM,NM)>0) THEN
      LL = 0
      UNPACK_REAL_RECV_PKG1: DO 
         IW = NINT(M2%REAL_RECV_PKG1(LL+1))
         IF (IW==-999) EXIT UNPACK_REAL_RECV_PKG1
         DO KK=M4%IJKW(12,IW),M4%IJKW(15,IW)
            DO JJ=M4%IJKW(11,IW),M4%IJKW(14,IW)
               DO II=M4%IJKW(10,IW),M4%IJKW(13,IW)
                  M2%RHOS(II,JJ,KK) = M2%REAL_RECV_PKG1(LL+2)
                  IF (N_SPECIES>0) M2%YYS(II,JJ,KK,1:N_SPECIES)= M2%REAL_RECV_PKG1(LL+3:LL+2+N_SPECIES)
                  LL = LL+2+N_SPECIES
               ENDDO
            ENDDO
         ENDDO
      ENDDO UNPACK_REAL_RECV_PKG1
   ENDIF 

   ! Unpack pressure following PREDICTOR stage of time step
 
   IF (CODE==2 .AND. NIC(NOM,NM)>0) THEN
      LL = 0
      UNPACK_REAL_RECV_PKG2: DO
         IW = NINT(M2%REAL_RECV_PKG2(LL+1))
         IF (IW==-999) EXIT UNPACK_REAL_RECV_PKG2
         DO KK=M4%IJKW(12,IW),M4%IJKW(15,IW)
            DO JJ=M4%IJKW(11,IW),M4%IJKW(14,IW)
               DO II=M4%IJKW(10,IW),M4%IJKW(13,IW)
                  M2%H(II,JJ,KK) = M2%REAL_RECV_PKG2(LL+2)
                  LL = LL+2
               ENDDO
            ENDDO
         ENDDO
      ENDDO UNPACK_REAL_RECV_PKG2
   ENDIF 

   ! Unpack density and species mass fractions following CORRECTOR update

   IF (CODE==3 .AND. NIC(NOM,NM)>0) THEN
      LL = 0
      UNPACK_REAL_RECV_PKG3: DO 
         IW = NINT(M2%REAL_RECV_PKG3(LL+1))
         IF (IW==-999) EXIT UNPACK_REAL_RECV_PKG3
         DO KK=M4%IJKW(12,IW),M4%IJKW(15,IW)
            DO JJ=M4%IJKW(11,IW),M4%IJKW(14,IW)
               DO II=M4%IJKW(10,IW),M4%IJKW(13,IW)
                  M2%RHO(II,JJ,KK) = M2%REAL_RECV_PKG3(LL+2)
                  IF (N_SPECIES>0) M2%YY(II,JJ,KK,1:N_SPECIES)= M2%REAL_RECV_PKG3(LL+3:LL+2+N_SPECIES)
                  LL = LL+2+N_SPECIES
               ENDDO
            ENDDO
         ENDDO
      ENDDO UNPACK_REAL_RECV_PKG3
   ENDIF

   ! Unpack pressure and velocities at the end of the CORRECTOR stage of the time step
 
   IF (CODE==4 .AND. NIC(NOM,NM)>0) THEN
      LL = 0
      UNPACK_REAL_RECV_PKG4: DO
         IW = NINT(M2%REAL_RECV_PKG4(LL+1))
         IF (IW==-999) EXIT UNPACK_REAL_RECV_PKG4
         DO KK=M4%IJKW(12,IW),M4%IJKW(15,IW)
            DO JJ=M4%IJKW(11,IW),M4%IJKW(14,IW)
               DO II=M4%IJKW(10,IW),M4%IJKW(13,IW)
                  M2%H(II,JJ,KK)   = M2%REAL_RECV_PKG4(LL+2)
                  M2%U(II,JJ,KK)   = M2%REAL_RECV_PKG4(LL+3)
                  M2%V(II,JJ,KK)   = M2%REAL_RECV_PKG4(LL+4)
                  M2%W(II,JJ,KK)   = M2%REAL_RECV_PKG4(LL+5)
                  M2%U(II-1,JJ,KK) = M2%REAL_RECV_PKG4(LL+6)
                  M2%V(II,JJ-1,KK) = M2%REAL_RECV_PKG4(LL+7)
                  M2%W(II,JJ,KK-1) = M2%REAL_RECV_PKG4(LL+8)
                  LL = LL+8
               ENDDO
            ENDDO
         ENDDO
      ENDDO UNPACK_REAL_RECV_PKG4
   ENDIF

   ! Unpack radiation information at the end of the CORRECTOR stage of the time step

   RECEIVE_RADIATION: IF ( CODE==4 .AND. NIC(NOM,NM)>0 .AND. EXCHANGE_RADIATION) THEN
      LL = 0
      UNPACK_REAL_RECV_PKG5: DO 
         LL  = LL + 1
         IW = NINT(M2%REAL_RECV_PKG5(LL))
         IF (IW==-999) EXIT UNPACK_REAL_RECV_PKG5
         DO NN2=1,NSB
            DO NN1=1,NRA
               LL = LL + 1
               M4%WALL(IW)%ILW(NN1,NN2) = M2%REAL_RECV_PKG5(LL)
            ENDDO
         ENDDO
      ENDDO UNPACK_REAL_RECV_PKG5
   ENDIF RECEIVE_RADIATION
 
   ! Get Number of Droplet Orphans
 
   IF (CODE==4 .AND. DROPLET_FILE) THEN
      M2%N_DROP_ADOPT = MIN(M2%N_DROP_ADOPT,N_DROP_ADOPT_MAX)
      IF (M4%NLP+M2%N_DROP_ADOPT>M4%NLPDIM) CALL RE_ALLOCATE_DROPLETS(1,NOM,0,N_DROP_ADOPT_MAX)
   ENDIF
 
   ! Sending/Receiving Droplet Buffer Arrays
 
   IF_RECEIVE_DROPLETS: IF (CODE==4 .AND. DROPLET_FILE) THEN 
      IF_DROPLETS_SENT: IF (M2%N_DROP_ADOPT>0) THEN
 
         NC = 13
         DO N=M4%NLP+1,M4%NLP+M2%N_DROP_ADOPT
            NN = N-M4%NLP-1
            M4%DROPLET(N)%X   = M2%REAL_RECV_PKG6((NN)*NC+1) 
            M4%DROPLET(N)%Y   = M2%REAL_RECV_PKG6((NN)*NC+2) 
            M4%DROPLET(N)%Z   = M2%REAL_RECV_PKG6((NN)*NC+3) 
            M4%DROPLET(N)%TMP = M2%REAL_RECV_PKG6((NN)*NC+4) 
            M4%DROPLET(N)%U   = M2%REAL_RECV_PKG6((NN)*NC+5) 
            M4%DROPLET(N)%V   = M2%REAL_RECV_PKG6((NN)*NC+6) 
            M4%DROPLET(N)%W   = M2%REAL_RECV_PKG6((NN)*NC+7) 
            M4%DROPLET(N)%R   = M2%REAL_RECV_PKG6((NN)*NC+8) 
            M4%DROPLET(N)%PWT = M2%REAL_RECV_PKG6((NN)*NC+9) 
            M4%DROPLET(N)%A_X = M2%REAL_RECV_PKG6((NN)*NC+10) 
            M4%DROPLET(N)%A_Y = M2%REAL_RECV_PKG6((NN)*NC+11) 
            M4%DROPLET(N)%A_Z = M2%REAL_RECV_PKG6((NN)*NC+12) 
            M4%DROPLET(N)%T   = M2%REAL_RECV_PKG6((NN)*NC+13) 
         ENDDO
 
         NC = 2
         DO N=M4%NLP+1,M4%NLP+M2%N_DROP_ADOPT
            NN = N-M4%NLP-1
            M4%DROPLET(N)%IOR    = M2%INTG_RECV_PKG1((NN)*NC+1) 
            M4%DROPLET(N)%CLASS  = M2%INTG_RECV_PKG1((NN)*NC+2) 
         ENDDO
 
         NC = 2
         DO N=M4%NLP+1,M4%NLP+M2%N_DROP_ADOPT
            NN = N-M4%NLP-1
            M4%DROPLET(N)%SHOW  = M2%LOGI_RECV_PKG1((NN)*NC+1)  
            M4%DROPLET(N)%SPLAT = M2%LOGI_RECV_PKG1((NN)*NC+2)  
         ENDDO
 
         M4%NLP = M4%NLP + M2%N_DROP_ADOPT
 
      ENDIF IF_DROPLETS_SENT
   ENDIF IF_RECEIVE_DROPLETS
 
ENDDO MESH_LOOP
 
TUSED(11,:)=TUSED(11,:) + SECOND() - TNOW
END SUBROUTINE MESH_EXCHANGE
 
 

SUBROUTINE WRITE_STRINGS
 
! Write character strings out to the .smv file
 
INTEGER :: N,NOM,N_STRINGS_DUM
CHARACTER(80), ALLOCATABLE, DIMENSION(:) :: STRING_DUM
 
! All meshes send their STRINGs to node 0
 
IF (MYID>0) THEN
   CALL MPI_SEND(MESHES(MYID+1)%N_STRINGS,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   IF (MESHES(MYID+1)%N_STRINGS>0)  CALL MPI_SEND(MESHES(MYID+1)%STRING(1),MESHES(MYID+1)%N_STRINGS*80, &
       MPI_CHARACTER,0,MYID,MPI_COMM_WORLD,IERR)
ENDIF
 
! Node 0 receives the STRINGs and writes them to the .smv file
 
IF (MYID==0) THEN
   DO N=1,MESHES(1)%N_STRINGS
      WRITE(LU_SMV,'(A)') TRIM(MESHES(1)%STRING(N))
   ENDDO
   OTHER_MESH_LOOP: DO NOM=2,NMESHES 
      CALL MPI_RECV(N_STRINGS_DUM,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      IF (N_STRINGS_DUM>0) THEN
         ALLOCATE(STRING_DUM(N_STRINGS_DUM))
         CALL MPI_RECV(STRING_DUM(1),N_STRINGS_DUM*80, MPI_CHARACTER,NOM-1,NOM-1, MPI_COMM_WORLD,STATUS,IERR)
         DO N=1,N_STRINGS_DUM
            WRITE(LU_SMV,'(A)') TRIM(STRING_DUM(N))
         ENDDO
         DEALLOCATE(STRING_DUM)
      ENDIF
   ENDDO OTHER_MESH_LOOP
ENDIF
 
! All STRING arrays are zeroed out
 
MESHES(MYID+1)%N_STRINGS = 0
 
END SUBROUTINE WRITE_STRINGS
 
 

SUBROUTINE EXCHANGE_DIAGNOSTICS
 
INTEGER  :: NOM,NM,NECYC
REAL(EB) :: T_SUM, TNOW
 
TNOW = SECOND()
NM = MYID+1

T_SUM = 0.
SUM_LOOP: DO I=2,N_TIMERS
   IF (I==9 .OR. I==10) CYCLE SUM_LOOP
   T_SUM = T_SUM + TUSED(I,NM)
ENDDO SUM_LOOP
 
NECYC          = MAX(1,NTCYC(NM)-NCYC(NM))
T_PER_STEP(NM) = (T_SUM-T_ACCUM(NM))/REAL(NECYC,EB)
T_ACCUM(NM)    = T_SUM
NCYC(NM)       = NTCYC(NM)
 
CALL MPI_GATHER(T(MYID+1),      1,MPI_DOUBLE_PRECISION,T,      1,MPI_DOUBLE_PRECISION,0,MPI_COMM_WORLD,IERR)
CALL MPI_GATHER(T_ACCUM(MYID+1),1,MPI_DOUBLE_PRECISION,T_ACCUM,1,MPI_DOUBLE_PRECISION,0,MPI_COMM_WORLD,IERR)
CALL MPI_GATHER(T_PER_STEP(MYID+1),1,MPI_DOUBLE_PRECISION, T_PER_STEP,1,MPI_DOUBLE_PRECISION,0, MPI_COMM_WORLD,IERR)
CALL MPI_GATHER(NTCYC(MYID+1),1,MPI_INTEGER, NTCYC,1,MPI_INTEGER,0, MPI_COMM_WORLD,IERR)
CALL MPI_GATHER(HRR(MYID+1),1,MPI_DOUBLE_PRECISION, HRR,1,MPI_DOUBLE_PRECISION,0, MPI_COMM_WORLD,IERR)
CALL MPI_GATHER(RHRR(MYID+1),1,MPI_DOUBLE_PRECISION, RHRR,1,MPI_DOUBLE_PRECISION,0, MPI_COMM_WORLD,IERR)
 
IF (MYID>0) THEN
 
   CALL MPI_SEND(MESHES(MYID+1)%DT,1,MPI_DOUBLE_PRECISION, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%CFL,1,MPI_DOUBLE_PRECISION, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%DIVMX,1,MPI_DOUBLE_PRECISION, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%DIVMN,1,MPI_DOUBLE_PRECISION, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%RESMAX,1,MPI_DOUBLE_PRECISION, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%POIS_PTB,1,MPI_DOUBLE_PRECISION, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%POIS_ERR,1,MPI_DOUBLE_PRECISION, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%VN,1,MPI_DOUBLE_PRECISION, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%ICFL,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%JCFL,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%KCFL,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%IMX,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%JMX,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%KMX,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%IMN,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%JMN,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%KMN,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%IRM,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%JRM,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%KRM,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%I_VN,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%J_VN,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%K_VN,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)
   CALL MPI_SEND(MESHES(MYID+1)%NLP,1,MPI_INTEGER, 0,1,MPI_COMM_WORLD,IERR)

ELSE 

   DO NOM=2,NMESHES
      CALL MPI_RECV(MESHES(NOM)%DT,1,MPI_DOUBLE_PRECISION, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%CFL,1,MPI_DOUBLE_PRECISION, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%DIVMX,1,MPI_DOUBLE_PRECISION, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%DIVMN,1,MPI_DOUBLE_PRECISION, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%RESMAX,1,MPI_DOUBLE_PRECISION, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%POIS_PTB,1,MPI_DOUBLE_PRECISION, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%POIS_ERR,1,MPI_DOUBLE_PRECISION, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%VN,1,MPI_DOUBLE_PRECISION, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%ICFL,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%JCFL,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%KCFL,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%IMX,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%JMX,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%KMX,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%IMN,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%JMN,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%KMN,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%IRM,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%JRM,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%KRM,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%I_VN,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%J_VN,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%K_VN,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
      CALL MPI_RECV(MESHES(NOM)%NLP,1,MPI_INTEGER, NOM-1,1,MPI_COMM_WORLD,STATUS,IERR)
   ENDDO
 
ENDIF
 
TUSED(11,:) = TUSED(11,:) + SECOND() - TNOW
END SUBROUTINE EXCHANGE_DIAGNOSTICS


SUBROUTINE CORRECT_PRESSURE(CODE) !!!! Experimental Code, DO NOT USE

REAL(EB), DIMENSION(NMESHES*4,NMESHES*4) :: A,A_LOC
REAL(EB), DIMENSION(NMESHES*4) :: B,B_LOC
TYPE (MESH_TYPE), POINTER :: M
TYPE (OMESH_TYPE), POINTER :: M3
INTEGER :: IERROR,NM,SNODE,RNODE,LL,II,JJ,KK,CODE,I,J,IOR

! Post Receives for arrays containing Pressure boundary info

N_PREQ = 0
NM = MYID+1

OTHER_MESH_LOOP: DO NOM=1,NMESHES

   IF (NIC(NM,NOM)==0 .AND. NIC(NOM,NM)==0) CYCLE OTHER_MESH_LOOP
   IF (.NOT.ACTIVE_MESH(NOM)) CYCLE OTHER_MESH_LOOP

   IF (DEBUG) THEN
      WRITE(0,*) NM,' posting pressure receives from ',NOM,' code=',code
      IF (CODE==0) WRITE(0,'(A,I2,A,I2,A,I5)') 'NIC(',NM,',',NOM,')=',NIC(NM,NOM)
   ENDIF

   M =>MESHES(NM)
   M3=>MESHES(NM)%OMESH(NOM)

   RNODE = NOM-1
   TAG   = TAGS(NM,NOM,CODE)

   N_PREQ = N_PREQ+1
   CALL MPI_IRECV(M3%REAL_RECV_PKG7(1),NIC(NM,NOM)*6+1, MPI_DOUBLE_PRECISION,RNODE,TAG,MPI_COMM_WORLD, PREQ(N_PREQ),IERR)

ENDDO OTHER_MESH_LOOP

! Exchange boundary values of H and FVX, FVY, FVZ

SEND_OTHER_MESH_LOOP: DO NOM=1,NMESHES

   IF (NIC(NOM,NM)==0 .AND. NIC(NM,NOM)==0) CYCLE SEND_OTHER_MESH_LOOP

   M =>MESHES(NM)
   M3=>MESHES(NM)%OMESH(NOM)
   M4=>MESHES(NOM)

   SNODE = NOM-1
   RNODE = NM-1

   TAG = TAGS(NM,NOM,CODE)

   LL = 0
   IWW = 0
   PACK_REAL_SEND_PKG7: DO IW=1,M4%NEWC
      IF (M3%IJKW(9,IW)/=NM .OR. M3%BOUNDARY_TYPE(IW)/=INTERPOLATED_BOUNDARY) CYCLE PACK_REAL_SEND_PKG7
      IWW = IWW + 1
      II = M3%IJKW(10,IW)
      JJ = M3%IJKW(11,IW)
      KK = M3%IJKW(12,IW)
      IOR = M3%IJKW(4,IW)
      M3%REAL_SEND_PKG7(LL+1) = REAL(IW,EB)
      M3%REAL_SEND_PKG7(LL+2) = M%H(II,JJ,KK)
      SELECT CASE(IOR)
         CASE(-1) 
            M3%REAL_SEND_PKG7(LL+3) = M%H(II-1,JJ,KK)
            M3%REAL_SEND_PKG7(LL+4) = M%FVX(II-1,JJ,KK)
         CASE( 1) 
            M3%REAL_SEND_PKG7(LL+3) = M%H(II+1,JJ,KK)
            M3%REAL_SEND_PKG7(LL+4) = M%FVX(II,JJ,KK)
         CASE(-2) 
            M3%REAL_SEND_PKG7(LL+3) = M%H(II,JJ-1,KK)
            M3%REAL_SEND_PKG7(LL+5) = M%FVY(II,JJ-1,KK)
         CASE( 2) 
            M3%REAL_SEND_PKG7(LL+3) = M%H(II,JJ+1,KK)
            M3%REAL_SEND_PKG7(LL+5) = M%FVY(II,JJ,KK)
         CASE(-3) 
            M3%REAL_SEND_PKG7(LL+3) = M%H(II,JJ,KK-1)
            M3%REAL_SEND_PKG7(LL+6) = M%FVZ(II,JJ,KK-1)
         CASE( 3) 
            M3%REAL_SEND_PKG7(LL+3) = M%H(II,JJ,KK+1)
            M3%REAL_SEND_PKG7(LL+6) = M%FVZ(II,JJ,KK)
      END SELECT
      LL = LL+6
   ENDDO PACK_REAL_SEND_PKG7
   M3%REAL_SEND_PKG7(IWW*6+1) = -999.0_EB
   N_PREQ=N_PREQ+1
   CALL MPI_ISEND(M3%REAL_SEND_PKG7(1),IWW*6+1,MPI_DOUBLE_PRECISION,SNODE,TAG,MPI_COMM_WORLD,PREQ(N_PREQ),IERR)

   IF (DEBUG) THEN
      WRITE(0,*) NM,' sending P data to ',NOM, ' tag=',TAGS(NM,NOM,CODE),' PREQ=',PREQ(N_PREQ)
   ENDIF

ENDDO SEND_OTHER_MESH_LOOP

CALL MPI_WAITALL(N_PREQ,PREQ(1:N_PREQ),ARRAY_OF_STATUSES2,IERR)

NOM = MYID+1

RECEIVE_OTHER_MESH_LOOP: DO NM=1,NMESHES

IF (NIC(NOM,NM)==0 .AND. NIC(NM,NOM)==0)  CYCLE RECEIVE_OTHER_MESH_LOOP

M =>MESHES(NM)
M2=>MESHES(NOM)%OMESH(NM)
M4=>MESHES(NOM)

SNODE = NOM-1
RNODE = NM-1
TAG = TAGS(NM,NOM,CODE)

LL = 0
UNPACK_REAL_RECV_PKG7: DO
   IW = NINT(M2%REAL_RECV_PKG7(LL+1))
   IF (IW==-999) EXIT UNPACK_REAL_RECV_PKG7
   II = M4%IJKW(10,IW)
   JJ = M4%IJKW(11,IW)
   KK = M4%IJKW(12,IW)
   IOR = M4%IJKW(4,IW)
   M2%H(II,JJ,KK)                = M2%REAL_RECV_PKG7(LL+2)
   SELECT CASE(IOR)
      CASE(-1) 
         M2%H(II-1,JJ,KK)   = M2%REAL_RECV_PKG7(LL+3)
         M2%FVX(II-1,JJ,KK) = M2%REAL_RECV_PKG7(LL+4)
      CASE( 1) 
         M2%H(II+1,JJ,KK)   = M2%REAL_RECV_PKG7(LL+3)
         M2%FVX(II,JJ,KK)   = M2%REAL_RECV_PKG7(LL+4)
      CASE(-2) 
         M2%H(II,JJ-1,KK)   = M2%REAL_RECV_PKG7(LL+3)
         M2%FVY(II,JJ-1,KK) = M2%REAL_RECV_PKG7(LL+5)
      CASE( 2) 
         M2%H(II,JJ+1,KK)   = M2%REAL_RECV_PKG7(LL+3)
         M2%FVY(II,JJ,KK)   = M2%REAL_RECV_PKG7(LL+5)
      CASE(-3) 
         M2%H(II,JJ,KK-1)   = M2%REAL_RECV_PKG7(LL+3)
         M2%FVZ(II,JJ,KK-1) = M2%REAL_RECV_PKG7(LL+6)
      CASE( 3) 
         M2%H(II,JJ,KK+1)   = M2%REAL_RECV_PKG7(LL+3)
         M2%FVZ(II,JJ,KK)   = M2%REAL_RECV_PKG7(LL+6)
   END SELECT
   LL = LL+6
ENDDO UNPACK_REAL_RECV_PKG7

ENDDO RECEIVE_OTHER_MESH_LOOP

! Construct "correction matrix" A

A = 0.
B = 0.
A_LOC = 0.
B_LOC = 0.

MESH_LOOP_1: DO NM=MYID+1,NMESHES,NUMPROCS
   CALL COMPUTE_A_B(A_LOC,B_LOC,NM)
ENDDO MESH_LOOP_1

CALL MPI_ALLREDUCE(A_LOC(1,1),A(1,1),(NMESHES*4)**2, MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
CALL MPI_ALLREDUCE(B_LOC(1),B(1),(NMESHES*4), MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)

IF (MYID==0) THEN
   DO I=1,4*NMESHES
      WRITE(0,'(16F7.1,3X,F7.1)') (A(I,J),J=1,4*NMESHES),B(I)
   ENDDO
ENDIF

CALL GAUSSJ(A,NMESHES*4,NMESHES*4,B,1,1,IERROR)

IF (MYID==0) THEN
   IF (IERROR>0) WRITE(0,*) ' IERROR= ',IERROR
   WRITE(0,*)
   DO I=1,4*NMESHES
      WRITE(0,'(F12.5)') B(I)
   ENDDO
   WRITE(0,*)
ENDIF

MESH_LOOP_2: DO NM=MYID+1,NMESHES,NUMPROCS
   CALL UPDATE_PRESSURE(B,NM)
ENDDO MESH_LOOP_2

END SUBROUTINE CORRECT_PRESSURE


SUBROUTINE DUMP_GLOBAL_OUTPUTS(T)

! Dump HRR data to CHID_hrr.csv, MASS data to CHID_mass.csv, DEVICE data to _devc.csv

REAL(EB) :: T
INTEGER :: N

! Dump out HRR info  after first "gathering" data to node 0

IF_DUMP_HRR: IF (T>=HRR_CLOCK) THEN
   CALL MPI_ALLGATHER(HRR_COUNT(MYID+1),1,MPI_DOUBLE_PRECISION, HRR_COUNT,1,MPI_DOUBLE_PRECISION,MPI_COMM_WORLD,IERR)
   IF (MINVAL(HRR_COUNT,MASK=.NOT.EVACUATION_ONLY)>0._EB) THEN
      CALL MPI_GATHER(HRR_SUM(MYID+1), 1, MPI_DOUBLE_PRECISION, HRR_SUM, 1,MPI_DOUBLE_PRECISION,0,MPI_COMM_WORLD,IERR)
      CALL MPI_GATHER(RHRR_SUM(MYID+1),1, MPI_DOUBLE_PRECISION, RHRR_SUM,1,MPI_DOUBLE_PRECISION,0,MPI_COMM_WORLD,IERR)
      CALL MPI_GATHER(CHRR_SUM(MYID+1),1, MPI_DOUBLE_PRECISION, CHRR_SUM,1,MPI_DOUBLE_PRECISION,0,MPI_COMM_WORLD,IERR)
      CALL MPI_GATHER(FHRR_SUM(MYID+1),1, MPI_DOUBLE_PRECISION, FHRR_SUM,1,MPI_DOUBLE_PRECISION,0,MPI_COMM_WORLD,IERR)
      CALL MPI_GATHER(MLR_SUM(MYID+1), 1, MPI_DOUBLE_PRECISION, MLR_SUM, 1,MPI_DOUBLE_PRECISION,0,MPI_COMM_WORLD,IERR)
      IF (MYID==0) CALL DUMP_HRR(T)
      HRR_CLOCK = HRR_CLOCK + DT_HRR
      HRR_SUM   = 0._EB
      RHRR_SUM  = 0._EB
      CHRR_SUM  = 0._EB
      FHRR_SUM  = 0._EB
      MLR_SUM   = 0._EB
      HRR_COUNT = 0._EB
   ENDIF
ENDIF IF_DUMP_HRR

! Dump out Evac info
IF (MYID==0) CALL EVAC_CSV(T)


! Dump out Mass info after first "gathering" data to node 0

IF_DUMP_MASS: IF (T>=MINT_CLOCK) THEN
   CALL MPI_ALLGATHER(MINT_COUNT(MYID+1),1,MPI_DOUBLE_PRECISION, MINT_COUNT,1,MPI_DOUBLE_PRECISION,MPI_COMM_WORLD,IERR)
   IF (MINVAL(MINT_COUNT,MASK=.NOT.EVACUATION_ONLY)>0.) THEN
      CALL MPI_GATHER(MINT_SUM(0,MYID+1),21,MPI_DOUBLE_PRECISION, MINT_SUM,21,MPI_DOUBLE_PRECISION,0,MPI_COMM_WORLD, IERR)
      IF (MYID==0) CALL DUMP_MASS(T)
      MINT_CLOCK    = MINT_CLOCK + DT_MASS
      MINT_SUM   = 0._EB
      MINT_COUNT = 0._EB
   ENDIF
ENDIF IF_DUMP_MASS

! Exchange DEVICE parameters among meshes and dump out DEVICE info after first "gathering" data to node 0
 
IF (N_DEVC>0) THEN
  
   ! Exchange the CURRENT_STATE of each DEViCe

   STATE_LOC(1:N_DEVC) = .FALSE.  ! _LOC is a temporary array that holds the STATE value for the devices on each node
   DO N=1,N_DEVC
      IF (DEVICE(N)%MESH==MYID+1) STATE_LOC(N) = DEVICE(N)%CURRENT_STATE 
   ENDDO
   CALL MPI_ALLREDUCE(STATE_LOC(1),STATE_GLB(1),N_DEVC,MPI_LOGICAL,MPI_LXOR,MPI_COMM_WORLD,IERR)
   DEVICE(1:N_DEVC)%CURRENT_STATE = STATE_GLB(1:N_DEVC)

   ! Exchange the PRIOR_STATE of each DEViCe

   STATE_LOC(1:N_DEVC) = .FALSE.  ! _LOC is a temporary array that holds the STATE value for the devices on each node
   DO N=1,N_DEVC
      IF (DEVICE(N)%MESH==MYID+1) STATE_LOC(N) = DEVICE(N)%PRIOR_STATE
   ENDDO
   CALL MPI_ALLREDUCE(STATE_LOC(1),STATE_GLB(1),N_DEVC,MPI_LOGICAL,MPI_LXOR,MPI_COMM_WORLD,IERR)
   DEVICE(1:N_DEVC)%PRIOR_STATE = STATE_GLB(1:N_DEVC)

   ! Exchange the INSTANT_VALUE of each DEViCe

   TC_LOC(1:N_DEVC) = 0._EB 
   DO N=1,N_DEVC
      IF (DEVICE(N)%MESH==MYID+1) TC_LOC(N) = DEVICE(N)%INSTANT_VALUE
   ENDDO
   CALL MPI_ALLREDUCE(TC_LOC(1),TC_GLB(1),N_DEVC,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   DEVICE(1:N_DEVC)%INSTANT_VALUE = TC_GLB(1:N_DEVC)

   ! Exchange the T_CHANGE of each DEViCe

   TC_LOC(1:N_DEVC) = 0._EB
   DO N=1,N_DEVC
      IF (DEVICE(N)%MESH==MYID+1) TC_LOC(N) = DEVICE(N)%T_CHANGE
   ENDDO
   CALL MPI_ALLREDUCE(TC_LOC(1),TC_GLB(1),N_DEVC,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   DEVICE(1:N_DEVC)%T_CHANGE = TC_GLB(1:N_DEVC)

ENDIF

! Exchange information about Devices that is only needed at print-out time

IF_DUMP_DEVC: IF (T>=DEVC_CLOCK .AND. N_DEVC>0) THEN

   ! Exchange the current COUNT of each DEViCe

   COUNT_LOC(1:N_DEVC) = DEVICE(1:N_DEVC)%COUNT
   CALL MPI_ALLREDUCE(COUNT_LOC(1),COUNT_GLB(1),N_DEVC,MPI_INTEGER,MPI_SUM,MPI_COMM_WORLD,IERR)

   ! Get the current VALUEs of all DEViCes into DEVICE(:)%VALUE on node 0

   IF (MINVAL(COUNT_GLB)>0) THEN
      TC_LOC(1:N_DEVC) = DEVICE(1:N_DEVC)%VALUE 
      CALL MPI_REDUCE(TC_LOC(1),TC_GLB(1),N_DEVC,MPI_DOUBLE_PRECISION,MPI_SUM,0,MPI_COMM_WORLD,IERR)
      IF (MYID==0) THEN
         DEVICE(1:N_DEVC)%VALUE = TC_GLB(1:N_DEVC)
         DEVICE(1:N_DEVC)%COUNT = COUNT_GLB(1:N_DEVC)
         CALL DUMP_DEVICES(T)
      ENDIF
      DEVC_CLOCK = DEVC_CLOCK + DT_DEVC
      DEVICE(1:N_DEVC)%VALUE = 0._EB
      DEVICE(1:N_DEVC)%COUNT = 0
   ENDIF

ENDIF IF_DUMP_DEVC

! Dump CONTROL info. No gathering required as CONTROL is updated on all meshes

IF (T>=CTRL_CLOCK .AND. N_CTRL>0) THEN
   IF (MYID==0) CALL DUMP_CONTROLS(T)
   CTRL_CLOCK = CTRL_CLOCK + DT_CTRL
ENDIF

END SUBROUTINE DUMP_GLOBAL_OUTPUTS


SUBROUTINE EVAC_READ_DATA
Implicit None
 
! Read input for EVACUATION routines
 
IF (.Not. ANY(EVACUATION_GRID)) N_EVAC = 0
IF (ANY(EVACUATION_GRID)) CALL READ_EVAC(MYID)

END SUBROUTINE EVAC_READ_DATA

SUBROUTINE INITIALIZE_EVAC(NM)
Implicit None
 
! Initialize evacuation meshes
 
INTEGER, INTENT(IN) :: NM
 
IF (ANY(EVACUATION_GRID)) CALL INITIALIZE_EVACUATION(NM,MESH_STOP_STATUS(NM))
! IF (EVACUATION_ONLY(NM)) T(NM) = -EVAC_DT_FLOWFIELD*EVAC_TIME_ITERATIONS
IF (EVACUATION_GRID(NM)) PART_CLOCK(NM) = T_EVAC + DT_PART
IF (EVACUATION_GRID(NM)) CALL DUMP_EVAC(T_EVAC,NM)
!EVAC: Timo this should be checked for the MPI
IF (ANY(EVACUATION_GRID)) ICYC = -EVAC_TIME_ITERATIONS

END SUBROUTINE INITIALIZE_EVAC

SUBROUTINE INIT_EVAC_DUMPS
Implicit None
 
! Initialize evacuation dumps
 
T_EVAC  = - EVAC_DT_FLOWFIELD*EVAC_TIME_ITERATIONS
T_EVAC_SAVE = T_EVAC
IF (ANY(EVACUATION_GRID)) CALL INITIALIZE_EVAC_DUMPS

END SUBROUTINE INIT_EVAC_DUMPS

SUBROUTINE EVAC_CSV(T)
Implicit None
REAL(EB), INTENT(IN) :: T
 
! Dump out Evac info
!EVAC: Timo: Should this  be  changed for MPI?
IF (T>=EVAC_CLOCK .AND. ANY(EVACUATION_GRID)) THEN
   CALL DUMP_EVAC_CSV(T)
   EVAC_CLOCK = EVAC_CLOCK + DT_HRR
ENDIF

END SUBROUTINE EVAC_CSV

SUBROUTINE EVAC_EXCHANGE
Implicit None
 
! Fire mesh information ==> Evac meshes
 
IF (.NOT.ANY(EVACUATION_GRID)) RETURN
IF (ANY(EVACUATION_GRID)) CALL EVAC_MESH_EXCHANGE(T_EVAC,T_EVAC_SAVE,I_EVAC,ICYC)

END SUBROUTINE EVAC_EXCHANGE

SUBROUTINE EVAC_PRESSURE_LOOP(NM)
Implicit None
 
! Evacuation flow field calculation
 
INTEGER, INTENT(IN) :: NM
INTEGER :: N
 
IF (EVACUATION_ONLY(NM)) THEN
   PRESSURE_ITERATION_LOOP: DO N=1,EVAC_PRESSURE_ITERATIONS
      CALL NO_FLUX
      CALL PRESSURE_SOLVER(NM)
   ENDDO PRESSURE_ITERATION_LOOP
END IF

END SUBROUTINE EVAC_PRESSURE_LOOP

SUBROUTINE EVAC_MAIN_LOOP
Implicit None
 
! Call evacuation routine and adjust time steps for evac meshes
 
REAL(EB) :: T_FIRE, FIRE_DT
 
IF (.NOT.ANY(EVACUATION_GRID)) RETURN
 
IF (ANY(EVACUATION_ONLY).AND.(ICYC <= 0)) ACTIVE_MESH = .FALSE.
EVAC_DT = EVAC_DT_STEADY_STATE
IF (ICYC < 1) EVAC_DT = EVAC_DT_FLOWFIELD
T_FIRE = T_EVAC + EVAC_DT
IF (ICYC > 0) THEN
   IF (.NOT.ALL(EVACUATION_ONLY)) THEN
      T_FIRE = MINVAL(T,MASK= (.NOT.EVACUATION_ONLY).AND.ACTIVE_MESH)
      DTNEXT_SYNC(1:NMESHES) = MESHES(1:NMESHES)%DTNEXT
      FIRE_DT = MINVAL(DTNEXT_SYNC,MASK= (.NOT.EVACUATION_ONLY).AND.ACTIVE_MESH)
      T_FIRE = T_FIRE + FIRE_DT
   ENDIF
ENDIF
EVAC_TIME_STEP_LOOP: DO WHILE (T_EVAC < T_FIRE)
   T_EVAC = T_EVAC + EVAC_DT
   CALL PREPARE_TO_EVACUATE(ICYC)
   DO NM=MYID+1,NMESHES,NUMPROCS
      IF (EVACUATION_ONLY(NM)) THEN
         ACTIVE_MESH(NM) = .FALSE.
         CHANGE_TIME_STEP(NM) = .FALSE.
         MESHES(NM)%DT     = EVAC_DT
         MESHES(NM)%DTNEXT = EVAC_DT
         T(NM)  = T_EVAC
         IF (ICYC <= 1 .And. .Not. BTEST(I_EVAC,2) ) THEN
            IF (ICYC <= 0) ACTIVE_MESH(NM) = .TRUE.
            IF (ICYC <= 0) T(NM) = T_EVAC + EVAC_DT_FLOWFIELD*EVAC_TIME_ITERATIONS - EVAC_DT
         ENDIF
         IF (EVACUATION_GRID(NM) ) THEN
            CALL EVACUATE_HUMANS(T_EVAC,NM,ICYC)
            IF (T_EVAC >= PART_CLOCK(NM)) THEN
               CALL DUMP_EVAC(T_EVAC,NM)
               DO
                  PART_CLOCK(NM) = PART_CLOCK(NM) + DT_PART
                  IF (PART_CLOCK(NM) >= T_EVAC) EXIT
               ENDDO
            ENDIF
         ENDIF
      ENDIF
   ENDDO
   IF (ICYC < 1) EXIT EVAC_TIME_STEP_LOOP
ENDDO EVAC_TIME_STEP_LOOP

END SUBROUTINE EVAC_MAIN_LOOP


SUBROUTINE GET_REVISION_NUMBER(REV_NUMBER,REV_DATE)
USE isodefs, ONLY : GET_REV_smvv
USE POIS, ONLY : GET_REV_pois
USE COMP_FUNCTIONS, ONLY : GET_REV_func
USE MESH_POINTERS, ONLY : GET_REV_mesh
USE RADCALV, ONLY : GET_REV_irad
USE DCDFLIB, ONLY : GET_REV_ieva
INTEGER,INTENT(INOUT) :: REV_NUMBER
CHARACTER(255),INTENT(INOUT) :: REV_DATE
INTEGER :: MODULE_REV
CHARACTER(255) :: MODULE_DATE

CALL GET_REV_cons(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_ctrl(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_devc(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_divg(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_dump(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_evac(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_fire(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_func(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_ieva(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_init(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_irad(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_mass(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_mesh(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_part(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_pois(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_prec(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_pres(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_radi(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_read(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_smvv(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_type(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_velo(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF
CALL GET_REV_wall(MODULE_REV,MODULE_DATE)
IF (MODULE_REV > REV_NUMBER) THEN
   REV_NUMBER = MODULE_REV
   WRITE(REV_DATE,'(A)') MODULE_DATE
ENDIF

END SUBROUTINE GET_REVISION_NUMBER

END PROGRAM FDS
