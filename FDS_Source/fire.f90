MODULE FIRE
 
! Compute combustion 
 
USE PRECISION_PARAMETERS
USE GLOBAL_CONSTANTS
USE MESH_POINTERS
USE COMP_FUNCTIONS, ONLY: SECOND
 
IMPLICIT NONE
PRIVATE
CHARACTER(255), PARAMETER :: fireid='$Id$'
CHARACTER(255), PARAMETER :: firerev='$Revision$'
CHARACTER(255), PARAMETER :: firedate='$Date$'

TYPE(REACTION_TYPE), POINTER :: RN=>NULL()
REAL(EB) :: Q_UPPER

PUBLIC COMBUSTION, GET_REV_fire
 
CONTAINS
 
SUBROUTINE COMBUSTION(NM)

INTEGER, INTENT(IN) :: NM
REAL(EB) :: TNOW

IF (EVACUATION_ONLY(NM)) RETURN

TNOW=SECOND()

IF (INIT_HRRPUV) RETURN

CALL POINT_TO_MESH(NM)

! Upper bounds on local HRR per unit volume

Q_UPPER = HRRPUA_SHEET/CELL_SIZE + HRRPUV_AVERAGE

! Call combustion ODE solver

CALL COMBUSTION_GENERAL

TUSED(10,NM)=TUSED(10,NM)+SECOND()-TNOW

END SUBROUTINE COMBUSTION


SUBROUTINE COMBUSTION_GENERAL

! Generic combustion routine for multi step reactions with kinetics either mixing controlled, finite rate, 
! or a temperature threshhold mixed approach

USE PHYSICAL_FUNCTIONS, ONLY: GET_SPECIFIC_GAS_CONSTANT,GET_MASS_FRACTION_ALL,GET_SPECIFIC_HEAT,GET_MOLECULAR_WEIGHT, &
                              GET_SENSIBLE_ENTHALPY_DIFF,GET_SENSIBLE_ENTHALPY
INTEGER :: I,J,K,NS,NR,II,JJ,KK,IIG,JJG,KKG,IW,N
REAL(EB):: ZZ_GET(0:N_TRACKED_SPECIES),ZZ_MIN=1.E-10_EB,DZZ(0:N_TRACKED_SPECIES),CP,HDIFF
LOGICAL :: DO_REACTION,REACTANTS_PRESENT,Q_EXISTS
TYPE (REACTION_TYPE),POINTER :: RN
TYPE (SPECIES_MIXTURE_TYPE), POINTER :: SM,SM0

Q          = 0._EB
D_REACTION = 0._EB
Q_EXISTS = .FALSE.
SM0 => SPECIES_MIXTURE(0)

DO K=1,KBAR
   DO J=1,JBAR
      ILOOP: DO I=1,IBAR
         !Check to see if a reaction is possible
         IF (SOLID(CELL_INDEX(I,J,K))) CYCLE ILOOP
         ZZ_GET(1:N_TRACKED_SPECIES) = ZZ(I,J,K,1:N_TRACKED_SPECIES)
         ZZ_GET(0) = 1._EB - MIN(1._EB,SUM(ZZ_GET(1:N_TRACKED_SPECIES)))
         DO_REACTION = .FALSE.
         DO NR=1,N_REACTIONS
            RN=>REACTION(NR)
            REACTANTS_PRESENT = .TRUE.
            DO NS=0,N_TRACKED_SPECIES
               IF (RN%NU(NS)<0._EB .AND. ZZ_GET(NS) < ZZ_MIN) THEN
                  REACTANTS_PRESENT = .FALSE.
                  EXIT
               ENDIF
            END DO            
            IF (.NOT. DO_REACTION) DO_REACTION = REACTANTS_PRESENT     
         END DO
         IF (.NOT. DO_REACTION) CYCLE ILOOP
         DZZ(1:N_TRACKED_SPECIES) = ZZ_GET(1:N_TRACKED_SPECIES) ! store old ZZ for divergence term
         ! Easily allow for user selected ODE solver
         IF (.NOT. EDC) THEN
            SELECT CASE (COMBUSTION_ODE)
               CASE(SINGLE_EXACT)
                  CALL ODE_EXACT(I,J,K,ZZ_GET,Q(I,J,K))
               CASE(EXPLICIT_EULER)
                  CALL ODE_EXPLICIT_EULER(I,J,K,ZZ_GET,Q(I,J,K))
               CASE(RUNGE_KUTTA_2)
                  CALL ODE_RUNGE_KUTTA_2(I,J,K,ZZ_GET,Q(I,J,K))
               CASE(RK2_RICHARDSON)
                  CALL ODE_RK2_RICHARDSON(I,J,K,ZZ_GET,Q(I,J,K))
               CASE(EDCM_RK2)
                  CALL EDC_MODEL(I,J,K,ZZ_GET,Q(I,J,K))
            END SELECT
         ELSE
            CALL COMBUSTION_MODEL(I,J,K,ZZ_GET,Q(I,J,K))
         ENDIF
         ! Update RSUM and ZZ       
         Q_IF: IF (ABS(Q(I,J,K)) > TWO_EPSILON_EB) THEN
            Q_EXISTS = .TRUE.
            CALL GET_SPECIFIC_GAS_CONSTANT(ZZ_GET,RSUM(I,J,K)) 
            TMP(I,J,K) = PBAR(K,PRESSURE_ZONE(I,J,K))/(RSUM(I,J,K)*RHO(I,J,K))            
            ZZ(I,J,K,1:N_TRACKED_SPECIES) = ZZ_GET(1:N_TRACKED_SPECIES)
            CP_IF: IF (.NOT.CONSTANT_SPECIFIC_HEAT) THEN
               ! Divergence term
               DZZ(1:N_TRACKED_SPECIES) = ZZ_GET(1:N_TRACKED_SPECIES) - DZZ(1:N_TRACKED_SPECIES)
               CALL GET_SPECIFIC_HEAT(ZZ_GET,CP,TMP(I,J,K))
               DO N=1,N_TRACKED_SPECIES
                  SM => SPECIES_MIXTURE(N)
                  CALL GET_SENSIBLE_ENTHALPY_DIFF(N,TMP(I,J,K),HDIFF)
                  D_REACTION(I,J,K) = D_REACTION(I,J,K) + ( (SM%RCON-SM0%RCON)/RSUM(I,J,K) - HDIFF/(CP*TMP(I,J,K)) )*DZZ(N)/DT
               ENDDO
            ENDIF CP_IF
         ENDIF Q_IF
      ENDDO ILOOP
   ENDDO
ENDDO

IF (.NOT. Q_EXISTS) RETURN

! Set Q in the ghost cell, just for better visualization.
DO IW=1,N_EXTERNAL_WALL_CELLS
   IF (WALL(IW)%BOUNDARY_TYPE/=INTERPOLATED_BOUNDARY .AND. WALL(IW)%BOUNDARY_TYPE/=OPEN_BOUNDARY) CYCLE
   II  = WALL(IW)%ONE_D%II
   JJ  = WALL(IW)%ONE_D%JJ
   KK  = WALL(IW)%ONE_D%KK
   IIG = WALL(IW)%ONE_D%IIG
   JJG = WALL(IW)%ONE_D%JJG
   KKG = WALL(IW)%ONE_D%KKG
   Q(II,JJ,KK) = Q(IIG,JJG,KKG)
ENDDO

END SUBROUTINE COMBUSTION_GENERAL


SUBROUTINE ODE_EXACT(I,J,K,ZZ_GET,Q_NEW)
INTEGER,INTENT(IN):: I,J,K
REAL(EB),INTENT(OUT):: Q_NEW
REAL(EB),INTENT(INOUT) :: ZZ_GET(0:N_TRACKED_SPECIES)
REAL(EB) :: DZF,Q_BOUND_1,Q_BOUND_2,RATE_CONSTANT,Z_LIMITER,REACTANT_MIN,DT2
LOGICAL :: MIN_FOUND
INTEGER :: NS
TYPE(REACTION_TYPE),POINTER :: RN=>NULL()

Q_NEW = 0._EB
RN=>REACTION(1)
CALL COMPUTE_RATE_CONSTANT(1,RN%MODE,1,0._EB,RATE_CONSTANT,ZZ_GET,I,J,K)

IF(RATE_CONSTANT < TWO_EPSILON_EB) RETURN

Z_LIMITER = RATE_CONSTANT*MIX_TIME(I,J,K)

DZF = -1._EB
!Check for reactant (i.e. fuel or oxidizer) limited combustion
MIN_FOUND = .FALSE.
REACTANT_MIN=1._EB
DO NS=0,N_TRACKED_SPECIES
   IF (RN%NU(NS) < -TWO_EPSILON_EB) &
      REACTANT_MIN = MIN(REACTANT_MIN,-ZZ_GET(NS)*SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW/(SPECIES_MIXTURE(NS)%MW*RN%NU(NS)))
   IF (ABS(Z_LIMITER - REACTANT_MIN) <= SPACING(Z_LIMITER)) THEN
      MIN_FOUND = .TRUE.
      DZF = REACTANT_MIN*(1._EB-EXP(-DT/MIX_TIME(I,J,K)))
      EXIT
   ENDIF
ENDDO

!For product limited combsiton find time of switch from product limited to reactant limited (if it occurs)
!and do two step exact solution
IF (.NOT. MIN_FOUND) THEN
   DT2 = MIX_TIME(I,J,K)*LOG((Z_LIMITER+REACTANT_MIN)/(2._EB*Z_LIMITER))
   IF (DT2 < DT) THEN
      DZF = ZZ_GET(RN%FUEL_SMIX_INDEX) - Z_LIMITER*(EXP(DT2/MIX_TIME(I,J,K))-1._EB)
      REACTANT_MIN = REACTANT_MIN - DZF
      DZF = DZF + REACTANT_MIN*(1._EB-EXP(-(DT-DT2)/MIX_TIME(I,J,K)))
   ELSE
      DZF = ZZ_GET(RN%FUEL_SMIX_INDEX) - Z_LIMITER*(EXP(DT/MIX_TIME(I,J,K))-1._EB)
   ENDIF
ENDIF

DZF = MIN(DZF,ZZ_GET(RN%FUEL_SMIX_INDEX))

!****** TEMP OVERRIDE TO ENSURE SAME RESULTS AS PREVIOUS *******
!DZF = Z_LIMITER*(1._EB-EXP(-DT/MIX_TIME(I,J,K)))
!***************************************************************

Q_BOUND_1 = DZF*RHO(I,J,K)*RN%HEAT_OF_COMBUSTION/DT
Q_BOUND_2 = Q_UPPER
Q_NEW = MIN(Q_BOUND_1,Q_BOUND_2)
DZF = Q_NEW*DT/(RHO(I,J,K)*RN%HEAT_OF_COMBUSTION)         

ZZ_GET = ZZ_GET + DZF*RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW

END SUBROUTINE ODE_EXACT


SUBROUTINE ODE_EXPLICIT_EULER(I,J,K,ZZ_GET,Q_OUT)
INTEGER,INTENT(IN):: I,J,K
REAL(EB),INTENT(OUT):: Q_OUT
REAL(EB),INTENT(INOUT) :: ZZ_GET(0:N_TRACKED_SPECIES)
REAL(EB) :: ZZ_0(0:N_TRACKED_SPECIES),ZZ_I(0:N_TRACKED_SPECIES),ZZ_N(0:N_TRACKED_SPECIES),DZZDT(0:N_TRACKED_SPECIES),&
            DT_ODE,DT_NEW,RATE_CONSTANT(1:N_REACTIONS),Q_NR(1:N_REACTIONS),Q_SUM,DT_SUM
INTEGER :: NR,I_TS,NS
INTEGER, PARAMETER :: NODETS=20
TYPE(REACTION_TYPE),POINTER :: RN=>NULL()

Q_OUT = 0._EB
ZZ_0 = MAX(0._EB,ZZ_GET)
ZZ_I = ZZ_0
DT_ODE = DT/REAL(NODETS,EB)
DT_NEW = DT_ODE
DT_SUM = 0._EB
I_TS = 1
ODE_LOOP: DO WHILE (DT_SUM < DT)
   DZZDT = 0._EB
   RATE_CONSTANT = 0._EB
   Q_NR = 0._EB
   REACTION_LOOP: DO NR = 1, N_REACTIONS   
      RN => REACTION(NR)      
      CALL COMPUTE_RATE_CONSTANT(NR,RN%MODE,I_TS,Q_OUT,RATE_CONSTANT(NR),ZZ_I,I,J,K)
      IF (RATE_CONSTANT(NR) < TWO_EPSILON_EB) CYCLE REACTION_LOOP
      Q_NR(NR) = RATE_CONSTANT(NR)*RN%HEAT_OF_COMBUSTION*RHO(I,J,K)
      DZZDT = DZZDT + RN%NU * SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
   END DO REACTION_LOOP     
   IF (ALL(DZZDT < TWO_EPSILON_EB)) EXIT ODE_LOOP
   ZZ_N = ZZ_I + DZZDT * DT_NEW

   IF (ANY(ZZ_N < 0._EB)) THEN
      DO NS=0,N_TRACKED_SPECIES
          IF (ZZ_N(NS) < 0._EB .AND. ABS(DZZDT(NS))>TWO_EPSILON_EB) DT_NEW = MIN(DT_NEW,-ZZ_I(NS)/DZZDT(NS))
      ENDDO
   ENDIF  

   Q_SUM = SUM(Q_NR)
   IF (Q_OUT + Q_SUM*DT_NEW > Q_UPPER * DT) THEN
      DT_NEW = MAX(0._EB,(Q_UPPER * DT - Q_OUT))/Q_SUM
      Q_OUT = Q_OUT+Q_SUM*DT_NEW
      ZZ_I = ZZ_I + DZZDT * DT_NEW
      EXIT ODE_LOOP
   ENDIF   
   Q_OUT = Q_OUT+Q_SUM*DT_NEW
   ZZ_I = ZZ_I + DZZDT * DT_NEW
   DT_SUM = DT_SUM + DT_NEW
   IF (DT_NEW < DT_ODE) DT_NEW = DT_ODE
   IF (DT_NEW + DT_SUM > DT) DT_NEW = DT - DT_SUM
   I_TS = I_TS + 1
ENDDO ODE_LOOP

ZZ_GET = ZZ_GET + ZZ_I - ZZ_0
Q_OUT = Q_OUT / DT

RETURN

END SUBROUTINE ODE_EXPLICIT_EULER


SUBROUTINE ODE_RUNGE_KUTTA_2(I,J,K,ZZ_GET,Q_OUT)
INTEGER,INTENT(IN):: I,J,K
REAL(EB),INTENT(OUT):: Q_OUT
REAL(EB),INTENT(INOUT) :: ZZ_GET(0:N_TRACKED_SPECIES)
REAL(EB) :: ZZ_0(0:N_TRACKED_SPECIES),ZZ_I(0:N_TRACKED_SPECIES),ZZ_N(0:N_TRACKED_SPECIES),&
            DZZDT(0:N_TRACKED_SPECIES),DZZDT2(0:N_TRACKED_SPECIES),&
            DT_ODE,DT_NEW,RATE_CONSTANT(1:N_REACTIONS),Q_NR(1:N_REACTIONS),Q_NR2(1:N_REACTIONS),Q_SUM,DT_SUM
INTEGER :: NR,I_TS,NS
INTEGER, PARAMETER :: NODETS=20
TYPE(REACTION_TYPE),POINTER :: RN=>NULL()

Q_OUT = 0._EB
ZZ_0 = MAX(0._EB,ZZ_GET)
ZZ_I = ZZ_0
DT_ODE = DT/REAL(NODETS,EB)
DT_NEW = DT_ODE
DT_SUM = 0._EB
I_TS = 1
ODE_LOOP: DO WHILE (DT_SUM < DT)
   DZZDT = 0._EB
   DZZDT2 = 0._EB
   Q_NR = 0._EB
   Q_NR2 = 0._EB
   RATE_CONSTANT = 0._EB
   REACTION_LOOP: DO NR = 1, N_REACTIONS   
      RN => REACTION(NR)      
      CALL COMPUTE_RATE_CONSTANT(NR,RN%MODE,I_TS,Q_OUT,RATE_CONSTANT(NR),ZZ_I,I,J,K)
      IF (RATE_CONSTANT(NR) < TWO_EPSILON_EB) CYCLE REACTION_LOOP
      Q_NR(NR) = RATE_CONSTANT(NR)*RN%HEAT_OF_COMBUSTION*RHO(I,J,K)
      DZZDT = DZZDT + RN%NU * SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
   END DO REACTION_LOOP   
   IF (ALL(DZZDT < TWO_EPSILON_EB)) EXIT ODE_LOOP    
   ZZ_N = ZZ_I + DZZDT * DT_NEW

   IF (ANY(ZZ_N < 0._EB)) THEN
      DO NS=0,N_TRACKED_SPECIES
          IF (ZZ_N(NS) < 0._EB .AND. ABS(DZZDT(NS))>TWO_EPSILON_EB) DT_NEW = MIN(DT_NEW,-ZZ_I(NS)/DZZDT(NS))
      ENDDO
   ENDIF  

   ZZ_N = ZZ_I + DZZDT * DT_NEW
   
   REACTION_LOOP2: DO NR = 1, N_REACTIONS   
      RN => REACTION(NR)      
      CALL COMPUTE_RATE_CONSTANT(NR,RN%MODE,I_TS,Q_OUT,RATE_CONSTANT(NR),ZZ_N,I,J,K)
      IF (RATE_CONSTANT(NR) < TWO_EPSILON_EB) CYCLE REACTION_LOOP2
      Q_NR2(NR) = RATE_CONSTANT(NR)*RN%HEAT_OF_COMBUSTION*RHO(I,J,K)
      DZZDT2 = DZZDT2 + RN%NU * SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
   END DO REACTION_LOOP2    
   IF (ALL(DZZDT2 < TWO_EPSILON_EB)) EXIT ODE_LOOP
   ZZ_N = ZZ_I +0.5_EB*(DZZDT+DZZDT2)*DT_NEW
   
   IF (ANY(ZZ_N < 0._EB)) THEN
      DO NS=0,N_TRACKED_SPECIES
          IF (ZZ_N(NS) < 0._EB .AND. ABS(DZZDT(NS)+DZZDT2(NS))>TWO_EPSILON_EB) &
             DT_NEW = MIN(DT_NEW,-2._EB*ZZ_I(NS)/(DZZDT(NS)+DZZDT2(NS)))
      ENDDO
   ENDIF     

   Q_SUM = SUM(0.5_EB*(Q_NR+Q_NR2))

   IF (Q_OUT + Q_SUM*DT_NEW > Q_UPPER * DT) THEN
      DT_NEW = MAX(0._EB,(Q_UPPER * DT - Q_OUT))/Q_SUM
      Q_OUT = Q_OUT+Q_SUM*DT_NEW
      ZZ_I = ZZ_I + 0.5_EB*(DZZDT+DZZDT2)*DT_NEW
      EXIT ODE_LOOP
   ENDIF   

   ZZ_I = ZZ_I +0.5_EB*(DZZDT+DZZDT2)*DT_NEW

   Q_OUT = Q_OUT+Q_SUM*DT_NEW
 
   DT_SUM = DT_SUM + DT_NEW
   IF (DT_NEW < DT_ODE) DT_NEW = DT_ODE
   IF (DT_NEW + DT_SUM > DT) DT_NEW = DT - DT_SUM
   I_TS = I_TS + 1
ENDDO ODE_LOOP

ZZ_GET = ZZ_GET + ZZ_I - ZZ_0
Q_OUT = Q_OUT / DT

END SUBROUTINE ODE_RUNGE_KUTTA_2


SUBROUTINE ODE_RK2_RICHARDSON(I,J,K,ZZ_GET,Q_OUT)

USE COMP_FUNCTIONS, ONLY: SHUTDOWN

INTEGER,INTENT(IN):: I,J,K
REAL(EB),INTENT(OUT):: Q_OUT
REAL(EB),INTENT(INOUT) :: ZZ_GET(0:N_TRACKED_SPECIES)
REAL(EB) :: ZZ_0(0:N_TRACKED_SPECIES),DZZDT(0:N_TRACKED_SPECIES),DZZDT2(0:N_TRACKED_SPECIES),RATE_CONSTANT(1:N_REACTIONS),&
            ERR_EST,ERR_TOL,Q_NR_1(1:N_REACTIONS),Q_NR2_1(1:N_REACTIONS),Q_NR_2(1:N_REACTIONS),&
            Q_NR2_2(1:N_REACTIONS),Q_NR_4(1:N_REACTIONS),Q_NR2_4(1:N_REACTIONS),Q_SUM_1,Q_SUM_2,Q_SUM_4,&
            A1(0:N_TRACKED_SPECIES),A2(0:N_TRACKED_SPECIES),A4(0:N_TRACKED_SPECIES),DT_SUB,DT_SUB_NEW,DT_ITER,&
            A1H(0:N_TRACKED_SPECIES),A2H(0:N_TRACKED_SPECIES),A4H(0:N_TRACKED_SPECIES),&
            DT_A1,DT_A2,DT_A4,ZZ_STORE(0:N_TRACKED_SPECIES,0:3),TV(0:2),ZZ_DIFF(0:2),Q1,Q2,Q4,Q_OUT2
REAL(EB), PARAMETER :: DT_SUB_MIN=1.E-10_EB
INTEGER :: I_TS,NR,NS,NSS,ITER,TVI,RICH_ITER
INTEGER, PARAMETER :: NODETS=20,SUB_DT1=1,SUB_DT2=2,SUB_DT4=4,TV_ITER_MIN=5,Q_ITER_MAX=10,RICH_ITER_MAX=100
TYPE(REACTION_TYPE),POINTER :: RN=>NULL()

Q_OUT = 0._EB
Q_OUT2 = 0._EB
RICH_ITER=0
ITER=0
DT_ITER = 0._EB
I_TS = 1
DT_SUB = DT 
DT_SUB_NEW = DT_SUB

ERR_TOL = RICHARDSON_ERROR_TOLERANCE

INTEGRATION_LOOP: DO WHILE (DT_ITER < DT)
   ERR_EST = 10._EB*ERR_TOL
   RICH_EX_LOOP: DO WHILE (ERR_EST > ERR_TOL)
      DT_SUB = DT_SUB_NEW
      IF (DT_ITER + DT_SUB > DT) THEN
         DT_SUB = DT - DT_ITER      
      ENDIF
   
      !--------------------
      ! Calculate A1 term
      ! Time step = DT_SUB
      !--------------------
      ZZ_0 = MAX(0._EB,ZZ_GET)
      Q1 = Q_OUT2
      ODE_LOOP1: DO NS = 1, SUB_DT1
         DZZDT = 0._EB
         DZZDT2 = 0._EB
         RATE_CONSTANT = 0._EB
      
         REACTION_LOOP: DO NR = 1,N_REACTIONS
            RN => REACTION(NR)
            CALL COMPUTE_RATE_CONSTANT(NR,RN%MODE,I_TS,Q_OUT2,RATE_CONSTANT(NR),ZZ_0,I,J,K)
            IF (RATE_CONSTANT(NR) <= 0.0_EB) CYCLE REACTION_LOOP
            DZZDT = DZZDT + RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
            Q_NR_1(NR) = RATE_CONSTANT(NR)*RN%HEAT_OF_COMBUSTION*RHO(I,J,K)
         END DO REACTION_LOOP
         IF (ALL(DZZDT < 0._EB)) EXIT INTEGRATION_LOOP
         A1H = ZZ_0 + DZZDT*DT_SUB
         IF (ANY(A1H < 0._EB)) THEN
            DO NSS=0,N_TRACKED_SPECIES
               IF (A1H(NSS) < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                  DT_SUB = MIN(DT_SUB,ABS(ZZ_0(NSS)/DZZDT(NSS)))
               ENDIF
            ENDDO
            A1H = ZZ_0 + DZZDT*DT_SUB
         ENDIF  
         
         REACTION_LOOP2: DO NR = 1,N_REACTIONS
            RN => REACTION(NR)      
            CALL COMPUTE_RATE_CONSTANT(NR,RN%MODE,I_TS,Q_OUT2,RATE_CONSTANT(NR),A1H,I,J,K)
            IF (RATE_CONSTANT(NR) <= 0.0_EB) CYCLE REACTION_LOOP2
            DZZDT2 = DZZDT2 + RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
            Q_NR2_1(NR) = RATE_CONSTANT(NR)*RN%HEAT_OF_COMBUSTION*RHO(I,J,K)
         END DO REACTION_LOOP2    
         IF (ALL(DZZDT2 < 0._EB)) EXIT INTEGRATION_LOOP
         A1 = ZZ_0 + 0.5_EB*(DZZDT+DZZDT2)*DT_SUB 
         IF (ANY(A1 < 0._EB)) THEN
            DO NSS=0,N_TRACKED_SPECIES
               IF (A1(NSS) < 0._EB .AND. ABS(DZZDT(NSS)+DZZDT2(NSS))>TWO_EPSILON_EB) THEN
                  DT_SUB = MIN(DT_SUB,ABS(-2._EB*A1H(NSS)/(DZZDT(NSS)+DZZDT2(NSS))))
               ENDIF
            ENDDO
            A1 = ZZ_0 + 0.5_EB*(DZZDT+DZZDT2)*DT_SUB
         ENDIF 
         IF (ABS(SUM(A1)-1._EB) < -TWO_EPSILON_EB) CALL SHUTDOWN('ERROR: Error in Reaction Species')
         Q_SUM_1 = SUM(0.5_EB*(Q_NR_1+Q_NR2_1))
         IF (Q1 + Q_SUM_1*DT_SUB > Q_UPPER * DT) THEN
            DT_SUB_NEW = MAX(0.0_EB,(Q_UPPER * DT-Q1)/Q_SUM_1)
            Q1 = Q1+Q_SUM_1*DT_SUB_NEW
            A1 = ZZ_0 + 0.5_EB*(DZZDT+DZZDT2)*DT_SUB_NEW
            EXIT ODE_LOOP1
         ENDIF  
         Q1 = Q1+Q_SUM_1*DT_SUB
         I_TS = I_TS + 1
      ENDDO ODE_LOOP1
      DT_A1 = DT_SUB

      !--------------------
      ! Calculate A2 term
      ! Time step = DT_SUB/2
      !-------------------- 
      ZZ_0 = MAX(0._EB,ZZ_GET)
      Q2 = Q_OUT2
      ODE_LOOP2: DO NS = 1, SUB_DT2
         DZZDT = 0._EB
         DZZDT2 = 0._EB
         RATE_CONSTANT = 0._EB
      
         REACTION_LOOP_2: DO NR = 1, N_REACTIONS
            RN => REACTION(NR)    
            CALL COMPUTE_RATE_CONSTANT(NR,RN%MODE,I_TS,Q_OUT2,RATE_CONSTANT(NR),ZZ_0,I,J,K)
            IF (RATE_CONSTANT(NR) <= 0.0_EB) CYCLE REACTION_LOOP_2
            DZZDT = DZZDT + RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
            Q_NR_2(NR) = RATE_CONSTANT(NR)*RN%HEAT_OF_COMBUSTION*RHO(I,J,K)
         END DO REACTION_LOOP_2   
         A2H = ZZ_0 + DZZDT*(DT_SUB/2._EB)
         IF (ANY(A2H < 0._EB)) THEN
            DO NSS=0,N_TRACKED_SPECIES
               IF (A2H(NSS) < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                  DT_SUB = MIN(DT_SUB,-ZZ_0(NSS)/DZZDT(NSS))
               ENDIF
            ENDDO
            A2H = ZZ_0 + DZZDT*(DT_SUB/2._EB)
         ENDIF  
         
         REACTION_LOOP2_2: DO NR = 1, N_REACTIONS
            RN => REACTION(NR)
            CALL COMPUTE_RATE_CONSTANT(NR,RN%MODE,I_TS,Q_OUT2,RATE_CONSTANT(NR),A2H,I,J,K)
            IF (RATE_CONSTANT(NR) <= 0.0_EB) CYCLE REACTION_LOOP2_2
            DZZDT2 = DZZDT2 + RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
            Q_NR2_2(NR) = RATE_CONSTANT(NR)*RN%HEAT_OF_COMBUSTION*RHO(I,J,K)
         END DO REACTION_LOOP2_2
         A2 = ZZ_0 + 0.5_EB*(DZZDT+DZZDT2)*(DT_SUB/2._EB)
         IF (ANY(A2 < 0._EB)) THEN
            DO NSS=0,N_TRACKED_SPECIES
               IF (A2(NSS) < 0._EB .AND. ABS(DZZDT(NSS)+DZZDT2(NSS))>TWO_EPSILON_EB) THEN
                  DT_SUB = MIN(DT_SUB,-2._EB*A2H(NSS)/(DZZDT(NSS)+DZZDT2(NSS)))
               ENDIF
            ENDDO
            A2 = ZZ_0 + 0.5_EB*(DZZDT+DZZDT2)*(DT_SUB/2._EB)
         ENDIF    
         IF (ABS(SUM(A2)-1._EB) < -TWO_EPSILON_EB) CALL SHUTDOWN('ERROR: Error in Reaction Species')
         Q_SUM_2 = SUM(0.5_EB*(Q_NR_2+Q_NR2_2))
         IF (Q2+Q_SUM_2*(DT_SUB/2._EB) > Q_UPPER * DT) THEN
            DT_SUB_NEW = MAX(0.0_EB,(Q_UPPER * DT-Q2)/Q_SUM_2)
            Q2 = Q2+Q_SUM_2*(DT_SUB_NEW)
            A2 = ZZ_0 + 0.5_EB*(DZZDT+DZZDT2)*(DT_SUB_NEW)
            EXIT ODE_LOOP2
         ENDIF  
         Q2 = Q2+Q_SUM_2*(DT_SUB/2._EB)
         I_TS = I_TS + 1
         ZZ_0 = A2
      ENDDO ODE_LOOP2
      DT_A2 = DT_SUB
      IF (DT_A2 < DT_A1) THEN
         DT_SUB_NEW = DT_A2
         CYCLE RICH_EX_LOOP
      ENDIF
     
      !--------------------
      ! Calculate A4 term
      ! Time step = DT_SUB/4
      !-------------------- 
      ZZ_0 = MAX(0._EB,ZZ_GET)
      Q4 = Q_OUT2
      ODE_LOOP4: DO NS = 1, SUB_DT4
         DZZDT = 0._EB
         DZZDT2 = 0._EB
         RATE_CONSTANT = 0._EB
      
         REACTION_LOOP_4: DO NR = 1, N_REACTIONS
            RN => REACTION(NR)
            CALL COMPUTE_RATE_CONSTANT(NR,RN%MODE,I_TS,Q_OUT2,RATE_CONSTANT(NR),ZZ_0,I,J,K)
            IF (RATE_CONSTANT(NR) <= 0.0_EB) CYCLE REACTION_LOOP_4
            DZZDT = DZZDT + RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
            Q_NR_4(NR) = RATE_CONSTANT(NR)*RN%HEAT_OF_COMBUSTION*RHO(I,J,K)
         END DO REACTION_LOOP_4   
         A4H = ZZ_0 + DZZDT*(DT_SUB/4._EB)
         IF (ANY(A4H < 0._EB)) THEN
            DO NSS=0,N_TRACKED_SPECIES
               IF (A4H(NSS) < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                  DT_SUB = MIN(DT_SUB,-ZZ_0(NSS)/DZZDT(NSS))
               ENDIF
            ENDDO
            A4H = ZZ_0 + DZZDT*(DT_SUB/4._EB)
         ENDIF
         
         REACTION_LOOP2_4: DO NR = 1, N_REACTIONS
            RN => REACTION(NR)
            CALL COMPUTE_RATE_CONSTANT(NR,RN%MODE,I_TS,Q_OUT2,RATE_CONSTANT(NR),A4H,I,J,K)
            IF (RATE_CONSTANT(NR) <= 0.0_EB) CYCLE REACTION_LOOP2_4
            DZZDT2 = DZZDT2 + RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
            Q_NR2_4(NR) = RATE_CONSTANT(NR)*RN%HEAT_OF_COMBUSTION*RHO(I,J,K)
         END DO REACTION_LOOP2_4
         A4 = ZZ_0 + 0.5_EB*(DZZDT+DZZDT2)*(DT_SUB/4._EB)
         IF (ANY(A4 < 0._EB)) THEN
            DO NSS=0,N_TRACKED_SPECIES
               IF (A4(NSS) < 0._EB .AND. ABS(DZZDT(NSS)+DZZDT2(NSS))>TWO_EPSILON_EB) THEN
                  DT_SUB = MIN(DT_SUB,-2._EB*A4H(NSS)/(DZZDT(NSS)+DZZDT2(NSS)))
               ENDIF
            ENDDO
            A4 = ZZ_0 + 0.5_EB*(DZZDT+DZZDT2)*(DT_SUB/4._EB)
         ENDIF
         IF (ABS(SUM(A4)-1._EB) < -TWO_EPSILON_EB) CALL SHUTDOWN('ERROR: Error in Reaction Species')
         Q_SUM_4 = SUM(0.5_EB*(Q_NR_4+Q_NR2_4))
         IF (ABS(Q4+Q_SUM_4*(DT_SUB/4._EB)) > Q_UPPER*DT) THEN
            DT_SUB_NEW = MAX(0.0_EB,(Q_UPPER*DT-Q4)/Q_SUM_4)
            Q4 = Q4+Q_SUM_4*(DT_SUB_NEW)
            A4 = ZZ_0 + 0.5_EB*(DZZDT+DZZDT2)*(DT_SUB_NEW)
            IF (ITER >= Q_ITER_MAX) THEN
               Q_OUT = Q4/DT
               ZZ_GET = A4
               EXIT INTEGRATION_LOOP
            ENDIF
            EXIT ODE_LOOP4
         ENDIF  
         Q4 = Q4+Q_SUM_4*(DT_SUB/4._EB)
         I_TS = I_TS + 1
         ZZ_0 = A4
      ENDDO ODE_LOOP4
      DT_A4 = DT_SUB
      IF (DT_A4 < DT_A2) THEN
         DT_SUB_NEW = DT_A4
         CYCLE RICH_EX_LOOP
      ENDIF

      ! Species Error Analysis
      ERR_EST = MAXVAL(ABS((4._EB*A4-A2) - (4._EB*A2-A1)))/45._EB  ! Estimate Error
      IF (ERR_EST <= 0.0_EB) THEN
         DT_SUB_NEW = DT
      ELSE
         DT_SUB_NEW = MAX(DT_SUB*(ERR_TOL/(ERR_EST))**(0.25_EB),DT_SUB_MIN) ! Determine New Time Step
      ENDIF
      IF (DT_SUB <= DT_SUB_MIN) EXIT RICH_EX_LOOP
      RICH_ITER = RICH_ITER+1
      IF (RICH_ITER >= RICH_ITER_MAX) EXIT RICH_EX_LOOP
   ENDDO RICH_EX_LOOP
   
   DT_ITER = DT_ITER + DT_SUB
   ITER = ITER + 1
   MAX_CHEM_SUBIT = MAX(MAX_CHEM_SUBIT,ITER)
   ZZ_GET = (4._EB*A4-A2)/3._EB
   Q_OUT = (4._EB*Q4-Q2)/3._EB/DT
   Q_OUT2 = (4._EB*Q4-Q2)/3._EB
   
   !Total Variation Scheme
   DO NS = 0,N_TRACKED_SPECIES
      DO TVI = 0,2
         ZZ_STORE(NS,TVI)=ZZ_STORE(NS,TVI+1)
      ENDDO
      ZZ_STORE(NS,3) = (4._EB*A4(NS)-A2(NS))/3._EB  
   ENDDO
   
   REACTION_LOOP_TV: DO NR = 1, N_REACTIONS
      RN => REACTION(NR)
      IF (.NOT. RN%REVERSIBLE) CYCLE REACTION_LOOP_TV
      DO TVI = 0,2
         TV(TVI) = ABS(ZZ_STORE(RN%FUEL_SMIX_INDEX,TVI+1)-ZZ_STORE(RN%FUEL_SMIX_INDEX,TVI))
         ZZ_DIFF(TVI) = ZZ_STORE(RN%FUEL_SMIX_INDEX,TVI+1)-ZZ_STORE(RN%FUEL_SMIX_INDEX,TVI)
      ENDDO
      IF (SUM(TV) > 0.0_EB .AND. SUM(TV) >= ABS(2.5_EB*SUM(ZZ_DIFF)) .AND. ITER >= TV_ITER_MIN) EXIT INTEGRATION_LOOP
   ENDDO REACTION_LOOP_TV  
    
ENDDO INTEGRATION_LOOP

END SUBROUTINE ODE_RK2_RICHARDSON


SUBROUTINE EDC_MODEL(I,J,K,ZZ_GET,Q_OUT)
USE COMP_FUNCTIONS, ONLY: SHUTDOWN
USE PHYSICAL_FUNCTIONS, ONLY: LES_FILTER_WIDTH_FUNCTION
INTEGER,INTENT(IN):: I,J,K
REAL(EB),INTENT(OUT):: Q_OUT
REAL(EB),INTENT(INOUT) :: ZZ_GET(0:N_TRACKED_SPECIES)
REAL(EB) :: ZZ_0(0:N_TRACKED_SPECIES),DZZDT1_1(0:N_TRACKED_SPECIES),DZZDT1_2(0:N_TRACKED_SPECIES),DZZDT2_1(0:N_TRACKED_SPECIES), &
            DZZDT2_2(0:N_TRACKED_SPECIES),DZZDT4_1(0:N_TRACKED_SPECIES),DZZDT4_2(0:N_TRACKED_SPECIES),RATE_CONSTANT(1:N_REACTIONS),&
            RATE_CONSTANT2(1:N_REACTIONS),ERR_EST,ERR_TOL,Q_NR1(1:N_REACTIONS),Q_NR2(1:N_REACTIONS),Q_NR4(1:N_REACTIONS),Q_SUM1,&
            Q_SUM2,Q_SUM4,A1(0:N_TRACKED_SPECIES),A2(0:N_TRACKED_SPECIES),A4(0:N_TRACKED_SPECIES),DT_SUB,&
            DT_SUB_NEW,DT_ITER,ZZ_STORE(0:N_TRACKED_SPECIES,0:3),TV(0:2),ZZ_DIFF(0:2),Q1,Q2,Q4,Q_OUT2,&
            PHI(0:N_TRACKED_SPECIES),ZZ_GET_0(0:N_TRACKED_SPECIES),ZETA_0,ZETA,CELL_VOLUME,CELL_MASS,&
            DZZDT(0:N_TRACKED_SPECIES),SPEC_MIX_MASS(0:N_TRACKED_SPECIES,0:1),TOTAL_MIX_MASS(0:1),TAU_D,TAU_G,TAU_U,DELTA
REAL(EB), PARAMETER :: DT_SUB_MIN=1.E-10_EB,EPS=1.E-10_EB
INTEGER :: NR,NS,NSS,ITER,TVI,RICH_ITER
INTEGER, PARAMETER :: NODETS=20,SUB_DT1=1,SUB_DT2=2,SUB_DT4=4,TV_ITER_MIN=5,Q_ITER_MAX=10,RICH_ITER_MAX=100
TYPE(REACTION_TYPE),POINTER :: RN=>NULL()

IF (FIXED_MIX_TIME>0._EB) THEN
   MIX_TIME(I,J,K)=FIXED_MIX_TIME
ELSE
   DELTA = LES_FILTER_WIDTH_FUNCTION(DX(I),DY(J),DZ(K))
   TAU_D=0._EB
   DO NR =1,N_REACTIONS
      RN => REACTION(NR)
      TAU_D = MAX(TAU_D,D_Z(MIN(4999,NINT(TMP(I,J,K))),RN%FUEL_SMIX_INDEX))
   ENDDO
   TAU_D = DELTA**2/TAU_D
   IF (LES) THEN
      IF (TURB_MODEL==DEARDORFF) THEN
         TAU_U = C_DEARDORFF*SC*RHO(I,J,K)*DELTA**2/MU(I,J,K) ! turbulent mixing time scale
      ELSE
         TAU_U = DELTA/SQRT(2._EB*KSGS(I,J,K)+EPS) ! advective time scale
      ENDIF
      TAU_G = SQRT(2._EB*DELTA/(GRAV+EPS)) ! acceleration time scale
      MIX_TIME(I,J,K)=MAX(TAU_CHEM,MIN(TAU_D,TAU_U,TAU_G,TAU_FLAME)) ! Eq. 7, McDermott, McGrattan, Floyd
   ELSE
      MIX_TIME(I,J,K)= TAU_D
   ENDIF
ENDIF

ZZ_STORE(:,:) = 0._EB
Q_OUT = 0._EB
Q_OUT2 = 0._EB
ITER= 0
RICH_ITER = 0
DT_ITER = 0._EB
DT_SUB = DT 
DT_SUB_NEW = DT_SUB
ERR_TOL = RICHARDSON_ERROR_TOLERANCE

ZZ_GET_0 = ZZ_GET
ZETA_0 = INITIAL_UNMIXED_FRACTION
CELL_VOLUME = DX(I)*DY(J)*DZ(K)
CELL_MASS = RHO(I,J,K)*CELL_VOLUME
DO NS=0,1
   TOTAL_MIX_MASS(NS) = (1-ZETA_0)*CELL_MASS
   SPEC_MIX_MASS(:,NS) = ZZ_GET*TOTAL_MIX_MASS(NS)
ENDDO
ZETA = ZETA_0*EXP(-DT/MIX_TIME(I,J,K))
TOTAL_MIX_MASS(1) = (1-ZETA)*CELL_MASS
SPEC_MIX_MASS(:,1) = SPEC_MIX_MASS(:,0) + (ZETA_0-ZETA)*CELL_MASS*ZZ_GET_0
PHI = SPEC_MIX_MASS(:,1)/(TOTAL_MIX_MASS(1))

INTEGRATION_LOOP: DO WHILE (DT_ITER < DT)
   
   IF (SIMPLE_CHEM) THEN
      Q_NR1 = 0._EB
      ZZ_0 = MAX(0._EB,PHI)
      Q1 = Q_OUT2
      DZZDT1_1 = 0._EB
      RATE_CONSTANT = 0._EB
      REACTION_LOOP: DO NR = 1, N_REACTIONS
         RN => REACTION(NR)
         CALL COMPUTE_RATE_CONSTANT_EDCM(NR,RATE_CONSTANT(NR),ZZ_0,I,J,K,DT_SUB)
         DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
         IF (ANY(ZZ_0 + (DZZDT1_1+DZZDT)*DT_SUB < 0._EB)) THEN
            DO NSS=0,N_TRACKED_SPECIES
               IF (ZZ_0(NSS) + (DZZDT(NSS)+DZZDT1_1(NSS))*DT_SUB < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                  RATE_CONSTANT(NR) = MIN(RATE_CONSTANT(NR),ZZ_0(NSS)/&
                  ((ABS(RN%NU(NSS))*SPECIES_MIXTURE(NSS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW)*DT_SUB))
               ENDIF
            ENDDO
            DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
         ENDIF
         DZZDT1_1 = DZZDT1_1+DZZDT
      ENDDO REACTION_LOOP  
      IF (ALL(DZZDT1_1 < 0._EB)) EXIT INTEGRATION_LOOP
      A1 = ZZ_0 + DZZDT1_1*DT_SUB
      DO NR = 1,N_REACTIONS
         RN => REACTION(NR)
         Q_NR1(NR) = RATE_CONSTANT(NR)*DT_SUB*RN%HEAT_OF_COMBUSTION*TOTAL_MIX_MASS(1)/CELL_VOLUME
      ENDDO
      Q_SUM1 = SUM(Q_NR1)
      IF (Q1 + Q_SUM1 > Q_UPPER) THEN
         Q_OUT = Q_UPPER/DT
         PHI = (Q_UPPER/(Q1+Q_SUM1))*A1
         EXIT INTEGRATION_LOOP
      ELSE 
         Q1 = Q1+Q_SUM1
      ENDIF
   ELSE
   ERR_EST = 10._EB*ERR_TOL
   RICH_EX_LOOP: DO WHILE (ERR_EST > ERR_TOL)
      DT_SUB = DT_SUB_NEW
      IF (DT_ITER + DT_SUB > DT) THEN
         DT_SUB = DT - DT_ITER
      ENDIF
      !--------------------
      ! Calculate A1 term
      ! Time step = DT_SUB
      !--------------------
      ZZ_0 = MAX(0._EB,PHI)
      Q1 = Q_OUT2
      ODE_LOOP1: DO NS = 1, SUB_DT1
         DZZDT1_1 = 0._EB
         DZZDT1_2 = 0._EB
         Q_NR1 = 0._EB
         RATE_CONSTANT = 0._EB
         RATE_CONSTANT2 = 0._EB
         REACTION_LOOP1_1: DO NR = 1, N_REACTIONS   
            RN => REACTION(NR)
            CALL COMPUTE_RATE_CONSTANT_EDCM(NR,RATE_CONSTANT(NR),ZZ_0,I,J,K,DT_SUB)
            DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
            IF (ANY(ZZ_0 + (DZZDT1_1+DZZDT)*DT_SUB < 0._EB)) THEN
               DO NSS=0,N_TRACKED_SPECIES
                  IF (ZZ_0(NSS) + (DZZDT(NSS)+DZZDT1_1(NSS))*DT_SUB < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                     RATE_CONSTANT(NR) = MIN(RATE_CONSTANT(NR),ZZ_0(NSS)/&
                     ((ABS(RN%NU(NSS))*SPECIES_MIXTURE(NSS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW)*DT_SUB))
                  ENDIF
               ENDDO
               DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
            ENDIF
            DZZDT1_1 = DZZDT1_1+DZZDT
         ENDDO REACTION_LOOP1_1 
         A1 = ZZ_0 + DZZDT1_1*DT_SUB
                         
         REACTION_LOOP1_2: DO NR = 1, N_REACTIONS   
            RN => REACTION(NR)
            CALL COMPUTE_RATE_CONSTANT_EDCM(NR,RATE_CONSTANT2(NR),A1,I,J,K,DT_SUB)
            DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT2(NR)
            IF (ANY(A1 + (DZZDT1_2+DZZDT)*DT_SUB < 0._EB)) THEN
               DO NSS=0,N_TRACKED_SPECIES
                  IF (A1(NSS) + (DZZDT(NSS)+DZZDT1_2(NSS))*DT_SUB < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                     RATE_CONSTANT2(NR) = MIN(RATE_CONSTANT2(NR),A1(NSS)/&
                     ((ABS(RN%NU(NSS))*SPECIES_MIXTURE(NSS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW)*DT_SUB))
                  ENDIF
               ENDDO
               DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT2(NR)
            ENDIF
            DZZDT1_2 = DZZDT1_2+DZZDT
         END DO REACTION_LOOP1_2    
         A1 = A1 + DZZDT1_2*DT_SUB 
         A1 = 0.5_EB*(ZZ_0 + A1)
         IF (ABS(SUM(A1)-1._EB) < -TWO_EPSILON_EB) CALL SHUTDOWN('ERROR: Error in Reaction Species')          
         DO NR = 1,N_REACTIONS
            RN => REACTION(NR)
            Q_NR1(NR) = 0.5_EB*(RATE_CONSTANT(NR)+RATE_CONSTANT2(NR))*DT_SUB*RN%HEAT_OF_COMBUSTION*TOTAL_MIX_MASS(1)/CELL_VOLUME
         ENDDO
         Q_SUM1 = SUM(Q_NR1)
         IF (Q1 + Q_SUM1 > Q_UPPER) THEN
            Q1 = Q_UPPER
            A1 = (Q_UPPER/(Q1 + Q_SUM1))*A1
            EXIT ODE_LOOP1
         ELSE 
            Q1 = Q1+Q_SUM1     
         ENDIF                      
      ENDDO ODE_LOOP1     

      !--------------------
      ! Calculate A2 term
      ! Time step = DT_SUB/2
      !-------------------- 
      ZZ_0 = MAX(0._EB,PHI)
      Q2 = Q_OUT2
      ODE_LOOP2: DO NS = 1, SUB_DT2
         DZZDT2_1 = 0._EB
         DZZDT2_2 = 0._EB
         Q_NR2 = 0._EB
         RATE_CONSTANT = 0._EB
         RATE_CONSTANT2 = 0._EB
         REACTION_LOOP2_1: DO NR = 1, N_REACTIONS   
            RN => REACTION(NR)
            CALL COMPUTE_RATE_CONSTANT_EDCM(NR,RATE_CONSTANT(NR),ZZ_0,I,J,K,DT_SUB)
            DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
            IF (ANY(ZZ_0 + (DZZDT2_1+DZZDT)*(DT_SUB/2._EB) < 0._EB)) THEN
               DO NSS=0,N_TRACKED_SPECIES
                  IF (ZZ_0(NSS) + (DZZDT(NSS)+DZZDT2_1(NSS))*(DT_SUB/2._EB) < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                     RATE_CONSTANT(NR) = MIN(RATE_CONSTANT(NR),ZZ_0(NSS)/&
                     ((ABS(RN%NU(NSS))*SPECIES_MIXTURE(NSS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW)*(DT_SUB/2._EB)))
                  ENDIF
               ENDDO
               DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
            ENDIF
            DZZDT2_1 = DZZDT2_1+DZZDT
         END DO REACTION_LOOP2_1   
         A2 = ZZ_0 + DZZDT2_1*(DT_SUB/2._EB)
                  
         REACTION_LOOP2_2: DO NR = 1, N_REACTIONS   
            RN => REACTION(NR)
            CALL COMPUTE_RATE_CONSTANT_EDCM(NR,RATE_CONSTANT2(NR),A2,I,J,K,DT_SUB)
            DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT2(NR)
            IF (ANY(A2 + (DZZDT2_2+DZZDT)*(DT_SUB/2._EB) < 0._EB)) THEN
               DO NSS=0,N_TRACKED_SPECIES
                  IF (A2(NSS) + (DZZDT(NSS)+DZZDT2_2(NSS))*(DT_SUB/2._EB) < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                     RATE_CONSTANT2(NR) = MIN(RATE_CONSTANT2(NR),A2(NSS)/&
                     ((ABS(RN%NU(NSS))*SPECIES_MIXTURE(NSS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW)*(DT_SUB/2._EB)))
                  ENDIF
               ENDDO
               DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT2(NR)
            ENDIF
            DZZDT2_2 = DZZDT2_2+DZZDT
         END DO REACTION_LOOP2_2    
         A2 = A2 + DZZDT2_2*(DT_SUB/2._EB)
         A2 = 0.5_EB*(ZZ_0 + A2) 
         IF (ABS(SUM(A2)-1._EB) < -TWO_EPSILON_EB) CALL SHUTDOWN('ERROR: Error in Reaction Species')
         DO NR = 1,N_REACTIONS
            RN => REACTION(NR)
            Q_NR2(NR) = 0.5_EB*(RATE_CONSTANT(NR)+RATE_CONSTANT2(NR))*(DT_SUB/2._EB)*RN%HEAT_OF_COMBUSTION*TOTAL_MIX_MASS(1)&
            /CELL_VOLUME
         ENDDO
         Q_SUM2 = SUM(Q_NR2)
         IF (Q2 + Q_SUM2 > Q_UPPER) THEN
            Q2 = Q_UPPER
            A2 = (Q_UPPER/(Q2 + Q_SUM2))*A2
            EXIT ODE_LOOP2
         ELSE 
            Q2 = Q2+Q_SUM2     
         ENDIF                
         ZZ_0 = A2
      ENDDO ODE_LOOP2
     
      !--------------------
      ! Calculate A4 term
      ! Time step = DT_SUB/4
      !-------------------- 
      ZZ_0 = MAX(0._EB,PHI)
      Q4 = Q_OUT2
      ODE_LOOP4: DO NS = 1, SUB_DT4
         DZZDT4_1 = 0._EB
         DZZDT4_2 = 0._EB
         Q_NR4 = 0._EB
         RATE_CONSTANT = 0._EB
         RATE_CONSTANT2 = 0._EB
         REACTION_LOOP4_1: DO NR = 1, N_REACTIONS   
            RN => REACTION(NR)
            CALL COMPUTE_RATE_CONSTANT_EDCM(NR,RATE_CONSTANT(NR),ZZ_0,I,J,K,DT_SUB)
            DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
            IF (ANY(ZZ_0 + (DZZDT4_1+DZZDT)*(DT_SUB/4._EB) < 0._EB)) THEN
               DO NSS=0,N_TRACKED_SPECIES
                  IF (ZZ_0(NSS) + (DZZDT(NSS)+DZZDT4_1(NSS))*(DT_SUB/4._EB) < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                     RATE_CONSTANT(NR) = MIN(RATE_CONSTANT(NR),ZZ_0(NSS)/&
                     ((ABS(RN%NU(NSS))*SPECIES_MIXTURE(NSS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW)*(DT_SUB/4._EB)))
                  ENDIF
               ENDDO
               DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
            ENDIF
            DZZDT4_1 = DZZDT4_1+DZZDT
         END DO REACTION_LOOP4_1   
         A4 = ZZ_0 + DZZDT4_1*(DT_SUB/4._EB)

         REACTION_LOOP4_2: DO NR = 1, N_REACTIONS   
            RN => REACTION(NR)
            CALL COMPUTE_RATE_CONSTANT_EDCM(NR,RATE_CONSTANT2(NR),A4,I,J,K,DT_SUB)
            DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT2(NR)
            IF (ANY(A4 + (DZZDT4_2+DZZDT)*(DT_SUB/4._EB) < 0._EB)) THEN
               DO NSS=0,N_TRACKED_SPECIES
                  IF (A4(NSS) + (DZZDT(NSS)+DZZDT4_2(NSS))*(DT_SUB/4._EB) < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                     RATE_CONSTANT2(NR) = MIN(RATE_CONSTANT2(NR),A4(NSS)/&
                     ((ABS(RN%NU(NSS))*SPECIES_MIXTURE(NSS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW)*(DT_SUB/4._EB)))
                  ENDIF
               ENDDO
               DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT2(NR)
            ENDIF
            DZZDT4_2 = DZZDT4_2+DZZDT
         END DO REACTION_LOOP4_2    
         A4 = A4 + DZZDT4_2*(DT_SUB/4._EB)
         A4 = 0.5_EB*(ZZ_0 + A4) 
         IF (ABS(SUM(A4)-1._EB) < -TWO_EPSILON_EB) CALL SHUTDOWN('ERROR: Error A4 in Reaction Species')
         DO NR = 1,N_REACTIONS
            RN => REACTION(NR)
            Q_NR4(NR) = 0.5_EB*(RATE_CONSTANT(NR)+RATE_CONSTANT2(NR))*(DT_SUB/4._EB)*RN%HEAT_OF_COMBUSTION*TOTAL_MIX_MASS(1)&
            /CELL_VOLUME
         ENDDO
         Q_SUM4 = SUM(Q_NR4)
         IF (Q4 + Q_SUM4 > Q_UPPER) THEN
            Q_OUT = Q_UPPER/DT
            PHI = (Q_UPPER/(Q4 + Q_SUM4))*A4
            EXIT INTEGRATION_LOOP
         ELSE 
            Q4 = Q4+Q_SUM4     
         ENDIF            
         ZZ_0 = A4
      ENDDO ODE_LOOP4

      ! Species Error Analysis
      ERR_EST = MAXVAL(ABS((4._EB*A4-A2) - (4._EB*A2-A1)))/45._EB  ! Estimate Error
      IF (ERR_EST <= TWO_EPSILON_EB) THEN
         DT_SUB_NEW = DT
      ELSE
         DT_SUB_NEW = MAX(DT_SUB*(ERR_TOL/(ERR_EST))**(0.25_EB),DT_SUB_MIN) ! Determine New Time Step
      ENDIF
      
      IF (DT_SUB <= DT_SUB_MIN) EXIT RICH_EX_LOOP
      RICH_ITER = RICH_ITER+1
      IF (RICH_ITER >= RICH_ITER_MAX) EXIT RICH_EX_LOOP
   ENDDO RICH_EX_LOOP
   ENDIF
  
   DT_ITER = DT_ITER + DT_SUB
   ITER = ITER + 1
   MAX_CHEM_SUBIT = MAX(MAX_CHEM_SUBIT,ITER)
   IF (SIMPLE_CHEM) THEN 
      PHI = A1
      Q_OUT = Q1/DT
      Q_OUT2 = Q1
   ELSE   
      PHI = (4._EB*A4-A2)/3._EB
      Q_OUT = (4._EB*Q4-Q2)/3._EB/DT
      Q_OUT2 = (4._EB*Q4-Q2)/3._EB
   ENDIF
   
   !Total Variation Scheme
   DO NS = 0,N_TRACKED_SPECIES
      DO TVI = 0,2
         ZZ_STORE(NS,TVI)=ZZ_STORE(NS,TVI+1)
      ENDDO
      ZZ_STORE(NS,3) = (4._EB*A4(NS)-A2(NS))/3._EB  
   ENDDO
   
   REACTION_LOOP_TV: DO NR = 1, N_REACTIONS   
      RN => REACTION(NR)      
      IF (.NOT. RN%REVERSIBLE) CYCLE REACTION_LOOP_TV
      DO TVI = 0,2
         TV(TVI) = ABS(ZZ_STORE(RN%FUEL_SMIX_INDEX,TVI+1)-ZZ_STORE(RN%FUEL_SMIX_INDEX,TVI))
         ZZ_DIFF(TVI) = ZZ_STORE(RN%FUEL_SMIX_INDEX,TVI+1)-ZZ_STORE(RN%FUEL_SMIX_INDEX,TVI)
      ENDDO
      IF (SUM(TV) > 0.0_EB .AND. SUM(TV) >= ABS(2.5_EB*SUM(ZZ_DIFF)) .AND. ITER >= TV_ITER_MIN) EXIT INTEGRATION_LOOP
   ENDDO REACTION_LOOP_TV 
       
ENDDO INTEGRATION_LOOP
ZZ_GET =  ZETA*ZZ_GET_0 + (1-ZETA)*PHI

CONTAINS

REAL(EB) FUNCTION KSGS(I,J,K)
INTEGER, INTENT(IN) :: I,J,K
REAL(EB) :: EPSK

! ke dissipation rate, assumes production=dissipation
EPSK = MU(I,J,K)*STRAIN_RATE(I,J,K)**2/RHO(I,J,K)
KSGS = 2.25_EB*(EPSK*DELTA/PI)**TWTH  ! estimate of subgrid ke, from Kolmogorov spectrum

END FUNCTION KSGS

END SUBROUTINE EDC_MODEL


RECURSIVE SUBROUTINE COMPUTE_RATE_CONSTANT_EDCM(NR,RATE_CONSTANT,ZZ_GET,I,J,K,DT_SUB)
USE PHYSICAL_FUNCTIONS, ONLY : GET_MASS_FRACTION_ALL
REAL(EB), INTENT(IN) :: ZZ_GET(0:N_TRACKED_SPECIES),DT_SUB
INTEGER, INTENT(IN) :: NR,I,J,K
REAL(EB), INTENT(INOUT) :: RATE_CONSTANT
REAL(EB) :: YY_PRIMITIVE(1:N_SPECIES),ZZ_MIN=1.E-10_EB,RATE_CONSTANT_LIMIT
INTEGER :: NS
TYPE(REACTION_TYPE),POINTER :: RN=>NULL()
RN => REACTION(NR)

IF_SUPPRESSION: IF (SUPPRESSION) THEN
   ! Evaluate empirical extinction criteria
   IF(EXTINCTION(I,J,K,ZZ_GET)) THEN
      RATE_CONSTANT = 0._EB
      RETURN
   ENDIF
ENDIF IF_SUPPRESSION   

RATE_CONSTANT_LIMIT = 1.E10_EB
DO NS=0,N_TRACKED_SPECIES
   IF (RN%NU(NS)<0._EB) THEN
      RATE_CONSTANT_LIMIT = MIN(RATE_CONSTANT_LIMIT,ZZ_GET(NS)/DT_SUB)
   ENDIF
ENDDO
   
IF (RN%FAST_CHEMISTRY) THEN
   RATE_CONSTANT = RATE_CONSTANT_LIMIT
   RETURN
ENDIF
RATE_CONSTANT = 0._EB
CALL GET_MASS_FRACTION_ALL(ZZ_GET,YY_PRIMITIVE)
RATE_CONSTANT = RN%A*RHO(I,J,K)**RN%RHO_EXPONENT*EXP(-RN%E/(R0*TMP(I,J,K)))*TMP(I,J,K)**RN%N_T
IF (ALL(RN%N_S<-998._EB)) THEN
   DO NS=0,N_TRACKED_SPECIES
      IF(RN%NU(NS)<0._EB .AND. ZZ_GET(NS) < ZZ_MIN) THEN
         RATE_CONSTANT = 0._EB
         RETURN
      ENDIF            
   ENDDO
ELSE
   DO NS=1,N_SPECIES
      IF(ABS(RN%N_S(NS)) <= TWO_EPSILON_EB) CYCLE
      IF(RN%N_S(NS)>= -998._EB) THEN
         IF (YY_PRIMITIVE(NS) < ZZ_MIN) THEN
            RATE_CONSTANT = 0._EB
         ELSE
            RATE_CONSTANT = YY_PRIMITIVE(NS)**RN%N_S(NS)*RATE_CONSTANT 
         ENDIF
      ENDIF
   ENDDO               
ENDIF

CONTAINS

LOGICAL FUNCTION EXTINCTION(I,J,K,ZZ_IN)
!This routine determines if local extinction occurs for a mixing controlled reaction.
!This is determined as follows:
!1) Determine how much fuel can burn (DZ_FUEL) by finding the limiting reactant and expressing it in terms of fuel mass
!2) Remove that amount of fuel form the local mixture, everything else is "air"  
!   (i.e. if we are fuel rich, excess fuel acts as a diluent)
!3) Search to find the minimum reactant other than fuel.  
!   Using the reaction stoichiometry, determine how much "air" (DZ_AIR) is needed to burn the fuel.
!4) GET_AVERAGE_SPECIFIC_HEAT for the fuel and the "air" at the current temp and the critical flame temp
!5) Check to see if the heat released from burning DZ_FUEL can raise the current temperature of DZ_FUEL and DZ_AIR
!   above the critical flame temp.
USE PHYSICAL_FUNCTIONS,ONLY:GET_AVERAGE_SPECIFIC_HEAT
REAL(EB),INTENT(IN)::ZZ_IN(0:N_TRACKED_SPECIES)
REAL(EB):: DZ_AIR,DZ_FUEL,CPBAR_F_0,CPBAR_F_N,CPBAR_G_0,CPBAR_G_N,ZZ_GET(0:N_TRACKED_SPECIES)
INTEGER, INTENT(IN) :: I,J,K
INTEGER :: NS

EXTINCTION = .FALSE.
IF (TMP(I,J,K) < RN%AUTO_IGNITION_TEMPERATURE) THEN
   EXTINCTION = .TRUE.
ELSE
   DZ_FUEL = 1._EB
   DZ_AIR = 0._EB
   !Search reactants to find limiting reactant and express it as fuel mass.  This is the amount of fuel
   !that can burn
   DO NS = 0,N_TRACKED_SPECIES
      IF (RN%NU(NS)<-TWO_EPSILON_EB) &
         DZ_FUEL = MIN(DZ_FUEL,-ZZ_IN(NS)*SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW/(RN%NU(NS)*SPECIES_MIXTURE(NS)%MW))
   ENDDO
   !Get the specific heat for the fuel at the current and critical flame temperatures
   ZZ_GET = 0._EB
   ZZ_GET(RN%FUEL_SMIX_INDEX) = 1._EB
   CALL GET_AVERAGE_SPECIFIC_HEAT(ZZ_GET,CPBAR_F_0,TMP(I,J,K)) 
   CALL GET_AVERAGE_SPECIFIC_HEAT(ZZ_GET,CPBAR_F_N,RN%CRIT_FLAME_TMP)
   ZZ_GET = ZZ_IN
   !Remove the burnable fuel from the local mixture and renormalize.  The remainder is "air"
   ZZ_GET(RN%FUEL_SMIX_INDEX) = ZZ_GET(RN%FUEL_SMIX_INDEX) - DZ_FUEL
   ZZ_GET = ZZ_GET/SUM(ZZ_GET)     
   !Get the specific heat for the "air"
   CALL GET_AVERAGE_SPECIFIC_HEAT(ZZ_GET,CPBAR_G_0,TMP(I,J,K)) 
   CALL GET_AVERAGE_SPECIFIC_HEAT(ZZ_GET,CPBAR_G_N,RN%CRIT_FLAME_TMP) 
   !Loop over non-fuel reactants and find the mininum.  Determine how much "air" is needed to provide the limting reactant
   DO NS = 0,N_TRACKED_SPECIES   
            IF (RN%NU(NS)<-TWO_EPSILON_EB .AND. NS/=RN%FUEL_SMIX_INDEX) &
              DZ_AIR = MAX(DZ_AIR, -DZ_FUEL*RN%NU(NS)*SPECIES_MIXTURE(NS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW/ZZ_GET(NS))
   ENDDO
   !See if enough energy is released to raise the fuel and required "air" temperatures above the critical flame temp
   IF ( (DZ_FUEL*CPBAR_F_0 + DZ_AIR*CPBAR_G_0)*TMP(I,J,K) + DZ_FUEL*RN%HEAT_OF_COMBUSTION < &
         (DZ_FUEL*CPBAR_F_N + DZ_AIR*CPBAR_G_N)*RN%CRIT_FLAME_TMP) EXTINCTION = .TRUE.
ENDIF

END FUNCTION EXTINCTION

END SUBROUTINE COMPUTE_RATE_CONSTANT_EDCM


RECURSIVE SUBROUTINE COMPUTE_RATE_CONSTANT(NR,MODE,I_TS,Q_IN,RATE_CONSTANT,ZZ_GET,I,J,K)
USE PHYSICAL_FUNCTIONS, ONLY : GET_MASS_FRACTION_ALL,LES_FILTER_WIDTH_FUNCTION
REAL(EB), INTENT(IN) :: ZZ_GET(0:N_TRACKED_SPECIES),Q_IN
INTEGER, INTENT(IN) :: NR,I_TS,MODE,I,J,K
REAL(EB), INTENT(INOUT) :: RATE_CONSTANT
REAL(EB) :: YY_PRIMITIVE(1:N_SPECIES),Y_F_MIN=1.E-15_EB,ZZ_MIN=1.E-10_EB,YY_F_LIM,ZZ_REACTANT,ZZ_PRODUCT, &
            TAU_D,TAU_G,TAU_U,DELTA,RATE_CONSTANT_ED,RATE_CONSTANT_FR
REAL(EB), PARAMETER :: TAU_CHEM_FINITE_RATE = 1.E-7_EB
INTEGER :: NS
TYPE(REACTION_TYPE),POINTER :: RN=>NULL()
RN => REACTION(NR)

SELECT CASE (MODE)
   CASE(MIXED)
      IF (Q_IN > 0._EB .AND. RN%THRESHOLD_TEMP >= TMP(I,J,K)) THEN
         CALL COMPUTE_RATE_CONSTANT(NR,EDDY_DISSIPATION,I_TS,Q_IN,RATE_CONSTANT,ZZ_GET,I,J,K)
      ELSE
         CALL COMPUTE_RATE_CONSTANT(NR,FINITE_RATE,I_TS,Q_IN,RATE_CONSTANT,ZZ_GET,I,J,K)      
      ENDIF
   CASE(EDDY_DISSIPATION)
         IF_SUPPRESSION: IF (SUPPRESSION) THEN
            ! Evaluate empirical extinction criteria
            IF (I_TS==1) THEN
                IF(EXTINCTION(I,J,K,ZZ_GET)) THEN
                   RATE_CONSTANT = 0._EB
                   RETURN
                ENDIF
            !ELSE
            !   IF (RATE_CONSTANT <= TWO_EPSILON_EB) RETURN
            ENDIF
         ENDIF IF_SUPPRESSION

         FIXED_TIME: IF (FIXED_MIX_TIME>0._EB) THEN
            MIX_TIME(I,J,K)=FIXED_MIX_TIME   
               
         ELSE FIXED_TIME
            DELTA = LES_FILTER_WIDTH_FUNCTION(DX(I),DY(J),DZ(K))

            LES_IF: IF (LES) THEN
            
               TAU_D = D_Z(MIN(4999,NINT(TMP(I,J,K))),RN%FUEL_SMIX_INDEX)
               TAU_D = DELTA**2/TAU_D ! diffusive time scale 
            
               IF (TURB_MODEL==DEARDORFF) THEN
                  TAU_U = 0.1_EB*SC*RHO(I,J,K)*DELTA**2/MU(I,J,K) ! turbulent mixing time scale
               ELSE
                  TAU_U = DELTA/SQRT(2._EB*KSGS(I,J,K)+1.E-10_EB) ! advective time scale
               ENDIF
               
               TAU_G = SQRT(2._EB*DELTA/(GRAV+1.E-10_EB)) ! acceleration time scale
               
               MIX_TIME(I,J,K)=MAX(TAU_CHEM,MIN(TAU_D,TAU_U,TAU_G,TAU_FLAME)) ! Eq. 7, McDermott, McGrattan, Floyd

            ELSE LES_IF

               TAU_D = D_Z(MIN(4999,NINT(TMP(I,J,K))),RN%FUEL_SMIX_INDEX)
               TAU_D = DELTA**2/TAU_D
               MIX_TIME(I,J,K)= TAU_D

            ENDIF LES_IF
         ENDIF FIXED_TIME
         
         YY_F_LIM=1.E15_EB
         IF (N_REACTIONS > 1) THEN
            DO NS=0,N_TRACKED_SPECIES
               IF(RN%NU(NS) < -TWO_EPSILON_EB) THEN
                  IF (ZZ_GET(NS) < ZZ_MIN) THEN
                     RATE_CONSTANT = 0._EB
                     RETURN
                  ENDIF
                  YY_F_LIM = MIN(YY_F_LIM,&
                                 ZZ_GET(NS)*SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW/(ABS(RN%NU(NS))*SPECIES_MIXTURE(NS)%MW))
               ENDIF
            ENDDO
         ELSE
            ZZ_REACTANT = 0._EB
            ZZ_PRODUCT = 0._EB
            DO NS=0,N_TRACKED_SPECIES
               IF(RN%NU(NS) < -TWO_EPSILON_EB) THEN
                  IF (ZZ_GET(NS) < ZZ_MIN) THEN
                     RATE_CONSTANT = 0._EB
                     RETURN
                  ENDIF               
                  ZZ_REACTANT = ZZ_REACTANT - RN%NU(NS)*SPECIES_MIXTURE(NS)%MW
                  YY_F_LIM = MIN(YY_F_LIM,&
                                 ZZ_GET(NS)*SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW/(ABS(RN%NU(NS))*SPECIES_MIXTURE(NS)%MW))
               ELSEIF(RN%NU(NS)>TWO_EPSILON_EB ) THEN
                  ZZ_PRODUCT = ZZ_PRODUCT + ZZ_GET(NS)
               ENDIF
            ENDDO
            ZZ_PRODUCT = BETA_EDC*MAX(ZZ_PRODUCT*SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW/ZZ_REACTANT,Y_P_MIN_EDC)
            YY_F_LIM = MIN(YY_F_LIM,ZZ_PRODUCT)
         ENDIF
         YY_F_LIM = MAX(YY_F_LIM,Y_F_MIN)
         RATE_CONSTANT = YY_F_LIM/MIX_TIME(I,J,K)      
   CASE(FINITE_RATE)
      RATE_CONSTANT = 0._EB
      CALL GET_MASS_FRACTION_ALL(ZZ_GET,YY_PRIMITIVE)
      RATE_CONSTANT = RN%A*RHO(I,J,K)**RN%RHO_EXPONENT*EXP(-RN%E/(R0*TMP(I,J,K)))*TMP(I,J,K)**RN%N_T
      IF (ALL(RN%N_S<-998._EB)) THEN
         DO NS=0,N_TRACKED_SPECIES
            IF(RN%NU(NS)<0._EB .AND. ZZ_GET(NS) < ZZ_MIN) THEN
               RATE_CONSTANT = 0._EB
               RETURN
            ENDIF            
         ENDDO
      ELSE
         DO NS=1,N_SPECIES
            IF(ABS(RN%N_S(NS)) <= TWO_EPSILON_EB) CYCLE
            IF(RN%N_S(NS)>= -998._EB) THEN
               IF (YY_PRIMITIVE(NS) < ZZ_MIN) THEN
                  RATE_CONSTANT = 0._EB
               ELSE
                  RATE_CONSTANT = YY_PRIMITIVE(NS)**RN%N_S(NS)*RATE_CONSTANT 
               ENDIF
            ENDIF
         ENDDO    
      ENDIF   
   CASE(EDDY_DISSIPATION_CONCEPT)
      CALL COMPUTE_RATE_CONSTANT(NR,EDDY_DISSIPATION,I_TS,Q_IN,RATE_CONSTANT,ZZ_GET,I,J,K)
      RATE_CONSTANT_ED=RATE_CONSTANT
      CALL COMPUTE_RATE_CONSTANT(NR,FINITE_RATE,I_TS,Q_IN,RATE_CONSTANT,ZZ_GET,I,J,K)
      RATE_CONSTANT_FR=RATE_CONSTANT
      IF (ABS(RATE_CONSTANT_ED) < TWO_EPSILON_EB .AND. ABS(RATE_CONSTANT_FR) < TWO_EPSILON_EB) THEN
         RATE_CONSTANT=0.0_EB
      ELSE
         RATE_CONSTANT = (RATE_CONSTANT_ED*RATE_CONSTANT_FR)/(RATE_CONSTANT_ED+RATE_CONSTANT_FR)
      ENDIF     

END SELECT

CONTAINS

LOGICAL FUNCTION EXTINCTION(I,J,K,ZZ_IN)
!This routine determines if local extinction occurs for a mixing controlled reaction.
!This is determined as follows:
!1) Determine how much fuel can burn (DZ_FUEL) by finding the limiting reactant and expressing it in terms of fuel mass
!2) Remove that amount of fuel form the local mixture, everything else is "air"  
!   (i.e. if we are fuel rich, excess fuel acts as a diluent)
!3) Search to find the minimum reactant other than fuel.  
!   Using the reaction stoichiometry, determine how much "air" (DZ_AIR) is needed to burn the fuel.
!4) GET_AVERAGE_SPECIFIC_HEAT for the fuel and the "air" at the current temp and the critical flame temp
!5) Check to see if the heat released from burning DZ_FUEL can raise the current temperature of DZ_FUEL and DZ_AIR
!   above the critical flame temp.
USE PHYSICAL_FUNCTIONS,ONLY:GET_AVERAGE_SPECIFIC_HEAT
REAL(EB),INTENT(IN)::ZZ_IN(0:N_TRACKED_SPECIES)
REAL(EB):: DZ_AIR,DZ_FUEL,CPBAR_F_0,CPBAR_F_N,CPBAR_G_0,CPBAR_G_N,ZZ_GET(0:N_TRACKED_SPECIES)
INTEGER, INTENT(IN) :: I,J,K
INTEGER :: NS

EXTINCTION = .FALSE.
IF (TMP(I,J,K) < RN%AUTO_IGNITION_TEMPERATURE) THEN
   EXTINCTION = .TRUE.
ELSE
   DZ_FUEL = 1._EB
   DZ_AIR = 0._EB
   !Search reactants to find limiting reactant and express it as fuel mass.  This is the amount of fuel
   !that can burn
   DO NS = 0,N_TRACKED_SPECIES
      IF (RN%NU(NS)<-TWO_EPSILON_EB) &
         DZ_FUEL = MIN(DZ_FUEL,-ZZ_IN(NS)*SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW/(RN%NU(NS)*SPECIES_MIXTURE(NS)%MW))
   ENDDO
   !Get the specific heat for the fuel at the current and critical flame temperatures
   ZZ_GET = 0._EB
   ZZ_GET(RN%FUEL_SMIX_INDEX) = 1._EB
   CALL GET_AVERAGE_SPECIFIC_HEAT(ZZ_GET,CPBAR_F_0,TMP(I,J,K)) 
   CALL GET_AVERAGE_SPECIFIC_HEAT(ZZ_GET,CPBAR_F_N,RN%CRIT_FLAME_TMP)
   ZZ_GET = ZZ_IN

   !Remove the burnable fuel from the local mixture and renormalize.  The remainder is "air"
   ZZ_GET(RN%FUEL_SMIX_INDEX) = ZZ_GET(RN%FUEL_SMIX_INDEX) - DZ_FUEL
   ZZ_GET = ZZ_GET/SUM(ZZ_GET)      
  
   !Get the specific heat for the "air"
   CALL GET_AVERAGE_SPECIFIC_HEAT(ZZ_GET,CPBAR_G_0,TMP(I,J,K)) 
   CALL GET_AVERAGE_SPECIFIC_HEAT(ZZ_GET,CPBAR_G_N,RN%CRIT_FLAME_TMP) 
   !Loop over non-fuel reactants and find the mininum.  Determine how much "air" is needed to provide the limting reactant
   DO NS = 0,N_TRACKED_SPECIES   
            IF (RN%NU(NS)<-TWO_EPSILON_EB .AND. NS/=RN%FUEL_SMIX_INDEX) &
              DZ_AIR = MAX(DZ_AIR, -DZ_FUEL*RN%NU(NS)*SPECIES_MIXTURE(NS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW/ZZ_GET(NS))
   ENDDO
   !See if enough energy is released to raise the fuel and required "air" temperatures above the critical flame temp   
   IF ( (DZ_FUEL*CPBAR_F_0 + DZ_AIR*CPBAR_G_0)*TMP(I,J,K) + DZ_FUEL*RN%HEAT_OF_COMBUSTION < &
         (DZ_FUEL*CPBAR_F_N + DZ_AIR*CPBAR_G_N)*RN%CRIT_FLAME_TMP) EXTINCTION = .TRUE.

ENDIF

END FUNCTION EXTINCTION


REAL(EB) FUNCTION KSGS(I,J,K)
INTEGER, INTENT(IN) :: I,J,K
REAL(EB) :: EPSK

! ke dissipation rate, assumes production=dissipation
EPSK = MU(I,J,K)*STRAIN_RATE(I,J,K)**2/RHO(I,J,K)
KSGS = 2.25_EB*(EPSK*DELTA/PI)**TWTH  ! estimate of subgrid ke, from Kolmogorov spectrum

END FUNCTION KSGS

END SUBROUTINE COMPUTE_RATE_CONSTANT


!---------------------------------------------------------------------------------------------------------------------------
!---------------------------------------------------------------------------------------------------------------------------
! Stand alone EDCM -- EDC =.TRUE. on MISC
!---------------------------------------------------------------------------------------------------------------------------
!---------------------------------------------------------------------------------------------------------------------------
   
SUBROUTINE COMBUSTION_MODEL(I,J,K,ZZ_GET,Q_OUT)
USE COMP_FUNCTIONS, ONLY: SHUTDOWN
USE PHYSICAL_FUNCTIONS, ONLY: LES_FILTER_WIDTH_FUNCTION
INTEGER,INTENT(IN):: I,J,K
REAL(EB),INTENT(OUT):: Q_OUT
REAL(EB),INTENT(INOUT) :: ZZ_GET(0:N_TRACKED_SPECIES)
REAL(EB) :: ZZ_0(0:N_TRACKED_SPECIES),DZZDT1_1(0:N_TRACKED_SPECIES),DZZDT1_2(0:N_TRACKED_SPECIES),DZZDT2_1(0:N_TRACKED_SPECIES), &
            DZZDT2_2(0:N_TRACKED_SPECIES),DZZDT4_1(0:N_TRACKED_SPECIES),DZZDT4_2(0:N_TRACKED_SPECIES),RATE_CONSTANT(1:N_REACTIONS),&
            RATE_CONSTANT2(1:N_REACTIONS),ERR_EST,ERR_TOL,ZZ_TEMP(0:N_TRACKED_SPECIES),&
            A1(0:N_TRACKED_SPECIES),A2(0:N_TRACKED_SPECIES),A4(0:N_TRACKED_SPECIES),Q_SUM,Q_CALC,&
            DT_SUB,DT_SUB_NEW,DT_ITER,ZZ_STORE(0:N_TRACKED_SPECIES,0:3),TV(0:2),ZZ_DIFF(0:2),&
            PHI(0:N_TRACKED_SPECIES),ZZ_GET_0(0:N_TRACKED_SPECIES),ZETA_0,ZETA,ZETA1,CELL_VOLUME,CELL_MASS,&
            DZZDT(0:N_TRACKED_SPECIES),SPEC_MIX_MASS(0:N_TRACKED_SPECIES,0:1),TOTAL_MIX_MASS(0:1),TAU_D,TAU_G,TAU_U,DELTA
REAL(EB), PARAMETER :: DT_SUB_MIN=1.E-10_EB,ZZ_MIN=1.E-10_EB,EPS=1.E-10_EB
INTEGER :: NR,NS,NSS,ITER,TVI,RICH_ITER
INTEGER, PARAMETER :: NODETS=20,SUB_DT1=1,SUB_DT2=2,SUB_DT4=4,TV_ITER_MIN=5,Q_ITER_MAX=10,RICH_ITER_MAX=100
TYPE(REACTION_TYPE),POINTER :: RN=>NULL()

IF (FIXED_MIX_TIME>0._EB) THEN
   MIX_TIME(I,J,K)=FIXED_MIX_TIME
ELSE
   DELTA = LES_FILTER_WIDTH_FUNCTION(DX(I),DY(J),DZ(K))
   TAU_D=0._EB
   DO NR =1,N_REACTIONS
      RN => REACTION(NR)
      TAU_D = MAX(TAU_D,D_Z(MIN(4999,NINT(TMP(I,J,K))),RN%FUEL_SMIX_INDEX))
   ENDDO
   TAU_D = DELTA**2/TAU_D
   IF (LES) THEN
      IF (TURB_MODEL==DEARDORFF) THEN
         TAU_U = 0.1_EB*SC*RHO(I,J,K)*DELTA**2/MU(I,J,K) ! turbulent mixing time scale
      ELSE
         TAU_U = DELTA/SQRT(2._EB*KSGS(I,J,K)+1.E-10_EB) ! advective time scale
      ENDIF
      TAU_G = SQRT(2._EB*DELTA/(GRAV+1.E-10_EB)) ! acceleration time scale
      MIX_TIME(I,J,K)=MAX(TAU_CHEM,MIN(TAU_D,TAU_U,TAU_G,TAU_FLAME)) ! Eq. 7, McDermott, McGrattan, Floyd
   ELSE
      MIX_TIME(I,J,K)= TAU_D
   ENDIF
ENDIF 

ZZ_STORE(:,:) = 0._EB      
Q_OUT = 0._EB
Q_CALC = 0._EB
Q_SUM = 0._EB
ITER= 0
RICH_ITER = 0
DT_ITER = 0._EB
DT_SUB = DT 
DT_SUB_NEW = DT_SUB
ERR_TOL = RICHARDSON_ERROR_TOLERANCE

ZZ_GET_0 = ZZ_GET
ZZ_TEMP = ZZ_GET       
PHI = ZZ_GET_0
ZETA_0 = INITIAL_UNMIXED_FRACTION
ZETA = ZETA_0
ZETA1 = ZETA_0
CELL_VOLUME = DX(I)*DY(J)*DZ(K)
CELL_MASS = RHO(I,J,K)*CELL_VOLUME
DO NS=0,1
   TOTAL_MIX_MASS(NS) = (1-ZETA_0)*CELL_MASS
   SPEC_MIX_MASS(:,NS) = ZZ_GET*TOTAL_MIX_MASS(NS)
ENDDO

INTEGRATION_LOOP: DO WHILE (DT_ITER < DT)
   IF (COMBUSTION_ODE /= EDCM_RK2) THEN ! Explicit Euler
      ZETA1 = ZETA_0*EXP(-DT_SUB/MIX_TIME(I,J,K))
      TOTAL_MIX_MASS(1) = (1-ZETA1)*CELL_MASS  
      SPEC_MIX_MASS(:,1) = SPEC_MIX_MASS(:,0) + (ZETA_0-ZETA1)*CELL_MASS*ZZ_GET_0
      PHI = SPEC_MIX_MASS(:,1)/(TOTAL_MIX_MASS(1))

      ZZ_0 = MAX(0._EB,PHI)
      DZZDT1_1 = 0._EB
      RATE_CONSTANT = 0._EB
      REACTION_LOOP1: DO NR = 1, N_REACTIONS   
         RN => REACTION(NR)
         CALL COMPUTE_RATE_CONSTANT_EDCM1(NR,RATE_CONSTANT(NR),ZZ_0,I,J,K,DT_SUB,ITER)
         DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
         IF (ANY(ZZ_0 + (DZZDT1_1+DZZDT)*DT_SUB < 0._EB)) THEN
            DO NSS=0,N_TRACKED_SPECIES
               IF (ZZ_0(NSS) + (DZZDT(NSS)+DZZDT1_1(NSS))*DT_SUB < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                  RATE_CONSTANT(NR) = MIN(RATE_CONSTANT(NR),(ZZ_0(NSS)/DT_SUB)*&
                  (SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW/(ABS(RN%NU(NSS))*SPECIES_MIXTURE(NSS)%MW)))
               ENDIF
            ENDDO
            DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
         ENDIF
         DZZDT1_1 = DZZDT1_1+DZZDT
      ENDDO REACTION_LOOP1 
      A1 = ZZ_0 + DZZDT1_1*DT_SUB 
      IF (ALL(DZZDT1_1 < 0._EB)) EXIT INTEGRATION_LOOP    
   ELSE ! RK2 w/ Richardson
      ERR_EST = 10._EB*ERR_TOL
      SPEC_MIX_MASS(:,0) = PHI*TOTAL_MIX_MASS(1)
      ZETA = ZETA1
  
      RICH_EX_LOOP: DO WHILE (ERR_EST > ERR_TOL)    
         DT_SUB = DT_SUB_NEW
         IF (DT_ITER + DT_SUB > DT) THEN
            DT_SUB = DT - DT_ITER      
         ENDIF  
         ZETA1 = ZETA_0*EXP(-(DT_ITER+DT_SUB)/MIX_TIME(I,J,K))
         TOTAL_MIX_MASS(1) = (1-ZETA1)*CELL_MASS     
         SPEC_MIX_MASS(:,1) = SPEC_MIX_MASS(:,0) + (ZETA-ZETA1)*CELL_MASS*ZZ_GET_0
         PHI = SPEC_MIX_MASS(:,1)/(TOTAL_MIX_MASS(1))
         !--------------------
         ! Calculate A1 term
         ! Time step = DT_SUB
         !--------------------
         ZZ_0 = MAX(0._EB,PHI)
         ODE_LOOP1: DO NS = 1, SUB_DT1
            DZZDT1_1 = 0._EB
            DZZDT1_2 = 0._EB
            RATE_CONSTANT = 0._EB
            RATE_CONSTANT2 = 0._EB
            REACTION_LOOP1_1: DO NR = 1, N_REACTIONS   
               RN => REACTION(NR)
               CALL COMPUTE_RATE_CONSTANT_EDCM1(NR,RATE_CONSTANT(NR),ZZ_0,I,J,K,DT_SUB,ITER)
               DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
               IF (ANY(ZZ_0 + (DZZDT1_1+DZZDT)*DT_SUB < 0._EB)) THEN
                  DO NSS=0,N_TRACKED_SPECIES
                     IF (ZZ_0(NSS) + (DZZDT(NSS)+DZZDT1_1(NSS))*DT_SUB < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                        RATE_CONSTANT(NR) = MIN(RATE_CONSTANT(NR),ZZ_0(NSS)/&
                        ((ABS(RN%NU(NSS))*SPECIES_MIXTURE(NSS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW)*DT_SUB))
                     ENDIF
                  ENDDO
                  DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
               ENDIF
               DZZDT1_1 = DZZDT1_1+DZZDT
            ENDDO REACTION_LOOP1_1 
            A1 = ZZ_0 + DZZDT1_1*DT_SUB
                         
            REACTION_LOOP1_2: DO NR = 1, N_REACTIONS   
               RN => REACTION(NR)
               CALL COMPUTE_RATE_CONSTANT_EDCM1(NR,RATE_CONSTANT2(NR),A1,I,J,K,DT_SUB,ITER)
               DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT2(NR)
               IF (ANY(A1 + (DZZDT1_2+DZZDT)*DT_SUB < 0._EB)) THEN
                  DO NSS=0,N_TRACKED_SPECIES
                     IF (A1(NSS) + (DZZDT(NSS)+DZZDT1_2(NSS))*DT_SUB < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                        RATE_CONSTANT2(NR) = MIN(RATE_CONSTANT2(NR),A1(NSS)/&
                        ((ABS(RN%NU(NSS))*SPECIES_MIXTURE(NSS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW)*DT_SUB))
                     ENDIF
                  ENDDO
                  DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT2(NR)
               ENDIF
               DZZDT1_2 = DZZDT1_2+DZZDT
            ENDDO REACTION_LOOP1_2    
            A1 = A1 + DZZDT1_2*DT_SUB 
            A1 = 0.5_EB*(ZZ_0 + A1)
            IF (SUM(A1)-1._EB > TWO_EPSILON_EB) CALL SHUTDOWN('ERROR: Error in Reaction Species')                               
         ENDDO ODE_LOOP1     
         !--------------------
         ! Calculate A2 term
         ! Time step = DT_SUB/2
         !-------------------- 
         ZZ_0 = MAX(0._EB,PHI)
         ODE_LOOP2: DO NS = 1, SUB_DT2
            DZZDT2_1 = 0._EB
            DZZDT2_2 = 0._EB
            RATE_CONSTANT = 0._EB
            RATE_CONSTANT2 = 0._EB
            REACTION_LOOP2_1: DO NR = 1, N_REACTIONS   
               RN => REACTION(NR)
               CALL COMPUTE_RATE_CONSTANT_EDCM1(NR,RATE_CONSTANT(NR),ZZ_0,I,J,K,DT_SUB,ITER)
               DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
               IF (ANY(ZZ_0 + (DZZDT2_1+DZZDT)*(DT_SUB*0.5_EB) < 0._EB)) THEN
                  DO NSS=0,N_TRACKED_SPECIES
                     IF (ZZ_0(NSS) + (DZZDT(NSS)+DZZDT2_1(NSS))*(DT_SUB*0.5_EB) < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                        RATE_CONSTANT(NR) = MIN(RATE_CONSTANT(NR),ZZ_0(NSS)/&
                        ((ABS(RN%NU(NSS))*SPECIES_MIXTURE(NSS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW)*(DT_SUB*0.5_EB)))
                     ENDIF
                  ENDDO
                  DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
               ENDIF
               DZZDT2_1 = DZZDT2_1+DZZDT
            ENDDO REACTION_LOOP2_1   
            A2 = ZZ_0 + DZZDT2_1*(DT_SUB*0.5_EB)
                  
            REACTION_LOOP2_2: DO NR = 1, N_REACTIONS   
               RN => REACTION(NR)
               CALL COMPUTE_RATE_CONSTANT_EDCM1(NR,RATE_CONSTANT2(NR),A2,I,J,K,DT_SUB,ITER)
               DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT2(NR)
               IF (ANY(A2 + (DZZDT2_2+DZZDT)*(DT_SUB*0.5_EB) < 0._EB)) THEN
                  DO NSS=0,N_TRACKED_SPECIES
                     IF (A2(NSS) + (DZZDT(NSS)+DZZDT2_2(NSS))*(DT_SUB*0.5_EB) < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                        RATE_CONSTANT2(NR) = MIN(RATE_CONSTANT2(NR),A2(NSS)/&
                        ((ABS(RN%NU(NSS))*SPECIES_MIXTURE(NSS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW)*(DT_SUB*0.5_EB)))
                     ENDIF
                  ENDDO
                  DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT2(NR)
               ENDIF
               DZZDT2_2 = DZZDT2_2+DZZDT
            ENDDO REACTION_LOOP2_2    
            A2 = A2 + DZZDT2_2*(DT_SUB*0.5_EB)
            A2 = 0.5_EB*(ZZ_0 + A2) 
            IF (SUM(A2)-1._EB > TWO_EPSILON_EB) CALL SHUTDOWN('ERROR: Error in Reaction Species')               
            ZZ_0 = A2
         ENDDO ODE_LOOP2
     
         !--------------------
         ! Calculate A4 term  
         ! Time step = DT_SUB/4
         !-------------------- 
         ZZ_0 = MAX(0._EB,PHI)
         ODE_LOOP4: DO NS = 1, SUB_DT4
            DZZDT4_1 = 0._EB
            DZZDT4_2 = 0._EB
            RATE_CONSTANT = 0._EB
            RATE_CONSTANT2 = 0._EB
            REACTION_LOOP4_1: DO NR = 1, N_REACTIONS   
               RN => REACTION(NR)
               CALL COMPUTE_RATE_CONSTANT_EDCM1(NR,RATE_CONSTANT(NR),ZZ_0,I,J,K,DT_SUB,ITER)
               DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
               IF (ANY(ZZ_0 + (DZZDT4_1+DZZDT)*(DT_SUB*0.25_EB) < 0._EB)) THEN
                  DO NSS=0,N_TRACKED_SPECIES
                     IF (ZZ_0(NSS) + (DZZDT(NSS)+DZZDT4_1(NSS))*(DT_SUB*0.25_EB) < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                        RATE_CONSTANT(NR) = MIN(RATE_CONSTANT(NR),ZZ_0(NSS)/&
                        ((ABS(RN%NU(NSS))*SPECIES_MIXTURE(NSS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW)*(DT_SUB*0.25_EB)))
                     ENDIF
                  ENDDO
                  DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT(NR)
               ENDIF
               DZZDT4_1 = DZZDT4_1+DZZDT
            END DO REACTION_LOOP4_1   
            A4 = ZZ_0 + DZZDT4_1*(DT_SUB*0.25_EB)

            REACTION_LOOP4_2: DO NR = 1, N_REACTIONS   
               RN => REACTION(NR)
               CALL COMPUTE_RATE_CONSTANT_EDCM1(NR,RATE_CONSTANT2(NR),A4,I,J,K,DT_SUB,ITER)
               DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT2(NR)
               IF (ANY(A4 + (DZZDT4_2+DZZDT)*(DT_SUB*0.25_EB) < 0._EB)) THEN
                  DO NSS=0,N_TRACKED_SPECIES
                     IF (A4(NSS) + (DZZDT(NSS)+DZZDT4_2(NSS))*(DT_SUB*0.25_EB) < 0._EB .AND. ABS(DZZDT(NSS))>TWO_EPSILON_EB) THEN
                        RATE_CONSTANT2(NR) = MIN(RATE_CONSTANT2(NR),A4(NSS)/&
                        ((ABS(RN%NU(NSS))*SPECIES_MIXTURE(NSS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW)*(DT_SUB*0.25_EB)))
                     ENDIF
                  ENDDO
                  DZZDT = RN%NU*SPECIES_MIXTURE%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW*RATE_CONSTANT2(NR)
               ENDIF
               DZZDT4_2 = DZZDT4_2+DZZDT
            ENDDO REACTION_LOOP4_2    
            A4 = A4 + DZZDT4_2*(DT_SUB*0.25_EB)
            A4 = 0.5_EB*(ZZ_0 + A4) 
            IF (SUM(A4)-1._EB > TWO_EPSILON_EB) CALL SHUTDOWN('ERROR: Error A4 in Reaction Species')             
            ZZ_0 = A4
         ENDDO ODE_LOOP4

         ! Species Error Analysis
         ERR_EST = MAXVAL(ABS((4._EB*A4-A2) - (4._EB*A2-A1)))/45._EB  ! Estimate Error
         IF (ERR_EST <= TWO_EPSILON_EB) THEN
            DT_SUB_NEW = DT
         ELSE
            DT_SUB_NEW = MAX(DT_SUB*(ERR_TOL/(ERR_EST))**(0.25_EB),DT_SUB_MIN) ! Determine New Time Step
         ENDIF
      
         IF (DT_SUB <= DT_SUB_MIN) EXIT RICH_EX_LOOP
         RICH_ITER = RICH_ITER+1
         IF (RICH_ITER >= RICH_ITER_MAX) EXIT RICH_EX_LOOP
      ENDDO RICH_EX_LOOP 
   ENDIF
  
   DT_ITER = DT_ITER + DT_SUB
   ITER = ITER + 1
   MAX_CHEM_SUBIT = MAX(MAX_CHEM_SUBIT,ITER)
   IF (COMBUSTION_ODE /= EDCM_RK2) THEN 
      PHI = A1
   ELSE   
      PHI = (4._EB*A4-A2)/3._EB
   ENDIF
   ZZ_GET =  ZETA1*ZZ_GET_0 + (1-ZETA1)*PHI

   ! Heat Release
   Q_SUM = 0._EB
   IF (MAXVAL(ABS(ZZ_GET-ZZ_TEMP)) > ZZ_MIN) THEN
      DO NSS = 0,N_TRACKED_SPECIES
         Q_SUM = Q_SUM - SPECIES_MIXTURE(NSS)%H_F*RHO(I,J,K)*(ZZ_GET(NSS)-ZZ_TEMP(NSS))
      ENDDO
   ENDIF
   IF (Q_CALC + Q_SUM > Q_UPPER*DT_ITER) THEN
      Q_OUT = Q_UPPER
      ZZ_GET = ZZ_TEMP + (Q_UPPER*DT_ITER/(Q_CALC + Q_SUM))*(ZZ_GET-ZZ_TEMP)
      EXIT INTEGRATION_LOOP
   ELSE 
      Q_CALC = Q_CALC+Q_SUM
      Q_OUT = Q_CALC/DT 
   ENDIF 

   !Total Variation Scheme
   IF (N_REACTIONS > 1) THEN
      DO NS = 0,N_TRACKED_SPECIES
         DO TVI = 0,2
            ZZ_STORE(NS,TVI)=ZZ_STORE(NS,TVI+1)
         ENDDO
         ZZ_STORE(NS,3) = (4._EB*A4(NS)-A2(NS))/3._EB  
      ENDDO
   
      REACTION_LOOP_TV: DO NR = 1, N_REACTIONS   
         RN => REACTION(NR)      
         DO TVI = 0,2
            TV(TVI) = ABS(ZZ_STORE(RN%FUEL_SMIX_INDEX,TVI+1)-ZZ_STORE(RN%FUEL_SMIX_INDEX,TVI))
            ZZ_DIFF(TVI) = ZZ_STORE(RN%FUEL_SMIX_INDEX,TVI+1)-ZZ_STORE(RN%FUEL_SMIX_INDEX,TVI)
         ENDDO
         IF (SUM(TV) > 0.0_EB .AND. SUM(TV) >= ABS(2.5_EB*SUM(ZZ_DIFF)) .AND. ITER >= TV_ITER_MIN) EXIT INTEGRATION_LOOP
      ENDDO REACTION_LOOP_TV  
   ENDIF
   ZZ_TEMP = ZZ_GET    
ENDDO INTEGRATION_LOOP

CONTAINS

REAL(EB) FUNCTION KSGS(I,J,K)
INTEGER, INTENT(IN) :: I,J,K
REAL(EB) :: EPSK
! ke dissipation rate, assumes production=dissipation
EPSK = MU(I,J,K)*STRAIN_RATE(I,J,K)**2/RHO(I,J,K)
KSGS = 2.25_EB*(EPSK*DELTA/PI)**TWTH  ! estimate of subgrid ke, from Kolmogorov spectrum
END FUNCTION KSGS

END SUBROUTINE COMBUSTION_MODEL

RECURSIVE SUBROUTINE COMPUTE_RATE_CONSTANT_EDCM1(NR,RATE_CONSTANT,PHI_IN,I,J,K,DT_SUB,ITER)
USE PHYSICAL_FUNCTIONS, ONLY : GET_MASS_FRACTION_ALL
REAL(EB), INTENT(IN) :: PHI_IN(0:N_TRACKED_SPECIES),DT_SUB
INTEGER, INTENT(IN) :: NR,I,J,K,ITER
REAL(EB), INTENT(INOUT) :: RATE_CONSTANT
REAL(EB) :: YY_PRIMITIVE(1:N_SPECIES),ZZ_MIN=1.E-10_EB,RATE_CONSTANT_LIMIT,DZ_FUEL,DZ_F(1:N_REACTIONS),DF_R(1:N_REACTIONS),&
            MASS_OX,MASS_OX_STOICH
INTEGER :: NS,NRR
TYPE(REACTION_TYPE),POINTER :: RN=>NULL(),RNN=>NULL()
RN => REACTION(NR)

IF_SUPPRESSION: IF (SUPPRESSION) THEN
   IF (ITER==0) THEN
      SELECT CASE (EXTINCT_MOD)
         CASE(EXTINCTION_1)
            IF(EXTINCT_1(I,J,K,PHI_IN)) THEN
               RATE_CONSTANT = 0._EB
               RETURN
            ENDIF
         CASE(EXTINCTION_2)
            IF(EXTINCT_2(I,J,K,PHI_IN)) THEN
               RATE_CONSTANT = 0._EB
               RETURN
            ENDIF
      END SELECT       
   ENDIF
ENDIF IF_SUPPRESSION

RATE_CONSTANT_LIMIT = 1.E10_EB
MASS_OX = 0._EB
MASS_OX_STOICH = 0._EB
IF (N_REACTIONS > 1) THEN 
   DO NS=0,N_TRACKED_SPECIES
      IF (RN%NU(NS)<-TWO_EPSILON_EB .AND. NS/=RN%FUEL_SMIX_INDEX) MASS_OX=PHI_IN(NS)
   ENDDO
   DO NRR=1,N_REACTIONS ! Calculate total Oxidizer needed to combust all fuel
      RNN => REACTION(NRR)
      IF (.NOT. RNN%FAST_CHEMISTRY) CYCLE
      DO NS=0,N_TRACKED_SPECIES
         IF (RN%NU(NS)<-TWO_EPSILON_EB .AND. NS/=RN%FUEL_SMIX_INDEX) &
            MASS_OX_STOICH = MASS_OX_STOICH + ABS(PHI_IN(RNN%FUEL_SMIX_INDEX)*RNN%NU(NS)*SPECIES_MIXTURE(NS)%MW/&
            SPECIES_MIXTURE(RNN%FUEL_SMIX_INDEX)%MW)
      ENDDO
   ENDDO
ENDIF
IF (RN%FAST_CHEMISTRY) THEN
   IF (MASS_OX_STOICH > MASS_OX .AND. N_REACTIONS > 1) THEN ! Global oxygen limited fast chemistry reactions  
      DZ_F(:) = 1._EB    
      DO NRR=1,N_REACTIONS
         RNN => REACTION(NRR)
         DO NS=0,N_TRACKED_SPECIES
            IF (RNN%NU(NS)<-TWO_EPSILON_EB) & 
            DZ_F(NRR) = MIN(DZ_F(NRR),-PHI_IN(NS)*SPECIES_MIXTURE(RNN%FUEL_SMIX_INDEX)%MW/(RNN%NU(NS)*SPECIES_MIXTURE(NS)%MW))
         ENDDO
      ENDDO 
      DO NRR=1,N_REACTIONS
         RNN => REACTION(NRR)
         DF_R(NRR) = DZ_F(NRR)/SUM(DZ_F)
      ENDDO
      RATE_CONSTANT_LIMIT = DZ_F(NR)*DF_R(NR)/DT_SUB    
   ELSE ! Fuel limited fast chemistry reaction
      DZ_FUEL = 1._EB
      DO NS=0,N_TRACKED_SPECIES
         IF (RN%NU(NS)<-TWO_EPSILON_EB) &   
         DZ_FUEL = MIN(DZ_FUEL,-PHI_IN(NS)*SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW/(RN%NU(NS)*SPECIES_MIXTURE(NS)%MW))
      ENDDO
      RATE_CONSTANT_LIMIT = DZ_FUEL/DT_SUB
   ENDIF
   RATE_CONSTANT = RATE_CONSTANT_LIMIT
   RETURN
ENDIF

RATE_CONSTANT = 0._EB
CALL GET_MASS_FRACTION_ALL(PHI_IN,YY_PRIMITIVE)
RATE_CONSTANT = RN%A*RHO(I,J,K)**RN%RHO_EXPONENT*EXP(-RN%E/(R0*TMP(I,J,K)))*TMP(I,J,K)**RN%N_T
IF (ALL(RN%N_S<-998._EB)) THEN
   DO NS=0,N_TRACKED_SPECIES
      IF(RN%NU(NS)<0._EB .AND. PHI_IN(NS) < ZZ_MIN) THEN
         RATE_CONSTANT = 0._EB
         RETURN
      ENDIF            
   ENDDO
ELSE
   DO NS=1,N_SPECIES
      IF(ABS(RN%N_S(NS)) <= TWO_EPSILON_EB) CYCLE
      IF(RN%N_S(NS)>= -998._EB) THEN
         IF (YY_PRIMITIVE(NS) < ZZ_MIN) THEN
            RATE_CONSTANT = 0._EB
         ELSE
            RATE_CONSTANT = YY_PRIMITIVE(NS)**RN%N_S(NS)*RATE_CONSTANT 
         ENDIF
      ENDIF
   ENDDO               
ENDIF
RETURN

CONTAINS

LOGICAL FUNCTION EXTINCT_1(I,J,K,ZZ_IN)
USE PHYSICAL_FUNCTIONS,ONLY:GET_AVERAGE_SPECIFIC_HEAT
REAL(EB),INTENT(IN)::ZZ_IN(0:N_TRACKED_SPECIES)
REAL(EB):: Y_O2,Y_O2_CRIT,CPBAR
INTEGER, INTENT(IN) :: I,J,K
INTEGER :: NS

EXTINCT_1 = .FALSE.
IF (TMP(I,J,K) < RN%AUTO_IGNITION_TEMPERATURE) THEN
   EXTINCT_1 = .TRUE.
ELSE
   CALL GET_AVERAGE_SPECIFIC_HEAT(ZZ_IN,CPBAR,TMP(I,J,K)) 

   DO NS = 0,N_TRACKED_SPECIES
      IF (RN%NU(NS)<-TWO_EPSILON_EB .AND. NS/=RN%FUEL_SMIX_INDEX) THEN
         Y_O2 = ZZ_IN(NS)    
      ENDIF  
   ENDDO 
   Y_O2_CRIT = CPBAR*(RN%CRIT_FLAME_TMP-TMP(I,J,K))/RN%EPUMO2 
   IF (Y_O2 < Y_O2_CRIT) EXTINCT_1 = .TRUE.
 
ENDIF
END FUNCTION EXTINCT_1

LOGICAL FUNCTION EXTINCT_2(I,J,K,PHI_IN)
USE PHYSICAL_FUNCTIONS,ONLY:GET_AVERAGE_SPECIFIC_HEAT
REAL(EB),INTENT(IN)::PHI_IN(0:N_TRACKED_SPECIES)
REAL(EB):: DZ_AIR,DZ_FUEL,CPBAR_F_0,CPBAR_F_N,CPBAR_G_0,CPBAR_G_N,ZZ_GET(0:N_TRACKED_SPECIES)
INTEGER, INTENT(IN) :: I,J,K
INTEGER :: NS

EXTINCT_2 = .FALSE.
IF (TMP(I,J,K) < RN%AUTO_IGNITION_TEMPERATURE) THEN
   EXTINCT_2 = .TRUE.
ELSE
   DZ_FUEL = 1._EB
   DZ_AIR = 0._EB
   !Search reactants to find limiting reactant and express it as fuel mass.  This is the amount of fuel
   !that can burn
   DO NS = 0,N_TRACKED_SPECIES
      IF (RN%NU(NS)<-TWO_EPSILON_EB) &
         DZ_FUEL = MIN(DZ_FUEL,-PHI_IN(NS)*SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW/(RN%NU(NS)*SPECIES_MIXTURE(NS)%MW))
   ENDDO
   !Get the specific heat for the fuel at the current and critical flame temperatures
   ZZ_GET = 0._EB
   ZZ_GET(RN%FUEL_SMIX_INDEX) = 1._EB
   CALL GET_AVERAGE_SPECIFIC_HEAT(ZZ_GET,CPBAR_F_0,TMP(I,J,K)) 
   CALL GET_AVERAGE_SPECIFIC_HEAT(ZZ_GET,CPBAR_F_N,RN%CRIT_FLAME_TMP)
   ZZ_GET = PHI_IN

   !Remove the burnable fuel from the local mixture and renormalize.  The remainder is "air"
   ZZ_GET(RN%FUEL_SMIX_INDEX) = ZZ_GET(RN%FUEL_SMIX_INDEX) - DZ_FUEL
   ZZ_GET = ZZ_GET/SUM(ZZ_GET)      
  
   !Get the specific heat for the "air"
   CALL GET_AVERAGE_SPECIFIC_HEAT(ZZ_GET,CPBAR_G_0,TMP(I,J,K)) 
   CALL GET_AVERAGE_SPECIFIC_HEAT(ZZ_GET,CPBAR_G_N,RN%CRIT_FLAME_TMP) 
   !Loop over non-fuel reactants and find the mininum.  Determine how much "air" is needed to provide the limting reactant
   DO NS = 0,N_TRACKED_SPECIES   
            IF (RN%NU(NS)<-TWO_EPSILON_EB .AND. NS/=RN%FUEL_SMIX_INDEX) &
              DZ_AIR = MAX(DZ_AIR, -DZ_FUEL*RN%NU(NS)*SPECIES_MIXTURE(NS)%MW/SPECIES_MIXTURE(RN%FUEL_SMIX_INDEX)%MW/ZZ_GET(NS))
   ENDDO
   !See if enough energy is released to raise the fuel and required "air" temperatures above the critical flame temp   
   IF ( (DZ_FUEL*CPBAR_F_0 + DZ_AIR*CPBAR_G_0)*TMP(I,J,K) + DZ_FUEL*RN%HEAT_OF_COMBUSTION < &
         (DZ_FUEL*CPBAR_F_N + DZ_AIR*CPBAR_G_N)*RN%CRIT_FLAME_TMP) EXTINCT_2 = .TRUE.

ENDIF
END FUNCTION EXTINCT_2

END SUBROUTINE COMPUTE_RATE_CONSTANT_EDCM1

SUBROUTINE GET_REV_fire(MODULE_REV,MODULE_DATE)
INTEGER,INTENT(INOUT) :: MODULE_REV
CHARACTER(255),INTENT(INOUT) :: MODULE_DATE

WRITE(MODULE_DATE,'(A)') firerev(INDEX(firerev,':')+2:LEN_TRIM(firerev)-2)
READ (MODULE_DATE,'(I5)') MODULE_REV
WRITE(MODULE_DATE,'(A)') firedate

END SUBROUTINE GET_REV_fire
 
END MODULE FIRE

